;===========================================================
;파일명: TITLE.ERB
;게임 구동시 타이틀 처리
;===========================================================
;함수명: SYSTEM_TITLE
;SYSTEM CALL 게임 구동시 자동. 없으면 표준 타이틀.
;GLOBAL 변수는 RESETDATA나 LOADDATA에 의해 초기화・덧쓰기되지 않는다.
;===========================================================
@SYSTEM_TITLE
#DIMS TITLE_HTML

LOADGLOBAL
RESULT = GLOBAL
SAVEGLOBAL

$TITLE_SELECT
;디버그 모드가 아닐때 타이틀 화상을 표시함
[IF_NDEBUG]
    TITLE_HTML = <p align='center'>
    TITLE_HTML += TITLE_PICTURE()
	HTML_PRINT TITLE_HTML
[ENDIF]

;타이틀 표시
DRAWLINEFORM =

ALIGNMENT CENTER
PRINTL
PRINTFORML %GAMEBASE_TITLE%
PRINTL
PRINTFORML %eraLAO_VER()%
PRINTFORML %GAMEBASE_AUTHOR%
PRINTL
PRINTFORML (%GAMEBASE_YEAR%)
PRINTL
PRINTL
PRINTL
PRINTFORML %GAMEBASE_INFO%
ALIGNMENT LEFT

DRAWLINEFORM =

;선택지 표시._Rename.csv로 지정한 값을 읽을 수 없기에 여기서 지정할 필요가 있다.
PRINTL [0] 처음부터 시작한다
PRINTL [1] 로드해 시작한다
DEBUGPRINTFORML [777] DEBUG
$TITLE_INPUT
INPUT
SELECTCASE RESULT
    CASE 0
        DRAWLINE
        RESETDATA
        ;ADDDEFCHARA는 eramaker의 동작을 재현 하기 위해서 존재하는 전용의 함수입니다
        ;다른 장면에서는 ADDCHARA, ADDSPCHARA등을 사용하는 것을 권합니다
        ADDDEFCHARA
        BEGIN FIRST
        RETURN
    CASE 1
        CALL LOADGAME_EX
        CLEARLINE 5000
        GOTO TITLE_SELECT
        ;LOADGAME_EX로 LOAD를 실시하지 못하고 돌아왔을 경우, 다시 한 번 더 선택한다.
    CASE 777
        [SKIPSTART]
        [IF_NDEBUG]
            REUSELASTLINE 올바른 값이 아닙니다.
            GOTO TITLE_INPUT
        [ENDIF]
        [SKIPEND]
        RESETDATA
        ADDDEFCHARA
        CALL DEBUG
        ;TODO 실제 릴리즈시 처리 요망
    CASEELSE
        CLEARLINE 1
        REUSELASTLINE 올바른 값이 아닙니다
        GOTO TITLE_INPUT
ENDSELECT

;SYSTEM_TITLE중에서 RETURN 하면 다음에 실행하는 행이 없어져, 에러가 된다.
;반드시 BEGIN 명령이나 LOADGAME를 실시하는 것.

;===========================================================
;함수명: TITLE_PICTURE
;HTML 기반 타이틀 화상 처리용 HTML 문자열 생성
;===========================================================
@TITLE_PICTURE
#FUNCTIONS
#DIMS TITLE_HTML
#DIMS TITLE_NAME

SELECTCASE RAND:6
    CASE 0
        TITLE_NAME = TITLE1
    CASE 1
        TITLE_NAME = TITLE2
    CASE 2
        TITLE_NAME = TITLE3
    CASE 3
        TITLE_NAME = TITLE4
    CASE 4
        TITLE_NAME = TITLE5
    CASE 5
        TITLE_NAME = TITLE6
ENDSELECT

TITLE_HTML = <img src='%TITLE_NAME%' height='3000' ypos='-3100'>
RETURNF TITLE_HTML
