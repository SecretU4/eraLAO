;========================================================================
;함수명: EVENTTURNEND
;마지막 수정: v0.9
;SYSYEM_CALL, BEGIN TURNEND 호출시
;시간대/날짜 변경 처리
;구현 방식상 ENDING 함수와 티키타카 가능
;========================================================================
@EVENTTURNEND
#DIM DYNAMIC 주회
#DIM DYNAMIC 지휘lv증가
#DIM DYNAMIC 총수입, 5
;지출, 수입 모두 양수값 저장이니 계산시 연산자의 올바른 사용 필수
;0 = 총합/1 = 유지비/2 = 출격수입/3,4,5 예비

SIF CFLAG:MASTER:엔딩플래그
    CALL ENDING(-1)

IF TIME == 0 ;낮 > 밤
    TIME = 1

    ;기본 아이템 재고 추가
    CALL SET_ITEM_SALES(2, 50, 10)

    ;낮 > 밤 캐릭터 변동 체크 ;원본 uptime1 대응
    CALL TURNEND_DAY_to_NIGHT

ELSEIF TIME == 1 ;밤 > 낮
    TIME = 0
    ;원본 날짜변경/etc 대응
    CALL ENDING_DAYEND_FLAG
    DAY ++
    CALL DAYEND_SORTIE
    총수입:2 = RESULT

    ;원본 uptime2 대응
    ;유지비 처리 및 메시지에 필요한 데이터 수집
    총수입:1 = TURNEND_NIGHT_to_DAY_PRE()

    ;캐릭터 스텟 및 능력 변동사항 처리
    FOR 주회, 1, CHARANUM

        ;미보유 캐릭터 제외
        SIF ITEM:(ITEM_BIO(NO:주회)) < 1
            CONTINUE

        ;본인이 정신붕괴라면 통과
        SIF TFLAG:정신붕괴인구 && TALENT:주회:정신쇠약 != 2
            CFLAG:주회:공포 += 2
        ;조건에 맞으면서 본인이 대상이 아닌 경우
        IF TALENT:주회:얀데레 && 주회 != TARGET
            CFLAG:주회:스트레스 += 20
            TFLAG:얀데레질투인구 ++
        ENDIF
        IF TALENT:주회:긍정도 == -1 && 주회 != TARGET ;비관적
            CFLAG:주회:스트레스 += 10
            TFLAG:비관적자학인구 ++
        ENDIF

        CALL GET_Annoyed(주회)
        IF RESULT == 1
            TFLAG:짜증획득인구 ++
        ELSEIF RESULT == -1
            TFLAG:짜증소멸인구 --
        ENDIF

        CALL GET_WeakMind(주회)
        SIF RESULT == 1
            TFLAG:정신쇠약획득인구 ++
        
        CALL GET_MindBreak(주회)
        SIF RESULT == 1
            TFLAG:정신붕괴획득인구 ++

        ;캐릭터별 특수 플래그 처리 (CFLAG:etc)
        CALL TURNEND_NIGHT_to_DAY_etc(주회)

        ;날짜 변동에 따른 스트레스, 공포 감소 처리
        CFLAG:주회:스트레스 -= 10
        CFLAG:주회:공포 -= 2
        CFLAG:주회:스트레스 = MAX(CFLAG:주회:스트레스, 0)
        CFLAG:주회:공포 = MAX(CFLAG:주회:공포, 0)
    NEXT

    ;비용 정리
    ;출격 수입은 @DAYEND_SORTIE에서 처리됨
    MONEY:0 -= 총수입:1 ;유지비 처리

    ;총수입:0은 기록 및 디버그용
    총수입 = 총수입:2 - 총수입:1

    ;기본 아이템 재고 보충 처리
    ;약혼반지는 하나만
    CALL SET_EX_ITEM_SALES(1, 1)
    CALL SET_ITEM_SALES(2, 50, 20)
    ;방추가 보충처리
    CALL SET_ITEM_SALES(203, 203, 1)

    ;MASTER의 전투레벨처리
    CALL BattleLV_UP(MASTER)
    지휘lv증가 = RESULT

    ;속박상태가 아니라면 메시지 표시
    IF !FLAG:속박상태
        SIF TFLAG:전투레벨변동인구
            PRINTL 전투레벨이 상승한 전투원이 있습니다.
        SIF TFLAG:정신붕괴인구
            PRINTL 보유중인 바이오로이드 중에 [정신붕괴]를 소지한 인원이 있어, 모든 바이오로이드의 공포가 소량 상승하였습니다.
        SIF TFLAG:얀데레질투인구
            PRINTL [얀데레]를 소지한 인원이 질투로 인해 스트레스가 소량 상승하였습니다.
        SIF TFLAG:비관적자학인구
            PRINTL [비관적]을 소지한 인원이 자학으로 인해 스트레스가 소량 상승하였습니다.
        SIF TFLAG:짜증획득인구
            PRINTL 바이오로이드 중 누군가의 짜증이 난 것 같습니다.
        SIF TFLAG:짜증소멸인구
            PRINTL 바이오로이드 중 누군가의 짜증이 풀린 것 같습니다.
        SIF TFLAG:정신쇠약획득인구
            PRINTL  바이오로이드 중 누군가의 정신이 쇠약해진 것 같습니다.
        SIF TFLAG:정신붕괴획득인구
            PRINTL  바이오로이드 중 누군가의 마음이 부숴진 것 같습니다.
        PRINTFORMW 보유한 전투원들의 유지비용으로 %MONEYSTR(총수입:1)%이 소모되었습니다.
        SIF GROUPMATCH(지휘lv증가, 1,2,3,4,5)
            PRINTFORMW 당신의 지휘레벨이 {지휘lv증가}레벨이 되었습니다.
    ENDIF

    SIF MONEY < 0
        CALL BANKRUPT

    SIF TARGET == 0
        GOTO NULL_TARGET

    CALL MSG_Morning
ENDIF

$NULL_TARGET

IF FLAG:속박상태
    PRINTW ??이 되었습니다.
ELSE
    IF TIME == 0
        PRINTW 아침이 되었습니다.
    ELSEIF TIME == 1
        PRINTW 밤이 되었습니다.
    ENDIF
ENDIF

;주식 가격은 GLOBAL 변수로 관리하기에 아무 세이브에서 시간이 바뀔때마다 변동
CALL UPDATE_STOCK

BEGIN SHOP

;========================================================================
;함수명: MAINTENANCE_COST(ARG)
;마지막 수정: v0.9
;캐릭터당 유지비 계산 후 결과값 반환
;ARG = 인게임 캐릭터번호
;========================================================================
@MAINTENANCE_COST(ARG)
#FUNCTION
#DIM DYNAMIC 유지비

유지비 = ITEMPRICE:(ITEM_BIO(NO:ARG)) / 20
유지비 += 유지비 * RAND(1, 100)
IF TALENT:ARG:분류 == 2 ;기동
    유지비 = 유지비 * 6 / 5
ELSEIF TALENT:ARG:분류 == 3 ;중장
    유지비 = 유지비 * 13 / 10
ENDIF
SIF TALENT:ARG:근육
    유지비 = 유지비 * 11 / 10
SIF TALENT:ARG:먹보
    유지비 = 유지비 * 3 / 2
IF TALENT:ARG:체형 == 0 ;작은체구
    유지비 = 유지비 * 7 / 10
ELSEIF TALENT:ARG:체형 == 2 ;큰체구
    유지비 = 유지비 * 13 / 10
ENDIF
SIF FLAG:속박상태
    유지비 = 0

;/100은 에라엔진의 랜덤변수(RAND) 후처리 목적
유지비 = 유지비 / 2 / 100
RETURNF 유지비
;========================================================================
;함수명: TURNEND_DAY_to_NIGHT
;마지막 수정: v0.9
;원본 api/uptime1 대응
;낮>밤시 CFLAG:etc의 처리
;========================================================================
@TURNEND_DAY_to_NIGHT
#DIM 대상

FOR 대상, 1, CHARANUM
    ;미보유 캐릭터 제외
    SIF ITEM:(ITEM_BIO(NO:대상)) < 1
        CONTINUE
    SELECTCASE NO:대상
        CASE 132 ;유미
            IF 대상 == TARGET
                SIF TALENT:대상:혼인 == 1
                    CFLAG:대상:etc1 = 0 ;유미 엔딩 3 카운트(혼인 5일 방치)
                SIF TALENT:대상:정신쇠약 == 2
                    CFLAG:대상:etc2 = 0 ;유미 엔딩 4 카운트(정신쇠약 5일 방치)
            ENDIF
        CASE 71 ;네오딤 ;TARGET 아니어도 처리
            SIF CFLAG:대상:스트레스 < 100
                CFLAG:대상:etc1 = 0 ;네오딤 스트레스 100상태 카운트
        CASE 76 ;닥터
            IF 대상 == TARGET
                CFLAG:대상:etc1 = 0 ;닥터 성장이벤 카운트
                CFLAG:대상:etc4 = 0 ;닥터 엔딩 3 카운트
            ENDIF
    ENDSELECT
NEXT
;========================================================================
;함수명: TURNEND_NIGHT_to_DAY_PRE
;마지막 수정: v0.9
;원본 api/uptime2 부분 대응
;밤 > 낮시 유지비 처리와 다음 처리시 필요한 TFLAG 생성
;uptime2에서의 출격 처리는 @DAYEND_SORTIE에서 따로 함
;총 유지비 반환(양수)
;========================================================================
@TURNEND_NIGHT_to_DAY_PRE
#FUNCTION
#DIM 대상
#DIM DYNAMIC 총유지비

FOR 대상, 1, CHARANUM
    ;미보유 캐릭터 제외
    SIF ITEM:(ITEM_BIO(NO:대상)) < 1
        CONTINUE

    ;유지비 처리
    총유지비 += MAINTENANCE_COST(대상)

    ;정신붕괴 인구조사
    SIF TALENT:대상:정신쇠약 == 2
        TFLAG:정신붕괴인구 ++
NEXT
RETURNF 총유지비
;========================================================================
;함수명: TURNEND_NIGHT_to_DAY_etc(ARG)
;밤 > 낮시 CFLAG:etc 관련 처리
;ARG = 인게임 캐릭터번호
;========================================================================
@TURNEND_NIGHT_to_DAY_etc(ARG)
SELECTCASE NO:ARG
    CASE 7 ;리제
        IF CFLAG:ARG:공포 > 299 && !TALENT:ARG:혼인
            CFLAG:ARG:etc1 ++ ;etc1 = 리제 엔딩 3 카운트
        ELSE
            CFLAG:ARG:etc1 = 0
        ENDIF
        IF TALENT:ARG:짜증 && !TALENT:ARG:혼인
            CFLAG:ARG:etc2 ++ ;etc2 = 리제 엔딩 4
        ELSE
            CFLAG:ARG:etc2 = 0
        ENDIF
    CASE 12 ;소완
        IF TALENT:ARG:정신쇠약 == 1 && ARG != TARGET
            CFLAG:ARG:etc1 ++ ;etc1 = 소완 엔딩 3 카운트
        ELSE
            CFLAG:ARG:etc1 = 0
        ENDIF
    CASE 68 ;에밀리
        IF TALENT:ARG:사랑 == 2 && TALENT:ARG:정신쇠약 == 1
            CFLAG:ARG:etc2 += 1 ;etc2 = 에밀리 엔딩 4 카운트
        ELSE
            CFLAG:ARG:etc2 = 0
        ENDIF
    CASE 71 ;네오딤
        IF CFLAG:ARG:스트레스 >= 100
            CFLAG:ARG:etc1 += 1 ;etc1 = 네오딤 스트레스 100상태 카운트
        ELSE
            CFLAG:ARG:etc1 = 0
        ENDIF
    CASE 73 ;팬텀
        IF FLAG:현캐릭터수 > 2
            CFLAG:ARG:etc2 = 1 ;etc2 = 팬텀 엔딩 4 바이오구매 유무
        ELSEIF TALENT:ARG:사랑 == 2 && !TALENT:ARG:짜증 && !TALENT:ARG:정신쇠약
            SIF !TALENT:ARG:혼인 && !CFLAG:ARG:etc2
                CFLAG:ARG:etc3 ++ ;etc3 팬텀 엔딩 4 카운트
        ELSE
            CFLAG:ARG:etc3 = 0
        ENDIF
    CASE 76 ;닥터
        IF ARG != TARGET && FLAG:현캐릭터수 > 2
            IF CFLAG:ARG:호감도 > 499 && !CFLAG:ARG:etc2 ;etc2 = 닥터 성장이벤 출력 유무
                CFLAG:ARG:etc1 ++ ;etc1 = 닥터 성장이벤 카운트
            ELSE
                CFLAG:ARG:etc1 = 0
            ENDIF
            IF TALENT:ARG:혼인 == 1 && TALENT:ARG:체형 == 2
                CFLAG:ARG:etc4 ++ ;etc4 = 닥터 엔딩 3 카운트
            ELSE
                CFLAG:ARG:etc4 = 0
            ENDIF
        ENDIF
    CASE 106 ;오드리
        SIF CFLAG:ARG:etc1 > 0 && CFLAG:ARG:etc1 < 4
            CFLAG:ARG:etc1 ++ ;etc1 = 오드리 더치와 이벤트 일수
    CASE 108 ;더치 걸
        IF TALENT:ARG:짜증 && CFLAG:ARG:호감도 < 500
            CFLAG:ARG:etc1 ++ ;etc1 = 더치 걸 엔딩 3 카운트
        ELSE
            CFLAG:ARG:etc1 = 0
        ENDIF
    CASE 132 ;유미
        IF !TALENT:ARG:의존증 && ARG != TARGET
            IF TALENT:ARG:혼인 == 1
                CFLAG:ARG:etc1 ++  ;etc1 = 유미 엔딩 3 카운트(혼인 5일 방치)
            ELSE
                CFLAG:ARG:etc1 = 0
            ENDIF
            IF TALENT:ARG:정신쇠약 == 2
                CFLAG:ARG:etc2 ++ ;etc2 = 유미 엔딩 4 카운트(정신붕괴 5일 방치)
            ELSE
                CFLAG:ARG:etc2 = 0
            ENDIF
        ELSE
            CFLAG:ARG:etc1 = 0
            CFLAG:ARG:etc2 = 0
        ENDIF
    CASE 138 ;베로니카
        SIF CFLAG:ARG:etc2 > 0 && CFLAG:ARG:etc2 < 11
            CFLAG:ARG:etc2 += 1 ;etc2 = 베로니카 실종 일수
ENDSELECT
;========================================================================
;함수명: BANKRUPT
;파산 처리 함수
;========================================================================
@BANKRUPT
PRINTL 돈이 부족하여 파산하셨습니다.
PRINTL
PRINTL
WHILE 1
    PRINTFORML 부족한 금액 : %MONEYSTR(MONEY)%
    PRINTL
    PRINTL 
    PRINTL [1] 바이오로이드 판매
    PRINTL [2] 주식 판매
    PRINTL [0] 그냥 파산할게요.
    INPUT
    SELECTCASE RESULT
        CASE 0
            PRINTL 모든 정보를 초기화합니다.
            PRINTL 동의하십니까?
            CALL ASK_YN("아뇨, 뚱인데요", "예")
            SELECTCASE RESULT
                CASE 0 ;아뇨
                    CONTINUE
                CASE 1 ;예
                    BREAK
            ENDSELECT
        CASE 1
            CALL FAREWELL(1)
        CASE 2
            CALL STOCK_MARKET
    ENDSELECT
    SIF MONEY > 0
        RETURN 1
WEND

BEGIN TITLE
