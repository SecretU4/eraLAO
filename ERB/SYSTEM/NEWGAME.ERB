;===========================================================
;파일명: NEWGAME.ERB
;새 게임 관련 처리
;===========================================================
;함수명: EVENTFIRST
;SYSTEM CALL 'BEGIN FIRST'시 이곳으로
;===========================================================
@EVENTFIRST

;세이브파일의 현 버전 설정
FLAG:LAST_LAOVERSION = GAMEBASE_VERSION

;전역변수 초기화
DAY = 1
MASTER = 0
TARGET = 0
ASSI = 0
MONEY:0 = 100000

;원룸으로 설정
FLAG:캐릭터보유고 = 2


CALL MESSAGE_OPENING

CALL COMMANDER_MAKING

;게임 시작시 변수 데이터 처리
CALL ADD_ALL_CHARA
CALL UPDATE_STOCK

;약혼반지는 하나만
CALL SET_EX_ITEM_SALES(1, 1)

CALL SET_ITEM_SALES(2, 50, 20)
CALL SET_EX_ITEM_SALES(200, 205)

;보너스 퀴즈
PRINTL 스타트 퀴즈가 있습니다.
PRINTL 10문제가 있으며
PRINTL 각 문항마다 초기자금 + 5000의 혜택이 있습니다.
CALL ASK_YN("합니다", "ㄴㄴ안함")
IF RESULT == 0
    CALL START_QUIZ
ELSE
ENDIF

PRINTW 부디 후회 없는 삶을 살아주세요.

BEGIN SHOP

;===========================================================
;함수명: MESSAGE_OPENING
;오프닝용 텍스트 출력
;===========================================================
@MESSAGE_OPENING

PRINTW ...
PRINTW ..
PRINTW .
PRINTFORML 당신은 눈을 떴다.
PRINTFORML 딱히, 잠에서 깼다는 건 아니고 정신을 차렸다는 뜻도 아니다.
PRINTFORML 인간이 하루에 1만번 이상을 반복하는 행위
PRINTFORML 그런 행위를 했을 뿐인 당신의 눈 앞에는
PRINTFORML 불과 1밀리 초 전의 모습을 찾아볼 수 없었다.
PRINTFORML 처음보는 방, 그 한 쪽에 틀어져있는 TV의 뉴스에선 바이오로이드, 철충 등의 익숙한 단어들이 들렸다.
PRINTFORML 바이오로이드들이 존재하는 세계, 철충이 존재하는 세계, 그러나 멸망하지 않은 세계
PRINTFORML 당신이 알지만 다른 세계
PRINTFORMW . . .
PRINTFORML 그런 세계 속에서
PRINTFORMW 당신은 일단 행동하기로 했다.

;===========================================================
;함수명: COMMANDER_MAKING
;주인공 생성 함수
;===========================================================
@COMMANDER_MAKING
#DIM MASTER_AGE

$SET_AGE
DRAWLINE
PRINTFORML 당신의 체형을 선택해주세요.
PRINTFORML [0] 소년
PRINTFORML [1] 청년
PRINTFORML [2] 중년
INPUT
;TODO 문자열 입력 가능할지 고려
IF GROUPMATCH(RESULT, 0,1,2)
    IF RESULT == 0
        PRINTFORML 소년의 몸으로 시작합니다.
    ELSEIF RESULT == 1
        PRINTFORML 청년의 몸으로 시작합니다.
    ELSEIF RESULT == 2
        PRINTFORML 중년의 몸으로 시작합니다.
    ENDIF
    MASTER_AGE = RESULT
    CALL ASK_YN("그래", "아니야")
    SIF RESULT == 1
        GOTO SET_AGE
ELSE
    PRINTL 제시된 목록에서 선택해주세요.
    GOTO SET_AGE
ENDIF
TALENT:MASTER:나이 = MASTER_AGE
MAXBASE:MASTER:체력 = BASE:MASTER:체력
MAXBASE:MASTER:기력 = BASE:MASTER:기력

CALL COMMANDER_NAMESET

;===========================================================
;함수명: START_QUIZ
;시작시 보너스용 문제 호출
;===========================================================
@START_QUIZ
VARSET LOCALS

LOCALS:0 = 에라라오PE는 모바일 게임 '라스트오리진'의 팬게임이다.
LOCALS:1 = '라스트오리진'은 한국산 모바일게임이다.
LOCALS:2 = 바이오로이드는 기계다.
LOCALS:3 = 매크로 혹은 직접 입력 상관없이 도배를 하게 되면 예기치 못한 오류가 생길 수 있다.
LOCALS:4 = 에라라오PE의 배경은 '라스트오리진' 세계관의 과거 이야기이다.
LOCALS:5 = 바이오로이드는 인공 생명체이며, 성장은 못하지만 음식섭취 및 배설, 임신이 가능하고 감정또한 존재한다.
LOCALS:6 = 에라라오PE를 종료하지 않은 채 장시간 방치 후 게임을 이어하면 계정데이터 일부가 초기화될 가능성이 있다.
LOCALS:7 = 에라라오PE(≒카톡)는 트래픽 걱정이 없다.
LOCALS:8 = 남성호르몬의 문제로 인해 남성 바이오로이드는 만들지 않는다.
LOCALS:9 = 철충은 AGS와 바이오로이드를 감염시킨다.

REPEAT 10
    DRAWLINE
    PRINTFORML %LOCALS:COUNT%
    SIF COUNT == 7
        PRINTFORML (트래픽 : 서버에서 개인에게 전송되는 데이터의 양)
    CALL ASK_YN("YES","NO")
    IF (GROUPMATCH(COUNT, 0,1,3,5,6,8) && RESULT == 0) || (GROUPMATCH(COUNT, 2,4,7,9) && RESULT == 1)
        PRINTL 정답입니다.
        PRINTW 소지금 + 5000
        MONEY:0 += 5000 
    ELSE
        PRINTW 오답입니다.
    ENDIF
REND
DRAWLINE
PRINTFORML 모든 문제가 끝났습니다.

;===========================================================
;함수명:ADD_ALL_CHARA
;현재 데이터베이스상 존재하는 캐릭터를 체크 및 구동 메모리에 추가
;LOCAL = CSV상 번호, COUNT = 인게임 호출번호
;===========================================================
@ADD_ALL_CHARA

FOR LOCAL, 1, TOTAL_CHARANUM
	SIF !EXISTCSV(LOCAL)
		CONTINUE
	ADDCHARA LOCAL
	; TARGET = GETCHARA(LOCAL)
	; CALL SET_MINIMUM_EXP_ALL(GETCHARA(LOCAL))
    RESULT = 0
    TRYCALLFORM IS_M_No{LOCAL}
    SIF RESULT
        CFLAG:(GETCHARA(LOCAL)):개별대사유무 = 1
NEXT
SORTCHARA

;===========================================================
;함수명:NEWGAME
;새로하기를 눌렀을때의 동작
;===========================================================
@NEWGAME
PRINTW 엔터 키를 눌러 계속...
RESETDATA
CLEARLINE LINECOUNT
BEGIN FIRST
;===========================================================
;함수명:COMMANDER_NAMESET
;MASTER 이름 변경 기능
;===========================================================
@COMMANDER_NAMESET
IF GROUPMATCH(NAME:MASTER, "복","규동","아이샤","오렌지","에이드","돈부리")
    SETCOLOR 250, 0, 0
    PRINTW 치터는 이름을 바꾸면 안돼지!
    RESETCOLOR
    RETURN
ENDIF

$SET_NAME
PRINTL 당신의 이름(NAME)은?
IF STRLENS(NAME:MASTER)
	PRINTFORM 현재의 이름：%NAME:MASTER%      
	PRINTBUTTON "[변경하지 않음]",""
	PRINTL
ENDIF
INPUTS
IF RESULTS == ""
	NAME:MASTER = %NAME:MASTER%
ELSEIF GROUPMATCH(RESULTS, "복","규동","아이샤","오렌지","에이드","돈부리")
    PRINTL 특정 이름은 막혀 있습니다.
    GOTO SET_NAME
ELSE
	NAME:MASTER = %RESULTS%
ENDIF

$SET_CALLNAME
PRINTL 당신의 통칭(CALLNAME)은?
IF STRLENS(CALLNAME:MASTER)
	PRINTFORM 현재의 통칭：%CALLNAME:MASTER%      
	PRINTBUTTON "[변경하지 않음]",""
	PRINTL
ENDIF
INPUTS
IF RESULTS == ""
	CALLNAME:MASTER = %CALLNAME:MASTER%
ELSEIF GROUPMATCH(RESULTS, "복","규동","아이샤","오렌지","에이드","돈부리")
    PRINTL 특정 이름은 막혀 있습니다.
    GOTO SET_CALLNAME
ELSE
	CALLNAME:MASTER = %RESULTS%
ENDIF
