;========================================================================
;버전명 출력
;========================================================================
@eraLAO_VER()
#FUNCTIONS

LOCALS = {GAMEBASE_VERSION/1000}.{(GAMEBASE_VERSION%1000)/100}{(GAMEBASE_VERSION%100)/10}
SIF GAMEBASE_VERSION % 10
    LOCALS = {GAMEBASE_VERSION/1000}.{(GAMEBASE_VERSION%1000)/100}{(GAMEBASE_VERSION%100)/10}{GAMEBASE_VERSION % 10}

RETURNF LOCALS
;========================================================================
;버전 변경시 추가/초기화 사항 처리
;========================================================================
@VERSION_UP

SIF FLAG:LAST_LAOVERSION == GAMEBASE_VERSION
    RETURN 0

FLAG:LAST_LAOVERSION = GAMEBASE_VERSION
RETURN 1
;=========================================================================
;시작시 디버그
;=========================================================================
@DEBUG
PRINTL 디버그 모드입니다.

;세이브파일의 현 버전 설정
FLAG:LAST_LAOVERSION = GAMEBASE_VERSION

;전역변수 초기화
DAY = 1
MASTER = 0
TARGET = 0
ASSI = 0
MONEY:0 = 100000000

;원룸으로 설정
FLAG:캐릭터보유고 = 2

NAME:MASTER = 아이샤
CALLNAME:MASTER = 아이샤

;게임 시작시 변수 데이터 처리
CALL ADD_ALL_CHARA

;약혼반지는 하나만
CALL SET_EX_ITEM_SALES(1, 1)

CALL SET_ITEM_SALES(2, 50, 20)
CALL SET_EX_ITEM_SALES(200, 203)

PRINTW 디버그 설정이 완료되었습니다.

BEGIN SHOP
;=========================================================================
;개별 디버그
;=========================================================================
@EPC_DEBUG

CALL EPC_MENU
WHILE 1
    INPUT
    SELECTCASE RESULT
        CASE 0 ; 돌아가기
            RETURN -1
        CASE 1 ; 캐릭터 설정
            CALL DEBUG_CHARA
        CASE 2 ; 당신 설정
        CASE 8 ; 현재시간 변경
            CALL CHANGE_TIME
        CASE 9 ; 휴식 이벤트 설정
        CASE 10 ; 엔딩 설정
    ENDSELECT
WEND
;-------------------------------------------------------------------------
@EPC_MENU
FOR LOCAL, 0, 11
    PRINTFORM [{LOCAL}]
    SELECTCASE LOCAL
        CASE 0
            PRINTL 돌아가기
        CASE 1
            PRINTL 캐릭터 설정
        CASE 2
            PRINTL 플레이어 설정
        CASE 8
            PRINTL 현재시간 변경
        CASE 9
            PRINTL 휴식 이벤트 설정
        CASE 10
            PRINTL 엔딩 설정
        CASEELSE
            CLEARLINE 1
    ENDSELECT
NEXT
;-------------------------------------------------------------------------
@CHANGE_TIME
#DIM 현재일자
#DIM 현재시각
#DIM 변경타임, 2
VARSET 변경타임
현재일자 = DAY
현재시각 = TIME
PRINTFORML 현재 {DAY}일차 \@ TIME ? 밤 # 낮 \@ 입니다.
WHILE 1
    PRINTL 몇일차로 변경할까요? (공란/0 입력시 변경없음)
    INPUT
    IF RESULT
        변경타임 = RESULT
    ELSE
        변경타임 = 현재일자
    ENDIF

    PRINTL 어떤 시간대로 이동할까요? (1 = 낮, 2 = 밤, 이외 = 변경없음)
    INPUT
    SELECTCASE RESULT
        CASE 1
            변경타임:1 = 0
        CASE 2
            변경타임:1 = 1
        CASEELSE
            변경타임:1 = 현재시각
    ENDSELECT
    IF 변경타임 == 현재일자 && 변경타임:1 == 현재시각
        PRINTW 현재 값을 유지하겠습니다.
        RETURN 0
    ELSE
        PRINTFORML {변경타임} 일차 \@ 변경타임:1 ? 밤 # 낮 \@ 으로 이동하시겠습니까?
        CALL ASK_YN
        IF RESULT ; 아니오
            PRINTW 다시 설정해주세요.
        ELSE ; 예
            DAY = 변경타임
            TIME = 변경타임:1
            RETURN 1
        ENDIF
    ENDIF
WEND
;-------------------------------------------------------------------------
@DEBUG_CHARA
#DIM 대상
;대상 선택
CALL SHOW_BIOLIST("DEBUG")
SIF RESULT == -1
    RETURN 0
대상 = RESULT

;변경 가능 목록 표시
PRINTL 변경할 수치/능력을 선택해주세요.
PRINTL 이벤트를 통하지 않기에 수치가 비정상적이라면 오류가 발생할 수 있습니다.
PRINTL
DRAWLINE
PRINTFORMLC [1] 호감도 (현재 {CFLAG:대상:호감도})
PRINTFORMLC [2] 공포 (현재 {CFLAG:대상:공포})
PRINTFORMLC [3] 스트레스 (현재 {CFLAG:대상:스트레스})
PRINTFORMLC [4] CFLAG 수정
PRINTL
DRAWLINE
FOR LOCAL, 21, 90
    IF TALENTNAME:LOCAL == ""
        CONTINUE
    ELSEIF GROUPMATCH(LOCAL, 30,62,70,72)
        SELECTCASE LOCAL
            CASE 30
                IF TALENT:대상:LOCAL == -1
                    LOCALS '= "[동생]"
                ELSEIF TALENT:대상:LOCAL == 1
                    LOCALS '= "[누님]"
                ELSE
                    LOCALS = TALENTNAME:LOCAL
                ENDIF
            CASE 62
                IF TALENT:대상:LOCAL == -1
                    LOCALS += "[비관적]"
                ELSEIF TALENT:대상:LOCAL == 1
                    LOCALS += "[긍정적]"
                ELSE
                    LOCALS = TALENTNAME:LOCAL
                ENDIF
            CASE 70
                IF TALENT:대상:LOCAL == -1
                    LOCALS += "[무식]"
                ELSEIF TALENT:대상:LOCAL == 1
                    LOCALS += "[지식]"
                ELSE
                    LOCALS = TALENTNAME:LOCAL
                ENDIF
            CASE 72
                IF TALENT:대상:LOCAL == 1
                    LOCALS += "[정신쇠약]"
                ELSEIF TALENT:대상:LOCAL == 2
                    LOCALS += "[정신붕괴]"
                ELSE
                    LOCALS = 정신관련
                ENDIF
        ENDSELECT
    ELSE
        LOCALS += @"[%TALENTNAME:LOCAL%]"
    ENDIF
    PRINTFORMLC [{LOCAL}] %LOCALS, 8%
NEXT
;위 값 초기화
VARSET LOCAL
LOCALS =
LOCALS:1 =
LOCALS:2 =
LOCALS:3 =
LOCALS:4 =
;실제 구동부
WHILE 1
    INPUT
    SELECTCASE RESULT
        CASE 1 ;호감도
            PRINTFORML %CALLNAME:대상%의 호감도값을 지정해주세요.
            INPUT
            LOCAL = RESULT
            PRINTFORML 호감도 수치를 {RESULT} 로 변경합니까?
            CALL ASK_YN
            IF RESULT == 0
                CFLAG:대상:호감도 = LOCAL
            ELSE
                CONTINUE
            ENDIF
        CASE 2 ;공포
            PRINTFORML %CALLNAME:대상%의 공포값을 지정해주세요.
            INPUT
            LOCAL = RESULT
            PRINTFORML 공포 수치를 {RESULT} 로 변경합니까?
            CALL ASK_YN
            IF RESULT == 0
                CFLAG:대상:공포 = LOCAL
            ELSE
                CONTINUE
            ENDIF
        CASE 3 ;스트레스
            PRINTFORML %CALLNAME:대상%의 스트레스값을 지정해주세요.
            INPUT
            LOCAL = RESULT
            PRINTFORML 스트레스 수치를 {RESULT} 로 변경합니까?
            CALL ASK_YN
            IF RESULT == 0
                CFLAG:대상:스트레스 = LOCAL
            ELSE
                CONTINUE
            ENDIF
        CASE 4 ;CFLAG 수정
            PRINTL 변경하실 CFLAG의 인덱스(CFLAG.CSV 내 숫자값)값을 지정해주세요. 0도 유효한 값으로 입력됩니다.
            INPUT
            IF CFLAGNAME:RESULT == ""
                PRINTL CSV에서 정의되지 않은 인덱스값입니다.
                PRINTW 다시 시도해주세요.
            ELSE
                LOCAL:1 = RESULT
                PRINTFORML %CALLANME:대상%의 %CFLAGNAME:(LOCAL:1)%의 현재 값은 {CFLAG:대상:(LOCAL:1)} 입니다.
                PRINTL 변경할 값을 입력해주세요. 0도 유효한 값으로 입력됩니다.
                INPUT
                LOCAL:2 = RESULT
                PRINTFORML %CALLANME:대상%의 %CFLAGNAME:(LOCAL:1)% 수치를 {RESULT}로 변경합니까?
                CALL ASK_YN
                IF RESULT == 0
                    CFLAG:대상:(LOCAL:1) = RESULT
                ELSE
                    CONTINUE
                ENDIF
            ENDIF
        CASE 21 TO 90 ;일반능력
            LOCAL = RESULT
            PRINTFORML 선택 능력 : %TALENTNAME:LOCAL%
            PRINT 현재 상태 : 
            ;단순 on/off 아님
            ;-1 < 0 > 1 형 TALENT
            IF GROUPMATCH(LOCAL, 30,62,70)
                ;ASK_M용 선택지 처리
                LOCALS:1 '= "바꾸지 않는다"
                LOCALS:2 '= "으로 한다"
                LOCALS:3 '= "으로 한다"
                LOCALS:4 '= "값을 지운다"
                SELECTCASE LOCAL
                    CASE 30 ;나이속성
                        IF TALENT:대상:LOCAL == -1
                            PRINTL [동생]
                            LOCALS:2 '= "[동생]" + LOCALS:2
                        ELSEIF TALENT:대상:LOCAL == 1
                            PRINTL [누님]
                            LOCALS:3 '= "[누님]" + LOCALS:3
                        ELSE
                            PRINTL 없음
                        ENDIF
                    CASE 62 ;긍정도
                        IF TALENT:대상:LOCAL == -1
                            PRINTL [비관적]
                            LOCALS:2 '= "[비관적]" + LOCALS:2
                        ELSEIF TALENT:대상:LOCAL == 1
                            PRINTL [긍정적]
                            LOCALS:3 '= "[긍정적]" + LOCALS:3
                        ELSE
                            PRINTL 없음
                        ENDIF
                    CASE 70 ;지능
                        IF TALENT:대상:LOCAL == -1
                            PRINTL [무식]
                            LOCALS:2 '= "[무식]" + LOCALS:2
                        ELSEIF TALENT:대상:LOCAL == 1
                            PRINTL [지식]
                            LOCALS:3 '= "[지식]" + LOCALS:3
                        ELSE
                            PRINTL 없음
                        ENDIF
                ENDSELECT
                PRINTL 어떻게 변경할까요?
                CALL ASK_M(LOCALS:1, 1, LOCALS:2, TALENT:대상:LOCAL != -1, LOCALS:3, TALENT:대상:LOCAL != 1, LOCALS:4, TALENT:대상:LOCAL != 0)
                SELECTCASE RESULT
                    CASE 0 ;현상태 유지
                        CONTINUE
                    CASE 1 ;상단값으로 변경
                        TALENT:대상:LOCAL = -1
                    CASE 2 ;하단값으로 변경
                        TALENT:대상:LOCAL = 1
                    CASE 3 ;비활성화
                        TALENT:대상:LOCAL = 0
                ENDSELECT
            ;0 > 1 > 2 형 TALENT
            ELSEIF GROUPMATCH(LOCAL, 25,26,72)
                ;ASK_M용 선택지 처리
                LOCALS:1 '= "바꾸지 않는다"
                LOCALS:2 '= "으로 한다"
                LOCALS:3 '= "으로 한다"
                LOCALS:4 '= "값을 지운다"
                SELECTCASE LOCAL
                    CASE 25 ;사랑
                        IF TALENT:대상:LOCAL == 1
                            PRINTL [연모]]
                        ELSEIF TALENT:대상:LOCAL == 2
                            PRINTL [사랑]
                        ELSE
                            PRINTL 없음
                        ENDIF
                    CASE 26 ;혼인                  
                        IF TALENT:대상:LOCAL == 1
                            PRINTL [혼인]
                        ELSEIF TALENT:대상:LOCAL == 2
                            PRINTL [입양]
                        ELSE
                            PRINTL 없음
                        ENDIF
                    CASE 72 ;정신쇠약
                        IF TALENT:대상:LOCAL == 1
                            PRINTL [정신쇠약]
                        ELSEIF TALENT:대상:LOCAL == 2
                            PRINTL [정신붕괴]
                        ELSE
                            PRINTL 없음
                        ENDIF
                ENDSELECT
                PRINTL 어떻게 변경할까요?
                CALL ASK_M(LOCALS:1, 1, LOCALS:2, TALENT:대상:LOCAL != 1, LOCALS:3, TALENT:대상:LOCAL != 2, LOCALS:4, TALENT:대상:LOCAL != 0)
                SELECTCASE RESULT
                    CASE 0 ;현상태 유지
                        CONTINUE
                    CASE 1 ;상단값으로 변경
                        TALENT:대상:LOCAL = 1
                    CASE 2 ;하단값으로 변경
                        TALENT:대상:LOCAL = 2
                    CASE 3 ;비활성화
                        TALENT:대상:LOCAL = 0
                ENDSELECT
            ELSE
                PRINTFORML \@ TALENT:대상:LOCAL ? 활성화 # 비활성화 \@
                PRINTL 현재 값을 변경합니까?
                CALL ASK_YN()
                IF RESULT == 0
                    TALENT:대상:LOCAL = !TALENT:대상:LOCAL
                ELSE
                    CONTINUE
                ENDIF
            ENDIF
    ENDSELECT
WEND