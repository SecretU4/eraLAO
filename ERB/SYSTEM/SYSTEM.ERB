;========================================================================
;SYSYEM_CALL, BEGIN TURNEND 호출시
;시간대/날짜 변경 처리
;========================================================================
@EVENTTURNEND
#DIM DYNAMIC 주회
#DIM 지휘lv증가
#DIM DYNAMIC 유지비
#DIM DYNAMIC 총수입, 5
;지출, 수입 모두 양수값 저장이니 계산시 연산자의 올바른 사용 필수
;0 = 총합/1 = 유지비/2 = 출격수입/3,4,5 예비

SIF CFLAG:MASTER:엔딩플래그
    CALL ENDING(-1)

IF TIME == 0 ;낮 > 밤
    TIME = 1
    
    ;낮 > 밤 캐릭터 변동 체크 ;원본 uptime1 대응
    FOR 주회, 1, CHARANUM
        ;미보유 캐릭터 제외
        SIF ITEM:(ITEM_BIO(NO:주회)) < 1
            CONTINUE
        SELECTCASE NO:주회
            CASE 132 ;유미
                IF 주회 == TARGET
                    SIF TALENT:주회:혼인 == 1
                        CFLAG:주회:etc1 = 0 ;유미 엔딩 3 카운트(혼인 5일 방치)
                    SIF TALENT:주회:정신쇠약 == 2
                        CFLAG:주회:etc2 = 0 ;유미 엔딩 4 카운트(정신쇠약 5일 방치)
                ENDIF
            CASE 71 ;네오딤 ;TARGET 아니어도 처리
                SIF CFLAG:주회:스트레스 < 100
                    CFLAG:주회:etc1 = 0 ;네오딤 스트레스 100상태 카운트
            CASE 76 ;닥터
                IF 주회 == TARGET
                    CFLAG:주회:etc1 = 0 ;닥터 성장이벤 카운트
                    CFLAG:주회:etc4 = 0 ;닥터 엔딩 3 카운트
                ENDIF
        ENDSELECT
    NEXT

ELSEIF TIME == 1 ;밤 > 낮
    TIME = 0
    DAY ++
    CALL DAYEND_SORTIE
    총수입:2 = RESULT

    ;원본 uptime2 대응, 출격 처리는 @DAYEND_SORTIE에서 미리 함
    ;유지비 처리 및 메시지에 필요한 데이터 수집
    FOR 주회, 1, CHARANUM
        ;미보유 캐릭터 제외
        SIF ITEM:(ITEM_BIO(NO:주회)) < 1
            CONTINUE

        ;유지비 처리
        유지비 = ITEMPRICE(ITEM_BIO(NO:주회)) / 20
        유지비 += 유지비 * RAND(1, 100)
        IF TALENT:주회:분류 == 2 ;기동
            유지비 = 유지비 * 6 / 5
        ELSEIF TALENT:주회:분류 == 3 ;중장
            유지비 = 유지비 * 13 / 10
        ENDIF
        SIF TALENT:주회:근육
            유지비 = 유지비 * 11 / 10
        SIF TALENT:주회:먹보
            유지비 = 유지비 * 3 / 2
        IF TALENT:주회:체형 == 0 ;작은체구
            유지비 = 유지비 * 7 / 10
        ELSEIF TALENT:주회:체형 == 2 ;큰체구
            유지비 = 유지비 * 13 / 10
        ENDIF
        SIF FLAG:속박상태
            유지비 = 0
        총수입:1 += (유지비 / 2 / 100)

        ;정신붕괴 인구조사
        SIF TALENT:주회:정신쇠약 == 2
            TFLAG:정신붕괴인구 ++
    NEXT
    주회 = 0

    ;캐릭터 스텟 및 능력 변동사항 처리
    FOR 주회, 1, CHARANUM

        ;미보유 캐릭터 제외
        SIF ITEM:(ITEM_BIO(NO:주회)) < 1
            CONTINUE

        ;본인이 정신붕괴라면 통과
        SIF TFLAG:정신붕괴인구 && TALENT:주회:정신쇠약 != 2
            CFLAG:주회:공포 += 2
        ;조건에 맞으면서 본인이 대상이 아닌 경우
        IF TALENT:주회:얀데레 && 주회 != TARGET
            CFLAG:주회:스트레스 += 20
            TFLAG:얀데레질투인구 ++
        ENDIF
        IF TALENT:주회:긍정도 == -1 && 주회 != TARGET ;비관적
            CFLAG:주회:스트레스 += 10
            TFLAG:비관적자학인구 ++
        ENDIF

        CALL GET_Annoyed(주회)
        IF RESULT == 1
            TFLAG:짜증획득인구 ++
        ELSEIF RESULT == -1
            TFLAG:짜증소멸인구 --
        ENDIF

        CALL GET_WeakMind(주회)
        SIF RESULT == 1
            TFLAG:정신쇠약획득인구 ++
        
        CALL GET_MindBreak(주회)
        SIF RESULT == 1
            TFLAG:정신붕괴획득인구 ++

        ;캐릭터별 특수 플래그 처리 (CFLAG:etc)
        SELECTCASE NO:주회
            CASE 7 ;리제
                IF CFLAG:주회:공포 > 299 && !TALENT:주회:혼인
                    CFLAG:etc1 ++ ;etc1 = 리제 엔딩 3 카운트
                ELSE
                    CFLAG:etc1 = 0
                ENDIF
                IF TALENT:주회:짜증 && !TALENT:주회:혼인
                    CFLAG:주회:etc2 ++ ;etc2 = 리제 엔딩 4
                ELSE
                    CFLAG:주회:etc2 = 0
            CASE 12 ;소완
                IF TALENT:주회:정신쇠약 == 1 && 주회 != TARGET
                    CFLAG:주회:etc1 ++ ;etc1 = 소완 엔딩 3 카운트
                ELSE
                    CFLAG:주회:etc1 = 0
                ENDIF
            CASE 68 ;에밀리
                IF TALENT:주회:사랑 == 2 && TALENT:주회:정신쇠약 == 1
                    CFLAG:주회:etc2 += 1 ;etc2 = 에밀리 엔딩 4 카운트
                ELSE
                    CFLAG:주회:etc2 = 0
                ENDIF
            CASE 71 ;네오딤
                IF CFLAG:주회:스트레스 >= 100
                    CFLAG:주회:etc1 += 1 ;etc1 = 네오딤 스트레스 100상태 카운트
                ELSE
                    CFLAG:주회:etc1 = 0
                ENDIF
            CASE 73 ;팬텀
                IF FLAG:현캐릭터수 > 2
                    CFLAG:etc2 = 1 ;etc2 = 팬텀 엔딩 4 바이오구매 유무
                ELSEIF TALENT:주회:사랑 == 2 && !TALENT:주회:짜증 && !TALENT:주회:정신쇠약
                    SIF !TALENT:주회:혼인 && !CFLAG:주회:etc2
                        CFLAG:주회:etc3 ++ ;etc3 팬텀 엔딩 4 카운트
                ELSE
                    CFLAG:주회:etc3 = 0
                ENDIF
            CASE 76 ;닥터
                IF 주회 != TARGET && FLAG:현캐릭터수 > 2
                    IF CFLAG:주회:호감도 > 499 && !CFLAG:주회:etc2 ;etc2 = 닥터 성장이벤 출력 유무
                        CFLAG:주회:etc1 ++ ;etc1 = 닥터 성장이벤 카운트
                    ELSE
                        CFLAG:주회:etc1 = 0
                    ENDIF
                    IF TALENT:주회:혼인 == 1 && TALENT:주회:체형 == 2
                        CFLAG:주회:etc4 ++ ;etc4 = 닥터 엔딩 3 카운트
                    ELSE
                        CFLAG:주회:etc4 = 0
                    ENDIF
                ENDIF
            CASE 106 ;오드리
                SIF CFLAG:주회:etc1 > 0 && CFLAG:주회:etc1 < 4
                    CFLAG:주회:etc1 ++ ;etc1 = 오드리 더치와 이벤트 일수
            CASE 108 ;더치 걸
                IF TALENT:주회:짜증 && CFLAG:주회:호감도 < 500
                    CFLAG:주회:etc1 ++ ;etc1 = 더치 걸 엔딩 3 카운트
                ELSE
                    CFLAG:주회:etc1 = 0
                ENDIF
            CASE 132 ;유미
                IF !TALENT:주회:의존증 && 주회 != TARGET
                    IF TALENT:주회:혼인 == 1
                        CFLAG:주회:etc1 ++  ;etc1 = 유미 엔딩 3 카운트(혼인 5일 방치)
                    ELSE
                        CFLAG:주회:etc1 = 0
                    ENDIF
                    IF TALENT:주회:정신쇠약 == 2
                        CFLAG:주회:etc2 ++ ;etc2 = 유미 엔딩 4 카운트(정신붕괴 5일 방치)
                    ELSE
                        CFLAG:주회:etc2 = 0
                    ENDIF
                ELSE
                    CFLAG:주회:etc1 = 0
                    CFLAG:주회:etc2 = 0
                ENDIF
            CASE 138 ;베로니카
                SIF CFLAG:주회:etc2 > 0 && CFLAG:주회:etc2 < 11
                    CFLAG:주회:etc2 += 1 ;etc2 = 베로니카 실종 일수
        ENDSELECT

        ;날짜 변동에 따른 스트레스, 공포 감소 처리
        CFLAG:주회:스트레스 -= 10
        CFLAG:주회:공포 -= 2
        CFLAG:주회:스트레스 = MAX(CFLAG:주회:스트레스, 0)
        CFLAG:주회:공포 = MAX(CFLAG:주회:공포, 0)
    NEXT

    ;비용 정리
    ;출격 수입은 @DAYEND_SORTIE에서 처리됨
    MONEY:0 -= 총수입:1 ;유지비 처리

    ;총수입:0은 기록 및 디버그용
    총수입 = 총수입:2 - 총수입:1

    ;MASTER의 전투레벨처리
    CALL BattleLV_UP(MASTER)
    지휘lv증가 = RESULT

    ;속박상태가 아니라면 메시지 표시
    IF !FLAG:속박상태
        SIF TFLAG:전투레벨변동인구
            PRINTL 전투레벨이 상승한 전투원이 있습니다.
        SIF TFLAG:정신붕괴인구
            PRINTL 보유중인 바이오로이드 중에 [정신붕괴]를 소지한 인원이 있어, 모든 바이오로이드의 공포가 소량 상승하였습니다.
        SIF TFLALG:얀데레질투인구
            PRINTL [얀데레]를 소지한 인원이 질투로 인해 스트레스가 소량 상승하였습니다.
        SIF TFLALG:비관적자학인구
            PRINTL [비관적]을 소지한 인원이 자학으로 인해 스트레스가 소량 상승하였습니다.
        SIF TFLALG:짜증획득인구
            PRINTL 바이오로이드 중 누군가의 짜증이 난 것 같습니다.
        SIF TFLALG:짜증소멸인구
            PRINTL 바이오로이드 중 누군가의 짜증이 풀린 것 같습니다.
        SIF TFLALG:정신쇠약획득인구
            PRINTL  바이오로이드 중 누군가의 정신이 쇠약해진 것 같습니다.
        SIF TFLALG:정신붕괴획득인구
            PRINTL  바이오로이드 중 누군가의 마음이 부숴진 것 같습니다.
        PRINTFORMW 보유한 전투원들의 유지비용으로 %MONEYSTR(총수입:1)%이 소모되었습니다.
        SIF GROUPMATCH(지휘lv증가, 1,2,3,4,5)
            PRINTFORMW 당신의 지휘레벨이 {지휘lv증가}레벨이 되었습니다.
    ENDIF

    SIF TARGET == 0
        GOTO NULL_TARGET

    CALL MSG_Morning
ENDIF

$NULL_TARGET

IF FLAG:속박상태
    PRINTW ??이 되었습니다.
ELSE
    IF TIME == 0
        PRINTW 아침이 되었습니다.
    ELSEIF TIME == 1
        PRINTW 밤이 되었습니다.
    ENDIF
ENDIF

;주식 가격은 GLOBAL 변수로 관리하기에 아무 세이브에서 시간이 바뀔때마다 변동
CALL UPDATE_STOCK

BEGIN SHOP

;========================================================================
;버전명 출력
;========================================================================
@eraLAO_VER()
#FUNCTIONS

LOCALS = {GAMEBASE_VERSION/1000}.{(GAMEBASE_VERSION%1000)/100}{(GAMEBASE_VERSION%100)/10}
SIF GAMEBASE_VERSION % 10
    LOCALS = {GAMEBASE_VERSION/1000}.{(GAMEBASE_VERSION%1000)/100}{(GAMEBASE_VERSION%100)/10}{GAMEBASE_VERSION % 10}

RETURNF LOCALS
;========================================================================
;버전 변경시 추가/초기화 사항 처리
;========================================================================
@VERSION_UP

SIF FLAG:LAST_LAOVERSION == GAMEBASE_VERSION
    RETURN 0

FLAG:LAST_LAOVERSION = GAMEBASE_VERSION
RETURN 1