;============================================================
;파일명: COMMON.ERB
;공통으로 쓰일 수 있는 함수 모음
;============================================================

;------------------------------------------------------------
;네/아니오의 선택지를 표시해 입력 결과를 돌려주는 함수
;인수 0=긍정 선택지의 캐릭터라인,1=부정 선택지의 캐릭터라인
;반환값 0=네,1=아니오
;------------------------------------------------------------
@ASK_YN(ARGS:0 = "　네　", ARGS:1 = "아니오")
CURRENTREDRAW
LOCAL:0 = RESULT
REDRAW 0
PRINTBUTTON @" [%ARGS:0%]", 0
PRINTL
PRINTBUTTON @" [%ARGS:1%]", 1
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT != 0 && RESULT != 1
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
REDRAW LOCAL:0
RETURN RESULT

;-------------------------------------------------
;선택지를 표시해 입력 결과를 돌려주는 함수 조건을 채우지 않는 선택지를 회색 표시로 해 무효로 하는 것이 가능
;인수에 대사와 조건(0으로 무효, 비0으로 유효)을 지정. 최대 6개까지
;반환값은 선택한 선택지의 번호
;-------------------------------------------------
@ASK_M(ARGS:0, ARG:0, ARGS:1, ARG:1, ARGS:2, ARG:2, ARGS:3, ARG:3, ARGS:4, ARG:4, ARGS:5, ARG:5, ARGS:6, ARG:6)
#DIM CONST 선택지상한 = 6
CURRENTREDRAW
LOCAL:0 = RESULT
REDRAW 0
FOR LOCAL:0, 0, (선택지상한 + 1)
	IF ARGS:(LOCAL:0) != ""
		IF ARG:(LOCAL:0)
			PRINTBUTTON @"{LOCAL:0, 2, RIGHT}[%ARGS:(LOCAL:0)%]", LOCAL:0
		ELSE
			SETCOLOR 0x606060
			PRINTPLAINFORM {LOCAL:0, 2, RIGHT}[%ARGS:(LOCAL:0)%]
			RESETCOLOR
		ENDIF
		PRINTL 
	ENDIF
NEXT
$INPUT_LOOP
INPUT
IF RESULT < 0 || RESULT > 선택지상한 || ARGS:RESULT == "" || !ARG:RESULT
	PRINTW 제시된 숫자에서 선택해주세요.
	CLEARLINE 2
	GOTO INPUT_LOOP
ENDIF
REDRAW LOCAL:0
RETURN RESULT

;-------------------------------------------------
;미독판정 함수 @FIRSTTIME
;식중에서 사용하는 함수입니다.
;ARGS는 이벤트 번호, ARG는 진이라면 첫회 판정을 갱신하지 않는, ARG:1은 캐릭터등록번호(생략 하면(자) TARGET,-1라면 MASTER)
;{이벤트 번호}/　그렇다고 하는 캐릭터라인을 CSTR:0에 처넣어 갑니다
;-------------------------------------------------
@FIRSTTIME(ARGS,ARG, ARG:1)
#FUNCTION
;ARG:1의 변환
ARG:1 = ARG:1 ? ARG:1 # TARGET
ARG:1 = ARG:1 == -1 ? MASTER # ARG:1
SIF CSTR:(ARG:1):0 == ""
	CSTR:(ARG:1):0 = /

LOCALS = /%ARGS%/
IF STRCOUNT(CSTR:(ARG:1):0,LOCALS)
	RETURNF 0
ELSEIF ARG
	RETURNF 1
ENDIF
;첫회 판정 갱신
CSTR:(ARG:1):0 = %CSTR:(ARG:1):0%%ARGS%/
RETURNF 1

;-------------------------------------------------
;악마로 메이드로부터 유용했습니다. 다사.
;단어1/단어2/…/단어 n 의 서식에서 쓰여진 ARGS:0~의 단어의 어떤 것인지를 등 확률로 돌려준다
;(ARGS가 다수 있는 것은 함수등이 돌진해지는 것을 상정하고 있기 (위해)때문에)
;＿(은)는 아무것도 출력하지 않는 선택으로 한다
;선택지의 수를 100까지 확장, RESULT 퇴피 처리 추가
;추첨 처리가 등 확률이라면 FOR-NEXT 사용하지 않아 좋다고 생각했으므로 RAND로 끝마친다
;-------------------------------------------------
@TEXTR(ARGS, ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5, ARGS:6)
#FUNCTIONS
#DIM  L_RESULT               ;RESULT 퇴피용
#DIMS DYNAMIC CHOICES        ;결과
#DIMS DYNAMIC STR_ARRAY, 100 ;선택지 격납용의 일시 변수
L_RESULT = RESULT
;우선 1개(살)로 정리한다
ARGS = %ARGS%\/%ARGS:1%\/%ARGS:2%\/%ARGS:3%\/%ARGS:4%\/%ARGS:5%\/%ARGS:6%
;불필요한"/"을 깎는 작업
;오쿠의 ARGS가 있으면(자)//가 많이 되어있고 판정의 방해가 되므로
;1. //(은)는 치환으로 깎는다
;2. 최말미가/라면 깎는다(SPLIT와 RAND를 그대로 사용하는 형편상)
ARGS '= REPLACE(ARGS, "\/+", "/")
SIF CHARATU(ARGS, STRLENSU(ARGS) - 1) == "/"
	ARGS '= SUBSTRINGU(ARGS, 0, STRLENSU(ARGS) - 1)
SPLIT ARGS, "/", STR_ARRAY
CHOICES '= STR_ARRAY:(RAND:RESULT)
SIF CHOICES == "＿"
	CHOICES = 
RESULT = L_RESULT
RETURNF CHOICES

;-------------------------------------------------------------
;"AAA/BBB" 형태의 문자열을 PRINTL AAA PRINTL BBB 형태로 출력함
;ARGS = 대상 문자열, ARG 0=W 1=L
;-------------------------------------------------------------
@SPLIT_MSG(ARGS, ARG=1)

SPLIT ARGS, "/", LOCALS

FOR LOCAL, 0, RESULT
	IF LOCALS:LOCAL == ""
		CONTINUE
	ELSEIF ARG == 0 || (ARG == 1 && RESULT == LOCAL)
		PRINTFORMW %LOCALS:LOCAL%
	ELSEIF ARG == 1
		PRINTFORML %LOCALS:LOCAL%
	ENDIF
NEXT
;===================================================
;캐릭터를 CSV 초기값으로 바꾸는 함수
;ARG = CSV상 캐릭터 번호
;===================================================
@RESET_CHARA(ARG)
#DIM INGAME_ID

INGAME_ID = GETCHARA(ARG)
;일단 새 캐릭터로 추가
ADDCHARA ARG
;원 캐릭터와 새 캐릭터의 인게임 번호 교체
SWAPCHARA INGAME_ID, CHARANUM
;가장 마지막에 오게 된 원 캐릭터 삭제
DELCHARA CHARANUM

;리셋한 캐릭터의 대사 여부 체크
RESULT = 0
TRYCALLFORM IS_M_No{ARG}
SIF RESULT
	CFLAG:INGAME_ID:개별대사유무 = 1

