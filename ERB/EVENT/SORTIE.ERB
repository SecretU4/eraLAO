[SKIPSTART]
작성시 참고사항

출격은 낮에만 가능
기본적으로는 누구나 가능하지만 정신붕괴시에는 출격 불가
정신붕괴여도 세이렌은 출격가능
목록에는 뜨지만 출격 불가능한 경우
    오드리: etc1>0 && etc1<4
    더치걸: 오드리가 etc1>0 && etc1<4 일때

관련 문자열
    출격 시킬 바이오로이드를 선택해주세요.
    더이상 출격시킬 수 없습니다.
    제시된 숫자에서 선택해주세요.
    이미 출격중인 대상입니다.
    출격 불가능한 상태입니다.
    bio.b_name+'를 출격시키겠습니까?\n\n1. 예\n0. 아뇨

관련 플래그
    FLAG:현출격인수
    EXP:출격경험
    ABL:전투lv
DIM.ERH
    FULL_SORTIE = 5
[SKIPEND]
;===========================================================
;함수명: SORTIE
;출격 기능 메인 함수
;출격대상 = 인게임 캐릭터 ID
;===========================================================
@SORTIE
#DIM 출격대상
출격대상 = -1
IF FLAG:현출격인수 >= FULL_SORTIE
    PRINTL 더이상 출격시킬 수 없습니다.
    RETURN -1
ENDIF
WHILE 1
    CALL CURRENT_BIOLIST("출격")
    출격대상 = RESULT
    IF 출격대상 == -1
        RETURN -1
    ;정신붕괴
    ELSEIF TALENT:출격대상:정신쇠약 == 2
        IF NO:출격대상 != 89
            PRINTFORML %조사처리(NAME:출격대상,"은")% 출격 불가능한 상태입니다.
            CONTINUE
        ENDIF
    ;오드리 더치걸 이벤트중
    ELSEIF GROUPMATCH(NO:출격대상, 106,108)
        IF CFLAG:GETCHARA(106):이벤트카운트1 > 0 && CFLAG:GETCHARA(106):이벤트카운트1 < 4
            PRINTFORML %조사처리(NAME:출격대상,"은")% 출격 불가능한 상태입니다.
            CONTINUE
        ENDIF
    ENDIF
    PRINTFORML %조사처리(NAME:출격대상,"을")% 출격시키겠습니까?
    PRINTL
    CALL ASK_YN("예", "아뇨")
    IF RESULT == 0
        BREAK
    ENDIF
WEND

SIF TARGET == 출격대상
    TARGET = 0

CALL MSG_Sortie(출격대상)

CFLAG:출격대상:출격중 = 1
FLAG:현출격인수 ++
RETURN 0

@DAYEND_SORTIE
#DIM DYNAMIC 출격대상
#DIM DYNAMIC 수입
#DIM DYNAMIC 총수입

FOR 출격대상, 1, CHARANUM
    ;미보유 캐릭터 제외
    SIF ITEM:(ITEM_BIO(NO:출격대상)) < 1
        CONTINUE

    IF CFLAG:출격대상:출격중
        IF (TALENT:출격대상:분류 == 2 && CFLAG:출격대상:출격중 > 2) || CFLAG:출격대상:출격중 > 3 ;기동은 출격일수 1일 감소
            CFLAG:출격대상:출격중 = 0
            EXP:출격대상:출격경험 ++
            EXP:MASTER:출격경험 ++
            CFLAG:출격대상:스트레스 += 20
            FLAG:현출격인수 --

            수입 = SORTIE_INCOME(출격대상)
            MONEY:0 += 수입
            CFLAG:출격대상:출격수입 += 수입
            FLAG:총출격수입 += 수입
            총수입 += 수입

            CALL BattleLV_UP(출격대상)
            SIF RESULT
                TFLAG:전투레벨변동인구 ++

            SIF !FLAG:속박상태
                CALL MSG_Sortie_Return(출격대상, 수입)

        ELSE
            CFLAG:출격대상:출격중 ++
        ENDIF
    ENDIF
NEXT

SIF FLAG:현출격인수 < 0 ;이상상황 처리용
    THROW 현출격인수 플래그가 음수입니다.

RETURN 총수입

; ARG = 인게임 캐릭터 번호
@SORTIE_INCOME(ARG)
#FUNCTION
#DIM 수입

수입 = ITEMPRICE:(ITEM_BIO(NO:ARG)) / 50
수입 += 수입 * RAND(1,99) / 100
SIF TALENT:ARG:정신쇠약 == 2 ;정신붕괴
    수입 = 0
SIF TALENT:ARG:굴복
    TIMES 수입, 2.00
SIF TALENT:ARG:군인
    TIMES 수입, 1.20
SIF TALENT:ARG:근육
    TIMES 수입, 1.30
SIF TALENT:ARG:분류 == 3 ;중장
    TIMES 수입, 1.30
SIF TALENT:ARG:짜증
    TIMES 수입, 0.50
SIF TALENT:ARG:정신쇠약 == 1
    TIMES 수입, 0.50

SELECTCASE ABL:ARG:전투lv
    CASE 0
    CASE 1
        TIMES 수입, 1.50
    CASE 2
        TIMES 수입, 2.00
    CASE 3
        TIMES 수입, 2.50
    CASE 4
        TIMES 수입, 3.00
    CASE 5
        TIMES 수입, 3.50
    CASEELSE
        THROW 상정되지 않은 전투lv {ABL:ARG:전투lv}
ENDSELECT

SELECTCASE ABL:MASTER:전투lv
    CASE 0
    CASE 1
        TIMES 수입, 1.50
    CASE 2
        TIMES 수입, 2.00
    CASE 3
        TIMES 수입, 2.50
    CASE 4
        TIMES 수입, 3.00
    CASE 5
        TIMES 수입, 3.50
    CASEELSE
        THROW 상정되지 않은 전투lv {ABL:MASTER:전투lv}
ENDSELECT

SIF TALENT:MASTER:지휘능력
    TIMES 수입, 1.50

RETURNF 수입

;출격경험 증가로 인한 출격lv 제어 함수
;레벨이 변한 경우 해당 레벨값, 아니면 0 반환
;ARG = 인게임 캐릭터번호
@BattleLV_UP(ARG)

IF ARG != MASTER
    IF EXP:ARG:출격경험 > 4 && ABL:ARG:전투lv < 1
        ABL:ARG:전투lv = 1
        RETURN 1
    ELSEIF EXP:ARG:출격경험 > 24 && ABL:ARG:전투lv < 2
        ABL:ARG:전투lv = 2
        RETURN 2
    ELSEIF EXP:ARG:출격경험 > 64 && ABL:ARG:전투lv < 3
        ABL:ARG:전투lv = 3
        RETURN 3
    ELSEIF EXP:ARG:출격경험 > 114 && ABL:ARG:전투lv < 4
        ABL:ARG:전투lv = 4
        RETURN 4
    ELSEIF EXP:ARG:출격경험 > 184 && ABL:ARG:전투lv < 5
        ABL:ARG:전투lv = 5
        RETURN 5
    ELSE
        RETURN 0
    ENDIF
ELSE ;MASTER 지휘레벨 처리, 지휘능력 취득은 -1 반환인것에 유의
    IF EXP:ARG:출격경험 > 19 && ABL:ARG:전투lv < 1
        ABL:ARG:전투lv = 1
        RETURN 1
    ELSEIF EXP:ARG:출격경험 > 69 && ABL:ARG:전투lv < 2
        ABL:ARG:전투lv = 2
        RETURN 2
    ELSEIF EXP:ARG:출격경험 > 169 && ABL:ARG:전투lv < 3
        ABL:ARG:전투lv = 3
        RETURN 3
    ELSEIF EXP:ARG:출격경험 > 369 && ABL:ARG:전투lv < 4
        ABL:ARG:전투lv = 4
        RETURN 4
    ELSEIF EXP:ARG:출격경험 > 719 && ABL:ARG:전투lv < 5
        ABL:ARG:전투lv = 5
        RETURN 5
    ELSEIF EXP:ARG:출격경험 > 999 && !TALENT:ARG:지휘능력
        TALENT:ARG:지휘능력 = 1
        RETURN -1
    ELSE
        RETURN 0
    ENDIF
ENDIF
