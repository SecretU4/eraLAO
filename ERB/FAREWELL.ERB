[SKIPSTART]
출격중인 바이오로이드는 아예 목록에 출력되지 않음

이별 불가능 캐릭터 조건
    106 etc1>0, etc1<4
    108 106이 etc1>0, etc1<4 일 때
    138 실종 일때
불가능한 캐릭터 선택시 - 이별 불가능한 상태입니다.
[SKIPEND]
@FAREWELL
#DIM CSV_ID
#DIM INGAME_ID
#DIM SELL_PRICE

CALL CURRENT_BIOLIST("이별")
INGAME_ID = RESULT
CSV_ID = NO:RESULT

SELL_PRICE = CACULATE_PRICE(CSV_ID)
MONEYSTR SELL_PRICE

PRINTL 어떻게 하실건가요?
PRINTFORM 판매시 가격: %RESULTS%
WHILE 1
    PRINTL
    PRINTBUTTON @" [판매]", 1
    PRINTL
    PRINTBUTTON @" [폐기]", 2
    PRINTL
    PRINTBUTTON @" [돌아가기]", 0
    PRINTL
    INPUT
    ;판매
    IF RESULT == 1
        CALL SELL_BIO(CSV_ID, SELL_PRICE)
        CALL ENDING_FAREWELL_FLAG(INGAME_ID,"판매")
        BREAK
    ;폐기
    ELSEIF RESULT == 2
        CALL DISPOSAL_BIO(CSV_ID)
        CALL ENDING_FAREWELL_FLAG(INGAME_ID,"폐기")
        BREAK
    ELSEIF RESULT == 0
        RETURN -1
    ENDIF
WEND
TARGET = 0
RETURN RESULT
;ARG = CSV상 캐릭터 번호
@CACULATE_PRICE(ARG)
#FUNCTION
#DIM SELL_PRICE

TARGET = GETCHARA(ARG)
SELL_PRICE = ITEMPRICE:(1000 + ARG) / 2

SELL_PRICE += CFLAG:TARGET:호감도
SELL_PRICE -= CFLAG:TARGET:스트레스
SELL_PRICE -= CFLAG:TARGET:공포 * 2

SIF TALENT:TARGET:연모
    SELL_PRICE = SELL_PRICE * 3 / 2
SIF TALENT:TARGET:사랑
    SELL_PRICE = SELL_PRICE * 3 / 2
SIF TALENT:TARGET:짜증
    SELL_PRICE = SELL_PRICE * 5 / 4
SIF TALENT:TARGET:정신쇠약 == 1 ;정신쇠약
    SELL_PRICE = SELL_PRICE * 5 / 4
SIF TALENT:TARGET:정신쇠약 == 2 ;정신붕괴
    SELL_PRICE = SELL_PRICE / 2

SELECTCASE ABL:TARGET:전투lv
    CASE 0
    CASE 1
        SELL_PRICE += SELL_PRICE / 10
    CASE 2
        SELL_PRICE += SELL_PRICE * 3 / 10
    CASE 3
        SELL_PRICE += SELL_PRICE * 5 / 10
    CASE 4
        SELL_PRICE += SELL_PRICE * 7 / 10
    CASE 5
        SELL_PRICE += SELL_PRICE
    CASEELSE
        THROW 상정 외의 전투lv
ENDSELECT

SELECTCASE ABL:TARGET:조리lv
    CASE 0
    CASE 1
        SELL_PRICE += SELL_PRICE / 10
    CASE 2
        SELL_PRICE += SELL_PRICE * 3 / 10
    CASE 3
        SELL_PRICE += SELL_PRICE * 5 / 10
    CASE 4
        SELL_PRICE += SELL_PRICE * 7 / 10
    CASE 5
        SELL_PRICE += SELL_PRICE
    CASEELSE
        THROW 상정 외의 조리lv
ENDSELECT

SELL_PRICE = MAX(SELL_PRICE, 100)
RETURNF SELL_PRICE

;ARG = CSV상 캐릭터 번호, ARG:1 = SELL_PRICE
@SELL_BIO(ARG, ARG:1)

ITEM:(1000+ARG) = -1
FLAG:현캐릭터수 --
MONEY:0 += ARG:1
CFLAG:판매여부 = 1

MONEYSTR ARG:1
PRINTFORMW %조사처리(NAME:GETCHARA(ARG),"는")% %RESULTS%에 팔렸습니다.
RETURN 1

@DISPOSAL_BIO(ARG)

ITEM:(1000+ARG) = -1
FLAG:현캐릭터수 --
CFLAG:폐기여부 = 1

PRINTFORMW %조사처리(NAME:GETCHARA(ARG),"를")% 폐기 완료했습니다.
RETURN 1
