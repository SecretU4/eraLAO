;==================================
;홈화면 관련
;SYSTEM_CALL 이곳이 메인 화면임
;==================================
@SHOW_SHOP

CALL SHOP_STATUS

CALLF SHOW_SHOP_MENU

@SHOW_SHOP_MENU
#FUNCTION
PRINTL - 메인 메뉴 -
SIF TARGET
    PRINTLC [100] 연애
PRINTLC [101] 상태보기
PRINTLC [102] 휴식
IF !FLAG:속박상태
    SIF TIME == 0;낮
        PRINTLC [103] 출격
    PRINTLC [104] 대상변경
    PRINTLC [105] 상점
ENDIF
PRINTLC [106] 요리
IF !FLAG:속박상태
    SIF TIME == 0,;낮
        PRINTLC [107] 헤어진다
    PRINTLC [108] 사령관 이름 변경
ENDIF
PRINTL

PRINTL
IF DAY >= 30 && !FLAG:속박상태
    PRINTFORML - 연장점검 {DAY}일째 -
ELSE
    PRINTL - 서버 점검중 -
ENDIF
SETCOLOR 125, 125, 125
PRINTLC 랭킹 보기
RESETCOLOR
PRINTL

PRINTL
PRINTL - 오프라인 모드 -
PRINTLC [200] SAVE
PRINTLC [300] LOAD
PRINTL


PRINTL
PRINTL - 시스템 콘솔 -
PRINTLC [500] 처음부터
SIF GAMEBASE_VERSION != FLAG:LAST_LAOVERSION
    SETCOLOR 255,0,0
PRINTLC [600] UPDATE
RESETCOLOR
PRINTLC [999] 종료
;==================================
;SYSTEM_CALL @SHOW_SHOP 이후 넘어옴
;==================================
@USERSHOP
SELECTCASE RESULT
    ;연애
    CASE 100
        IF TARGET
            BEGIN TRAIN
            RETURN 1
        ENDIF
    ;상태보기
    CASE 101
        CALL SEE_BIOSTATUS
        RETURN 1
    ;휴식
    CASE 102
        IF NO:(FLAG:히든메시지) == 7 ; 리제 히든엔딩시 휴식도 연애로 이동
            BEGIN TRAIN
            RETURN 1
        ENDIF
        CALL COMMON_EVENT
        SIF RESULT == 0
            PRINTL 작동된 메시지가 없었습니다.
        RETURN 1
    ;출격
    CASE 103
        IF FLAG:속박상태           
            CALL MSG_SHOP_HIDDEN(103)
            RETURN -1
        ELSEIF TIME == 1
            RETURN -1
        ENDIF
        CALL SORTIE
        RETURN 1
    ;대상변경
    CASE 104
        IF FLAG:속박상태
            CALL MSG_SHOP_HIDDEN(104)
            RETURN -1
        ENDIF
        PRINTL
        CALL CURRENT_BIOLIST("대상변경")
        TARGET = RESULT
        SIF TARGET == -1
            TARGET = 0
        RETURN 1
    ;상점
    CASE 105
        IF FLAG:속박상태
            CALL MSG_SHOP_HIDDEN(105)
            RETURN -1
        ENDIF
        CALL MAIN_SHOP
        RETURN 1
    ;요리
    ;정상 실행시 바로 TURNEND이므로 여기 오는 경우는 미실행시뿐임.
    CASE 106
        CALL COOK
        RETURN 0
    ;헤어진다
    CASE 107
        IF FLAG:속박상태
            CALL MSG_SHOP_HIDDEN(107)
            RETURN -1
        ELSEIF TIME == 1
            RETURN -1
        ENDIF
        CALL FAREWELL
        RETURN 1
    ;사령관 이름 변경
    CASE 108
        IF FLAG:속박상태
            CALL MSG_SHOP_HIDDEN(108)
            RETURN -1
        ENDIF
        CALL COMMANDER_NAMESET
        RETURN 1
    ;SAVE
    CASE 200
        CALL SAVEGAME_EX
    ;LOAD
    CASE 300
        CALL LOADGAME_EX
    ;처음부터
    CASE 500
        PRINTL 정말로 처음부터 시작하시겠습니까?
        CALL ASK_YN
        IF RESULT == 0
            CALL NEWGAME
        ENDIF
    ;UPDATE
    CASE 600
        CALL UPDATE
    CASE 999
        PRINTL 게임을 종료합니다.
        PRINTW 엔터 키를 눌러 계속...
        QUIT
    ;개별디버그(debug)
    CASE 1048596
        IF TFLAG:99 == 1130209 || TFLAG:99 == 1048596
            TFLAG:99 = 1048596
            CALL EPC_DEBUG
            RETURN RESULT
        ELSE
            PRINTW 제시된 숫자를 입력해주세요.
        ENDIF
    CASEELSE
        SIF RESULT == 1130209
            TFLAG:99 = 1130209
        PRINTW 제시된 숫자를 입력해주세요.
ENDSELECT
