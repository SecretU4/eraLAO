;SYSTEM_CALL
;BEGIN AFTERTRAIN 이후 호출
@EVENTEND
CSTR:1 '= SET_MSG_CSTR(TARGET)

REPEAT 10
    TRYCALLFORM M_No{NO:TARGET}_TRAIN_END
    IF RESULT == 0
        CSTR:1 '= Alternative_CSTR(TARGET)
        SIF COUNT == 9
            DEBUGPRINTFORML 조건(%CSTR:1%)에 맞는 %CALLNAME:TARGET%의 커맨드 지문이 발견되지 않음
    ELSE
        BREAK
    ENDIF
REND

;일반적인 에라가 아니라서 통과
;BEGIN ABLUP
CALL ABL_CHECK

;턴수 초기화
TIME:2 = 0

PRINTL

BEGIN TURNEND

;에라엔진의 ABLUP 대용
;수치에 따른 캐릭터 기능 취득 처리
@ABL_CHECK

;정신쇠약/연모 아니면서 호감도 1000 이상, 공포 300 이상시 굴복 취득
CALL GET_Submit(TARGET)
IF RESULT == 1 || RESULT == 2
    PRINTFORML %타겟은% 당신에게 절대적인 패배를 인정했습니다.
    PRINTPLAIN [굴복]
    PRINTW 을 취득했다.
    IF RESULT == 2
        PRINTFORML %타겟은% 모든 것을 포기했습니다.
        PRINTPLAIN [억압]
        PRINTW 이 소멸했다.
    ENDIF
ENDIF

;공포 299 초과시 정신쇠약 취득
CALL GET_WeakMind(TARGET)
IF RESULT == 1
    PRINTFORML %CALLNAME:TARGET%의 정신이 쇠약해진 것 같다.
    PRINTS 타겟은
    PRINTPLAIN [정신쇠약]
    PRINTW 을 취득했다.
ENDIF

;에밀리 제외 공포 800 초과시 정신붕괴 취득
CALL GET_MindBreak(TARGET)
IF RESULT == 1
    PRINTFORML %CALLNAME:TARGET%의 정신이 망가진 것 같다.
    PRINTS 타겟은
    PRINTPLAIN [정신붕괴]
    PRINTW 를 취득했다.
ENDIF

;스트레스 299 초과시 짜증 취득, 200 미만시 짜증 소멸
CALL GET_Annoyed(TARGET)
IF RESULT == 1
    PRINTFORML %타겟은% 진절머리가 난 것 같다.
    PRINTS 타겟은
    PRINTPLAIN [짜증]
    PRINTW 을 취득했다.
ELSEIF RESULT == -1
    PRINTFORML %타겟은% 화가 풀린 것 같다.
    PRINTFORM %CALLNAME:TARGET%의
    PRINTPLAIN [짜증]
    PRINTW 이 소멸했다.
ENDIF

;짜증 아님, 호감도 999 초과, 공포 100 미만, 정신쇠약 아닐때 연모
CALL GET_Affection(TARGET)
IF RESULT == 1 || RESULT == 2
    PRINTFORML %타겟은% 당신에게 특별한 감정을 느낀 것 같다.
    PRINTS 타겟은
    PRINTPLAIN [연모]
    PRINTW 를 취득했다.
    IF RESULT == 2
        PRINTFORML %타겟을% 속박하고 있던 무언가가 풀렸습니다.
        PRINTFORM %CALLNAME:TARGET%의
        PRINTPLAIN [억압]
        PRINTW 이 소멸했다.
    ENDIF
ELSE
    ;연모/사랑 조건 동시 충족시 연모가 우선
    ;짜증 아님, 공포 50 미만, 정신쇠약 아닐때가 사랑 최저조건
    ;에밀리는 호감도가 7999 초과이기만 하면 됨
    ;나머지는 호감도 9999 초과
    CALL GET_Love(TARGET)
    IF RESULT == 1 || RESULT == 2
        PRINTFORML %타겟은% 당신에게 특별한 감정을 느낀 것 같다.
        PRINTS 타겟은
        PRINTPLAIN [사랑]
        PRINTW 을 취득했다.
        IF RESULT == 2
            PRINTFORM %CALLNAME:TARGET%의
            PRINTPLAIN [비관적]
            PRINTW 이 소멸했다.
        ENDIF
    ENDIF
ENDIF


;고백받은인수 5명 초과시 매력 취득
IF FLAG:고백받은인수 > 5 && !TALENT:MASTER:매력
    TALENT:MASTER:매력 = 1
    PRINTPLAIN 당신은 [매력]
    PRINTW 을 얻었다
ENDIF

;=============================================================
;각종 능력의 획득/소멸 관련 함수
;안에서 조건 확인 후 처리하기에 특수케이스는 따로 작성이 필요
;처리되었다면 1(획득/소멸이라면 각 1,-1), 아니라면 0 반환
;ARG = 인게임 캐릭터번호
;-------------------------------------------------------------
;v1.20 현재 본 함수군을 사용하는 위치 목록
;@EVENTEND - 연애커맨드/종료 대응
;@EVENTTURNEND - 날짜변경/(uptime1|uptime2) 대응
;@EAT_FOOD - 식사시 호감도 상승분 처리
;=============================================================
;-------------------------------------------------------------
;정신쇠약 취득
;공포 299 초과시
;굴복시에는 취득 안됨
;-------------------------------------------------------------
@GET_WeakMind(ARG)

IF !TALENT:ARG:정신쇠약 && CFLAG:ARG:공포 > 299
    SIF TALENT:ARG:굴복
        RETURN 0
    TALENT:ARG:정신쇠약 = 1
    EXP:ARG:쇠약획득경험 ++
    RETURN 1
ENDIF
RETURN 0
;-------------------------------------------------------------
;정신붕괴 취득
;이미 정신쇠약 상태, 공포 799 초과시
;획득시 호감도, 공포, 스트레스가 0이 된다.
;-------------------------------------------------------------
@GET_MindBreak(ARG)

IF TALENT:ARG:정신쇠약 == 1 && CFLAG:ARG:공포 > 799
    SELECTCASE NO:ARG
        CASE 68
            SIF TALENT:ARG:사랑 == 2;에밀리는 사랑이면 제외
                RETURN 0
        CASE 118
            SIF !TALENT:ARG:사랑 ;LRL은 연모 없으면 제외
                RETURN 0
    ENDSELECT
    TALENT:ARG:정신쇠약 = 2
    CFLAG:ARG:호감도 = 0
    CFLAG:ARG:스트레스 = 0
    CFLAG:ARG:공포 = 0
    RETURN 1
ENDIF
RETURN 0
;-------------------------------------------------------------
;짜증 취득/소멸
;스트레스 299 초과시 짜증 취득, 200 미만시 짜증 소멸
;굴복시에는 취득 안됨
;-------------------------------------------------------------
@GET_Annoyed(ARG)

IF !TALENT:ARG:짜증 && CFLAG:ARG:스트레스 > 299 && !TALENT:ARG:굴복
    TALENT:ARG:짜증 = 1
    EXP:ARG:짜증획득경험 ++
    RETURN 1
ELSEIF TALENT:ARG:짜증 && CFLAG:ARG:스트레스 < 200
    TALENT:ARG:짜증 = 0
    EXP:ARG:짜증소멸경험 ++
    RETURN -1
ENDIF
RETURN 0
;-------------------------------------------------------------
;연모 취득
;짜증/정신쇠약/굴복 아님, 호감도 999 초과, 공포 100 미만일 때 연모
;억압 처리시 2 반환
;-------------------------------------------------------------
@GET_Affection(ARG)

SIF TALENT:ARG:짜증 || TALENT:ARG:정신쇠약 || TALENT:ARG:굴복
    RETURN 0
IF !TALENT:ARG:사랑 && CFLAG:ARG:호감도 > 999 && CFLAG:ARG:공포 < 100
    TALENT:ARG:사랑 = 1
    FLAG:연모인수 ++
    IF TALENT:ARG:억압 ;억압 소멸
        TALENT:ARG:억압 = 0
        RETURN 2
    ELSE
        RETURN 1
    ENDIF
ENDIF
RETURN 0
;-------------------------------------------------------------
;사랑 취득
;짜증 아님, 공포 50 미만, 정신쇠약 아닐때가 사랑 최저조건
;연모 있어야 사랑 취득 가능
;에밀리는 호감도가 7999 초과이기만 하면 됨
;나머지는 호감도 9999 초과
; * 연모/사랑 조건 동시 충족시 연모가 우선
;비관적 처리시 2 반환
;-------------------------------------------------------------
@GET_LOVE(ARG)

SIF TALENT:ARG:짜증 || TALENT:ARG:정신쇠약
    RETURN 0
IF TALENT:ARG:사랑 == 1 && CFLAG:ARG:공포 < 50
    ;에밀리는 호감도가 7999 초과이기만 하면 됨
    IF NO:ARG == 68 && CFLAG:ARG:호감도 > 7999
        TALENT:사랑 = 2
        RETURN 1
    ;나머지는 호감도 9999 초과
    ELSEIF CFLAG:ARG:호감도 > 9999
        TALENT:ARG:사랑 = 2
        IF TALENT:ARG:긍정도 == -1 ;비관적 소멸
            TALENT:ARG:긍정도 = 0
            RETURN 2
        ELSE
            RETURN 1
        ENDIF
    ENDIF
ENDIF
RETURN 0
;-------------------------------------------------------------
;굴복 취득
;연모 미보유, 쇠약 미보유, 호감도 1000이상, 공포 300이상
;v1.20 현재 턴종료시에만 체크, 113(켈베로스)에만 실장됨.
;이후 종의 징표를 사용해 복종 취득 가능
;억압 처리시 2 반환
;-------------------------------------------------------------
@GET_Submit(ARG)
;이 아래 SIF는 추후 굴복 범용화시 삭제
SIF ARG != GETCHARA(113) ;켈베로스가 아니라면 제외
    RETURN 0

SIF TALENT:ARG:사랑 || TALENT:ARG:정신쇠약 || TALENT:ARG:굴복
    RETURN 0
IF CFLAG:ARG:호감도 >= 1000 && CFLAG:ARG:공포 >= 300
    TALENT:ARG:굴복 = 1
    IF TALENT:ARG:억압 ;억압 소멸
        TALENT:ARG:억압 = 0
        RETURN 2
    ELSE
        RETURN 1
    ENDIF
ENDIF
RETURN 0
