;SYSTEM_CALL
;BEGIN AFTERTRAIN 이후 호출
@EVENTEND

CALL SET_MSG_CSTR(TARGET)
WHILE 1
    TRYCALLFORM M_No{NO:TARGET}_TRAIN_END
    IF RESULT == 0
        CSTR:1 = Alternative_CSTR(TARGET)
    ELSE
        BREAK
    ENDIF
WEND

;일반적인 에라가 아니라서 통과
;BEGIN ABLUP
CALL ABL_CHECK

;턴수 초기화
TIME:2 = 0

PRINTL

BEGIN TURNEND

;에라엔진의 ABLUP 대용
;수치에 따른 캐릭터 기능 취득 처리
@ABL_CHECK

;공포 299 초과시 정신쇠약 취득
CALL GET_WeakMind(TARGET)
IF RESULT == 1
    PRINTFORML %CALLNAME:TARGET%의 정신이 쇠약해진 것 같다.
    PRINTS 타겟은
    PRINTPLAIN [정신쇠약]
    PRINTW 을 취득했다.
ENDIF

;에밀리 제외 공포 800 초과시 정신붕괴 취득
CALL GET_MindBreak(TARGET)
IF RESULT == 1
    PRINTFORML %CALLNAME:TARGET%의 정신이 망가진 것 같다.
    PRINTS 타겟은
    PRINTPLAIN [정신붕괴]
    PRINTW 를 취득했다.
ENDIF

;스트레스 299 초과시 짜증 취득, 200 미만시 짜증 소멸
CALL GET_Annoyed(TARGET)
IF RESULT == 1
    PRINTFORML %타겟은% 진절머리가 난 것 같다.
    PRINTS 타겟은
    PRINTPLAIN [짜증]
    PRINTW 을 취득했다.
ELSEIF RESULT == -1
    PRINTFORML %타겟은% 화가 풀린 것 같다.
    PRINTFORM %CALLNAME:TARGET%의
    PRINTPLAIN [짜증]
    PRINTW 이 소멸했다.
ENDIF

;짜증 아님, 호감도 999 초과, 공포 100 미만, 정신쇠약 아닐때 연모
CALL GET_Affection(TARGET)
IF RESULT == 1 || RESULT == 2
    PRINTFORML %타겟은% 당신에게 특별한 감정을 느낀 것 같다.
    PRINTS 타겟은
    PRINTPLAIN [연모]
    PRINTW 를 취득했다.
    IF RESULT == 2
        PRINTFORML %타겟을% 속박하고 있던 무언가가 풀렸습니다.
        PRINTFORM %CALLNAME:TARGET%의
        PRINTPLAIN [억압]
        PRINTW 이 소멸했다.
    ENDIF
ELSE
    ;연모/사랑 조건 동시 충족시 연모가 우선
    ;짜증 아님, 공포 50 미만, 정신쇠약 아닐때가 사랑 최저조건
    ;에밀리는 호감도가 7999 초과이기만 하면 됨
    ;나머지는 호감도 9999 초과
    CALL GET_Love(TARGET)
    IF RESULT == 1 || RESULT == 2
        PRINTFORML %타겟은% 당신에게 특별한 감정을 느낀 것 같다.
        PRINTS 타겟은
        PRINTPLAIN [사랑]
        PRINTW 을 취득했다.
        IF RESULT == 2
            PRINTFORM %CALLNAME:TARGET%의
            PRINTPLAIN [비관적]
            PRINTW 이 소멸했다.
        ENDIF
    ENDIF
ENDIF


;고백받은인수 5명 초과시 매력 취득
IF FLAG:고백받은인수 > 5 && !TALENT:MASTER:매력
    TALENT:MASTER:매력 = 1
    PRINTPLAIN 당신은 [매력]
    PRINTW 을 얻었다
ENDIF

;=============================================================
;각종 능력의 획득/소멸 관련 함수
;안에서 조건 확인 후 처리하기에 특수케이스는 따로 작성이 필요
;처리되었다면 1(획득/소멸이라면 각 1,-1), 아니라면 0 반환
;ARG = 인게임 캐릭터번호
;-------------------------------------------------------------
;v0.9 현재 본 함수군을 사용하는 위치 목록
;@EVENTEND - 연애커맨드/종료 대응
;@EVENTTURNEND - 날짜변경/(uptime1|uptime2) 대응
;@EAT_FOOD - 식사시 호감도 상승분 처리
;=============================================================
;-------------------------------------------------------------
;정신쇠약 취득
;공포 299 초과시
;-------------------------------------------------------------
@GET_WeakMind(ARG)

IF !TALENT:ARG:정신쇠약 && CFLAG:ARG:공포 > 299
    TALENT:ARG:정신쇠약 = 1
    SIF NO:ARG == 51 && CFLAG:ARG:etc3 < 5 ;etc3 = 메이 쇠약 획득수
        CFLAG:ARG:etc3 ++
    RETURN 1
ENDIF
RETURN 0
;-------------------------------------------------------------
;정신붕괴 취득
;공포 799 초과시
;획득시 호감도, 공포, 스트레스가 0이 된다.
;-------------------------------------------------------------
@GET_MindBreak(ARG)

IF !TALENT:ARG:정신쇠약 != 2 && CFLAG:ARG:공포 > 799
    SIF NO:ARG == 68 && TALENT:ARG:사랑 == 2;에밀리는 사랑이면 제외
        RETURN 0
    TALENT:ARG:정신쇠약 = 2
    CFLAG:ARG:호감도 = 0
    CFLAG:ARG:스트레스 = 0
    CFLAG:ARG:공포 = 0
    RETURN 1
ENDIF
RETURN 0
;-------------------------------------------------------------
;짜증 취득/소멸
;스트레스 299 초과시 짜증 취득, 200 미만시 짜증 소멸
;-------------------------------------------------------------
@GET_Annoyed(ARG)

IF !TALENT:ARG:짜증 && CFLAG:ARG:스트레스 > 299
    TALENT:ARG:짜증 = 1
    RETURN 1
ELSEIF TALENT:ARG:짜증 && CFLAG:ARG:스트레스 < 200
    TALENT:ARG:짜증 = 0
    RETURN -1
ENDIF
RETURN 0
;-------------------------------------------------------------
;연모 취득
;짜증 아님, 호감도 999 초과, 공포 100 미만, 정신쇠약 아닐때 연모
;억압 처리시 2 반환
;-------------------------------------------------------------
@GET_Affection(ARG)

SIF TALENT:ARG:짜증 || TALENT:ARG:정신쇠약
    RETURN 0
IF !TALENT:ARG:사랑 && CFLAG:ARG:호감도 > 999 && CFLAG:ARG:공포 < 100
    TALENT:ARG:사랑 = 1
    IF TALENT:ARG:억압 ;억압 소멸
        TALENT:ARG:억압 = 0
        RETURN 2
    ELSE
        RETURN 1
    ENDIF
ENDIF
RETURN 0
;-------------------------------------------------------------
;사랑 취득
;짜증 아님, 공포 50 미만, 정신쇠약 아닐때가 사랑 최저조건
;연모 있어야 사랑 취득 가능
;에밀리는 호감도가 7999 초과이기만 하면 됨
;나머지는 호감도 9999 초과
; * 연모/사랑 조건 동시 충족시 연모가 우선
;비관적 처리시 2 반환
;-------------------------------------------------------------
@GET_LOVE(ARG)

SIF TALENT:ARG:짜증 || TALENT:ARG:정신쇠약 || !TALENT:ARG:사랑
    RETURN 0
IF TALENT:ARG:사랑 != 2 && CFLAG:ARG:공포 < 50
    ;에밀리는 호감도가 7999 초과이기만 하면 됨
    IF NO:ARG == 68 && CFLAG:ARG:호감도 > 7999
        TALENT:사랑 = 2
        RETURN 1
    ;나머지는 호감도 9999 초과
    ELSEIF CFLAG:ARG:호감도 > 9999
        TALENT:ARG:사랑 = 2
        IF TALENT:ARG:긍정도 == -1 ;비관적 소멸
            TALENT:ARG:긍정도 = 0
            RETURN 2
        ELSE
            RETURN 1
        ENDIF
    ENDIF
ENDIF
RETURN 0
