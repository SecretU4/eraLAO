;SYSTEM CALL
;필터링, 속박상태 대응 등을 위해 시스템의 PRINT TRAIN 기능을 사용하지 않고 이곳에서 처리
@SHOW_USERCOM
TFLAG:연애중여부 = 1
FOR LOCAL, 0, 200
    CALL TOTAL_COMABLE(LOCAL)
    IF !RESULT
        CONTINUE
    ;개별 캐릭터의 특정 커맨드번호가 다른 경우
    ELSEIF COM_ALTER_CHECK(TARGET, LOCAL)
        PRINTBUTTONLC @"[{LOCAL, 3}] %COM_ALTER_NAME(LOCAL)%", COM_ALTER_CHECK(TARGET, LOCAL)
        CONTINUE
    ENDIF
    ;일반적인 경우
    IF STRLENS(TRAINNAME:LOCAL)
        PRINTFORMLC [{LOCAL, 3}] %TRAINNAME:LOCAL%
    ENDIF
NEXT
PRINTL
DRAWLINE
PRINTL [999] 종료한다

@USERCOM
#DIM 선택커맨드
;연애 시작 고백시 여기서 처리
IF TFLAG:고백함
    CFLAG:고백함 = TFLAG:고백함
    TFLAG:고백함 = 0
    FLAG:고백받은인수 ++
ENDIF
선택커맨드 = RESULT
선택커맨드 = COM_ALTER_CHECK(TARGET, 선택커맨드)

CALL COM_Misery_FLAG
SIF RESULT != -1
    선택커맨드 = RESULT

SELECTCASE 선택커맨드
    CASE 999
        BEGIN AFTERTRAIN
    CASEELSE
        CALL TOTAL_COMABLE(선택커맨드)
        TFLAG:연애중여부 = 0
        SIF !RESULT
            RETURN

        DOTRAIN 선택커맨드
ENDSELECT
;SYSTEM_CALL
;COM 선택시
@EVENTCOM

CALLFORM COM{SELECTCOM}_MSG

;SYSTEM_CALL
;SOURCE_CHECK 종료시
@EVENTCOMEND

;횟수 오버시 강제 탈출
SIF TIME:2 >= TIMELIMIT
    BEGIN AFTERTRAIN

;커맨드 실행 가능 여부 전역처리 함수
@TOTAL_COMABLE(ARG)
SIF !STRLENS(TRAINNAME:ARG)
    RETURN 0

;개별커맨드 여부 처리
LOCAL = COM_ALTER_CHECK(TARGET, ARG)
SIF LOCAL
    ARG = LOCAL

TRYCALLFORM COM_ABLE{ARG}
IF RESULT
    RETURN 1
ELSE
    RETURN 0
ENDIF
;속박상태용 선택지 랜덤 선출
@COM_Misery_FLAG
#DIM 선택지 = -1
IF FLAG:속박상태
    PRINTW 당신은 묶여있어 움직일 수 없다.
    SELECTCASE NO:(FLAG:속박상태)
        CASE 7 ;리제 ;정해진 커맨드 중 하나 랜덤 실행
            SELECTCASE RAND:10
                CASE 0 ;잡담
                    선택지 = 2
                CASE 1 ;야한농담
                    선택지 = 3
                CASE 2 ;손을잡는다
                    선택지 = 4
                CASE 3 ;머리쓰다듬기
                    선택지 = 5
                CASE 4 ;포옹한다
                    선택지 = 9
                CASE 5 ;뽀뽀한다
                    선택지 = 12
                CASE 6 ;귀를깨문다
                    선택지 = 14
                CASE 7 ;키스
                    선택지 = 15
                CASE 8 ;목덜미핥기
                    선택지 = 16
                CASE 9 ;파후파후
                    선택지 = 18
            ENDSELECT
    ENDSELECT
ENDIF
RETURN 선택지

;COM 어레인지의 원본 번호가 필요한 경우 사용
;ARG = 원 선택지번호
;원본 번호가 있다면 해당 번호를, 아니라면 -1 반환
@COM_ALTER_SELECT_Rev(C_ID, ARG)
#FUNCTION
#DIM C_ID ;인게임 캐릭터번호

SELECTCASE NO:C_ID
    CASE 108 ;더치 걸
        SELECTCASE ARG
            CASE 201 ;안마부탁
                RETURNF 13
            CASE 202 ;볼만지기
                RETURNF 14
            CASE 203 ;이마맞대기
                RETURNF 15
            CASE 204 ;머리빗어주기
                RETURNF 16
            CASE 205 ;발마사지
                RETURNF 18
        ENDSELECT
    CASE 118 ;LRL
        SELECTCASE ARG
            CASE 206 ;귀에바람
                RETURNF 14
            CASE 207 ;안대당기기
                RETURNF 15
            CASE 208 ;볼비비기
                RETURNF 16
            CASE 209 ;볼꼬집기
                RETURNF 17
            CASE 210 ;꿀밤
                RETURNF 18
        ENDSELECT
ENDSELECT
RETURNF -1

;ARG = 선택한 커맨드(SELECTCOM)
;해당 커맨드의 어레인지가 있다면 해당 어레인지의 번호를 반환, 없다면 0 반환
@COM_ALTER_CHECK(C_ID, ARG)
#FUNCTION
#DIM C_ID ;인게임 캐릭터번호

FOR LOCAL, 201, 300 ;개별COM 범위
    SIF COM_ALTER_SELECT_Rev(C_ID, LOCAL) == ARG
        RETURNF LOCAL
NEXT
RETURNF 0

;ARG = 선택한 커맨드(SELECTCOM)
;해당 커맨드번호의 다른 어레인지가 있다면 해당 어레인지의 이름을 반환함
@COM_ALTER_NAME(ARG)
#FUNCTIONS

REPLACE TALENTNAME:ARG, "_", " "

RETURNF RESULTS
