;SYSTEM CALL
;필터링, 속박상태 대응 등을 위해 시스템의 PRINT TRAIN 기능을 사용하지 않고 이곳에서 처리
@SHOW_USERCOM
TFLAG:연애중여부 = 1
FOR LOCAL, 0, 200
    CALL TOTAL_COMABLE(LOCAL)
    IF !RESULT
        CONTINUE
    ELSEIF GROUPMATCH(NO:TARGET, 108)
        SELECTCASE NO:TARGET
            CASE 108
                SELECTCASE LOCAL
                    CASE 13
                        PRINTBUTTONLC @"[{LOCAL, 3}] 안마를 부탁한다", COM_ALTER_SELECT(LOCAL)
                        CONTINUE
                    CASE 14
                        PRINTBUTTONLC @"[{LOCAL, 3}] 볼을 만진다", COM_ALTER_SELECT(LOCAL)
                        CONTINUE
                    CASE 15
                        PRINTBUTTONLC @"[{LOCAL, 3}] 이마를 맞댄다", COM_ALTER_SELECT(LOCAL)
                        CONTINUE
                    CASE 16
                        PRINTBUTTONLC @"[{LOCAL, 3}] 머리를 빗어준다", COM_ALTER_SELECT(LOCAL)
                        CONTINUE
                    CASE 18
                        PRINTBUTTONLC @"[{LOCAL, 3}] 발 마사지를 해준다", COM_ALTER_SELECT(LOCAL)
                        CONTINUE
                    ;실제 커맨드번호는 표시 안하고 통과
                    CASE 90, 91, 92, 93, 94
                        CONTINUE
                ENDSELECT
        ENDSELECT
    ENDIF
    ;일반적인 경우
    IF STRLENS(TRAINNAME:LOCAL)
        PRINTFORMLC [{LOCAL, 3}] %TRAINNAME:LOCAL%
    ENDIF
NEXT
PRINTL
DRAWLINE
PRINTL [999] 종료한다

@USERCOM
#DIM 선택커맨드
;연애 시작 고백시 여기서 처리
IF TFLAG:고백함
    CFLAG:고백함 = TFLAG:고백함
    TFLAG:고백함 = 0
    FLAG:고백받은인수 ++
ENDIF
선택커맨드 = RESULT
선택커맨드 = COM_ALTER_SELECT(선택커맨드)

CALL COM_Misery_FLAG
SIF RESULT != -1
    선택커맨드 = RESULT

SELECTCASE 선택커맨드
    CASE 999
        BEGIN AFTERTRAIN
    CASEELSE
        CALL TOTAL_COMABLE(선택커맨드)
        TFLAG:연애중여부 = 0
        SIF !RESULT
            RETURN

        DOTRAIN 선택커맨드
ENDSELECT
;SYSTEM_CALL
;COM 선택시
@EVENTCOM

CALLFORM COM{SELECTCOM}_MSG

;SYSTEM_CALL
;SOURCE_CHECK 종료시
@EVENTCOMEND

;횟수 오버시 강제 탈출
SIF TIME:2 >= TIMELIMIT
    BEGIN AFTERTRAIN

;커맨드 실행 가능 여부 전역처리 함수
@TOTAL_COMABLE(ARG)
SIF !STRLENS(TRAINNAME:ARG)
    RETURN 0
SIF COM_ALTER_SELECT(ARG) != ARG
    ARG = COM_ALTER_SELECT(ARG)

TRYCALLFORM COM_ABLE{ARG}
IF RESULT
    RETURN 1
ELSE
    RETURN 0
ENDIF
;속박상태용 선택지 랜덤 선출
@COM_Misery_FLAG
#DIM 선택지 = -1
IF FLAG:속박상태
    PRINTW 당신은 묶여있어 움직일 수 없다.
    SELECTCASE NO:(FLAG:속박상태)
        CASE 7 ;리제 ;정해진 커맨드 중 하나 랜덤 실행
            SELECTCASE RAND:10
                CASE 0 ;잡담
                    선택지 = 2
                CASE 1 ;야한농담
                    선택지 = 3
                CASE 2 ;손을잡는다
                    선택지 = 4
                CASE 3 ;머리쓰다듬기
                    선택지 = 5
                CASE 4 ;포옹한다
                    선택지 = 9
                CASE 5 ;뽀뽀한다
                    선택지 = 12
                CASE 6 ;귀를깨문다
                    선택지 = 14
                CASE 7 ;키스
                    선택지 = 15
                CASE 8 ;목덜미핥기
                    선택지 = 16
                CASE 9 ;파후파후
                    선택지 = 18
            ENDSELECT
    ENDSELECT
ENDIF
RETURN 선택지

;캐릭터에 따라 같은 번호 다른 선택지가 주어지는 경우
;ARG = 원 선택지번호
@COM_ALTER_SELECT(ARG)
#FUNCTION
SELECTCASE NO:TARGET
    CASE 108 ;더치 걸
        SELECTCASE ARG
            CASE 13
                RETURNF 90
            CASE 14
                RETURNF 91
            CASE 15
                RETURNF 92
            CASE 16
                RETURNF 93
            CASE 18
                RETURNF 94
        ENDSELECT
ENDSELECT
RETURNF ARG

;COM_ALTER_SELECT의 원본값이 필요한 경우 사용
;ARG = 원 선택지번호
@COM_ALTER_SELECT_Rev(ARG)
#FUNCTION
SELECTCASE NO:TARGET
    CASE 108 ;더치 걸
        SELECTCASE ARG
            CASE 90
                RETURNF 13
            CASE 91
                RETURNF 14
            CASE 92
                RETURNF 15
            CASE 93
                RETURNF 16
            CASE 94
                RETURNF 18
        ENDSELECT
ENDSELECT
RETURNF ARG