;----------------------------------------------
;함수명: SET_MSG_CSTR(ARG, ARG:1 = 0)
;원본의 line 세팅과 같음
;메시지 기본 조건절 처리
;ARG = 인게임 캐릭터번호, ARG:1 = 태그 탐색불가시 1
;----------------------------------------------
@SET_MSG_CSTR(ARG, ARG:1 = 0)
#FUNCTIONS
#DIMS LINES

IF TALENT:ARG:백치
    LINES '= "regress"
ELSEIF TALENT:ARG:정신쇠약 == 2
    LINES '= "Break"
ELSEIF TALENT:ARG:정신쇠약 == 1
    LINES '= "Weak"
ELSEIF TALENT:ARG:혼인
    LINES '= "Marryed"
ELSEIF TALENT:ARG:사랑 == 2
    LINES '= "Love"
ELSEIF TALENT:ARG:혼인 == 1 || TALENT:ARG:사랑
    LINES '= "Affection"
ELSEIF !ARG:1 && TALENT:ARG:굴복
    LINES '= "Submit"
ELSEIF CFLAG:ARG:공포 > 199
    LINES '= "Fear"
ELSEIF TALENT:ARG:짜증
    LINES '= "Annoyed"
ELSEIF CFLAG:ARG:호감도 > 499
    LINES '= "High"
ELSEIF CFLAG:ARG:호감도 < 500
    LINES '= "Low"
ELSE
    LINES '= 
ENDIF

;개별 캐릭터 중 대부분의 경우 포함하게 되어있는 특수태그 입력
SELECTCASE NO:ARG
    CASE 7 ;리제
        SIF FLAG:히든메시지
            LINES '= "Hidden"
    CASE 68 ;에밀리
        IF TALENT:ARG:사랑 == 2
            IF TALENT:ARG:정신쇠약 == 1 ;범용 LINES = Weak
                LINES '= "Weak2"
            ELSEIF CFLAG:ARG:공포 > 199
                LINES '= "Fear2"
            ELSEIF TALENT:ARG:짜증
                LINES '= "Annoyed2"
            ENDIF
        ENDIF
    CASE 118 ;LRL
        IF TALENT:ARG:사랑 ;원본은 연모만
            IF TALENT:ARG:정신쇠약 == 1
                LINES '= "Weak2"
            ELSEIF CFLAG:ARG:공포 > 199
                LINES '= "Fear2"
            ENDIF
        ENDIF
ENDSELECT

RETURNF LINES
;================================================
;함수명: EVENTTRAIN
;SYSTEM_CALL
;연애 커맨드 진입시 (BEGIN TRAIN)
;================================================
@EVENTTRAIN

;초기화
CSTR:1 = 
VARSET LOCAL

;연애 커맨드 진입시에만 가능한 특수이벤 처리용
SELECTCASE NO:TARGET
    CASE 25 ; 브라우니 엔딩 3 처리용
        IF CFLAG:TARGET:캐릭터히든 == 1
            SETBIT CFLAG:MASTER:엔딩플래그, 28
            BEGIN TURNEND
        ENDIF
    CASE 171 ;뽀끄루 남은일수 표기용
        IF CFLAG:엔딩조건카운트1 > 0 && TALENT:가수 == 1
            CALL CONCERT_D_DAY(TARGET, 30 - CFLAG:엔딩조건카운트1)
        ENDIF
ENDSELECT

CSTR:1 '= SET_MSG_CSTR(TARGET)

SIF !CFLAG:조교여부 ;첫조교시
    CSTR:1 = First

;Confession(고백)/Obey(복종) 가부 판별
;'조건절 추가'보다 후순위(덮어쓰기됨)로 처리됨
IF TALENT:사랑 && CFLAG:고백함 == 0
    VARSET LOCALS
    IF NO:TARGET == 68 ;에밀리는 정신쇠약 있어도 사랑이면 가능
        IF TALENT:사랑 == 2
            LOCALS = Confession
            SETBIT CFLAG:개별선택지, 0
        ENDIF
    ELSEIF !TALENT:정신쇠약
        LOCALS = Confession
        SELECTCASE NO:TARGET
            CASE 52 ;나엔 비관적 삭제
                SIF TALENT:긍정도 == -1
                    TALENT:긍정도 = 0
        ENDSELECT
    ENDIF
    SIF LOCALS != ""
        CSTR:1 = %LOCALS%
ELSEIF TALENT:굴복 == 2 && CFLAG:고백함 == 0
    CSTR:1 '= "Obey"
ENDIF

;조건절 추가
SELECTCASE NO:TARGET
    CASE 21 ;마리
        IF TALENT:MASTER:나이 == 0
            IF !CFLAG:조교여부
                CFLAG:호감도 += 500
                TALENT:변태 = 1
            ENDIF
            CSTR:1 += "/Yboy"
        ELSE
            CSTR:1 += "/Nboy"
        ENDIF
    CASE 51 ;메이
        IF TALENT:정신쇠약 == 2 && !GETBIT(CFLAG:특수출력유무, 9) ;etc2
            CSTR:1 = fBreak
        ELSEIF EXP:쇠약소멸경험 >= 5 && !GETBIT(CFLAG:특수출력유무, 13)
            CSTR:1 = nHeal
        ELSEIF EXP:쇠약획득경험 >= 5 && !GETBIT(CFLAG:특수출력유무, 12)
            CSTR:1 = nWeak
        ELSEIF EXP:쇠약소멸경험 < 5 && !GETBIT(CFLAG:특수출력유무, 11) ;etc6
            CSTR:1 = Heal
        ELSEIF EXP:쇠약획득경험 < 5 && !GETBIT(CFLAG:특수출력유무, 10) ;etc5
            CSTR:1 = fWeak
        ELSEIF TALENT:사랑 == 2 && !GETBIT(CFLAG:특수출력유무, 8) ;etc1
            CSTR:1 = fLove
        ENDIF
    CASE 52 ;나엔
        IF CFLAG:공포 < 200 && !IS_CHARA_UNHEALTHY(TARGET, 1)
            IF ITEM:ITEM_BIO(51) > 0 && !GETBIT(CFLAG:특수출력유무, 8) ;메이 이벤트
                LOCAL = GETCHARA(51)
                IF CFLAG:LOCAL:공포 < 200 && !IS_CHARA_UNHEALTHY(LOCAL, 1)
                    IF TALENT:TARGET:사랑 && TALENT:LOCAL:사랑 ;메이/나엔 모두 연모 이상, 원본에서는 연모만
                        CSTR:1 = mayE2
                    ELSE
                        CSTR:1 = mayE1
                    ENDIF
                ENDIF
            ELSEIF ITEM:ITEM_BIO(106) > 0 && TALENT:사랑 ;오드리 이벤트, 원본에서는 연모만
                LOCAL = GETCHARA(106)
                IF CFLAG:LOCAL:공포 < 200 && !IS_CHARA_UNHEALTHY(LOCAL, 1)
                    SELECTCASE CFLAG:이벤트카운트1
                        CASE 0
                            CSTR:1 = audreyE1
                        CASE 1
                            CSTR:1 = audreyE2
                        CASEELSE
                    ENDSELECT
                ENDIF
            ELSEIF TALENT:사랑 && !RAND:10 ;원본에서는 연모만, 10% 확률 ;naEvent4
                SELECTCASE RAND:4 ;4개 중 랜덤 선택. 추후 원본 경우의 수 추가시 조정 필요
                    CASE 0
                        CSTR:1 = NAE1
                    CASE 1
                        CSTR:1 = NAE2
                    CASE 2 ;취함
                        IF TIME == 1 ;밤일때만 적용
                            TCVAR:취함 = 1
                            CSTR:1 = Drunk
                        ENDIF
                    CASE 3 ;외출중
                        TCVAR:외출중 = 1
                        CSTR:1 = Date_1
                ENDSELECT
            ENDIF
        ENDIF
    CASE 73 ;팬텀
        IF TALENT:정신쇠약 == 2 && !GETBIT(CFLAG:특수출력유무, 8)
            CSTR:1 = Awaken
        ENDIF
    CASE 76
        IF CSTR:1 == "Affection" || CSTR:1 == "Marryed"
            IF TALENT:체형 == 0
                CSTR:1 += "/Young"
            ELSEIF TALENT:체형 == 1
                CSTR:1 += "/Adult"
            ENDIF
        ENDIF
    CASE 91
        IF !CFLAG:조교여부 ;슬레이프니르 첫 연애
            SETBIT CFLAG:개별선택지, 0
        ELSEIF EXP:가창경험 > 19 && !GETBIT(CFLAG:특수출력유무, 8)
            CSTR:1 = Superstar
        ENDIF
    CASE 108
        IF TALENT:혼인 == 2
            CSTR:1 = Marryed
        ELSEIF TALENT:혼인 == 1
            CSTR:1 = Love
        ENDIF
    CASE 123 ;모모
        IF TALENT:정신쇠약 == 2 && !GETBIT(CFLAG:특수출력유무, 8)
            CSTR:1 = Awaken
        ENDIF
    CASE 171 ;뽀끄루
        IF CFLAG:고백함 && !GETBIT(CFLAG:특수출력유무, 8)
            CSTR:1 = singer_mode
            SETBIT CFLAG:개별선택지, 0
        ELSEIF ITEM:ITEM_BIO(118) > 0 && !GETBIT(CFLAG:특수출력유무, 11)
            LOCAL = GETCHARA(118)
            IF !IS_CHARA_UNHEALTHY(LOCAL) && !IS_CHARA_UNHEALTHY(TARGET)
                CSTR:1 = LRL_Faucre_1
            ENDIF
        ENDIF
ENDSELECT

;hidden 대응
SIF FLAG:히든메시지
    RETURN 0

SIF GETBIT(CFLAG:개별선택지, 0)
    CALL Personal_MSG_Select(TARGET)

;그림 표시 설정
LOCAL = -1
SIF CFLAG:개별그림유무
    CALL ILLUST(TARGET, "DateStart")

REPEAT 10
    IF COUNT == 9
        DEBUGPRINTFORML 조건(%CSTR:1%)에 맞는 %CALLNAME:TARGET%의 조교시작시 지문이 발견되지 않음
        BREAK
    ENDIF
    DEBUGPRINTFORML %CSTR:1% 조교여부 = {CFLAG:조교여부}

    TRYCALLFORM M_No{NO:TARGET}_TRAIN_START_{CFLAG:조교여부}
    IF RESULT == 0
        CSTR:1 '= Alternative_CSTR(TARGET, COUNT)
    ELSE
        BREAK
    ENDIF
REND
;----------------------------------------------
;COM 실행시 메시지 라인 설정 함수
;----------------------------------------------
@BIO_COM_MSG
#DIMS 꼬릿말
꼬릿말 = 

CSTR:1 '= SET_MSG_CSTR(TARGET)
SIF FIRSTTIME(SELECTCOM)
    CSTR:1 '= "First/" + CSTR:1

DEBUGPRINTFORML 스트레스 {SOURCE:스트레스} 호감도 {SOURCE:호감도}

IF SOURCE:스트레스 > 0 || SOURCE:호감도 < 0
    LOCALS = Low
ELSEIF SOURCE:스트레스 < 0 || SOURCE:호감도 > 0
    LOCALS = High
ELSE
    LOCALS = Low
ENDIF

IF SELECTCOM == 2
    SIF CFLAG:호감도 < 400 && LOCALS == "High"
        LOCALS = Low
ENDIF
DEBUGPRINTFORML LOCALS = %LOCALS%, CSTR = %CSTR:1%

SIF GROUPMATCH(CSTR:1, "High","Low")
    CSTR:1 = %LOCALS%

SELECTCASE NO:TARGET
    CASE 21 ;마리
        IF SELECTCOM == 2
            IF CSTR:1 == "Marryed" || LOCALS == "High"
                IF TALENT:MASTER:나이 == 0
                    CSTR:1 += "/Yboy"
                ELSE
                    CSTR:1 += "/Nboy"
                ENDIF
            ENDIF
        ELSEIF SELECTCOM == 3
            IF LOCALS == "High"
                IF TALENT:MASTER:나이 == 0
                    CSTR:1 += "/Yboy"
                ELSE
                    CSTR:1 += "/Nboy"
                ENDIF
            ENDIF
        ELSEIF GROUPMATCH(SELECTCOM, 5,9,14,16,20,21,22)
            IF TALENT:MASTER:나이 == 0
                CSTR:1 += "/Yboy"
            ELSE
                CSTR:1 += "/Nboy"
            ENDIF
        ELSEIF SELECTCOM == 15 && FIRSTTIME(SELECTCOM, 1)
            IF TALENT:MASTER:나이 == 0
                CSTR:1 += "/Yboy"
            ELSE
                CSTR:1 += "/Nboy"
            ENDIF
        ENDIF
    CASE 52 ;나앤
        IF TCVAR:취함
            IF STRFIND(CSTR:1, "First/")
                CSTR:1 '= "First/Drunk"
            ELSE
                CSTR:1 '= "Drunk"
            ENDIF
        ELSEIF TCVAR:외출중
            CSTR:1 '= "Date_1"
        ENDIF
    CASE 76 ;닥터
        IF GROUPMATCH(SELECTCOM, 2,3,12)
            IF TALENT:사랑
                IF TALENT:TARGET:체형 == 0
                    CSTR:1 = Young
                ELSEIF TALENT:TARGET:체형 == 1
                    CSTR:1 = Adult
                ENDIF
            ENDIF
        ENDIF
    CASE 106 ;오드리
        SIF SELECTCOM == 2 && (CFLAG:이벤트카운트1 > 0 && CFLAG:이벤트카운트1 < 4)
            CSTR:1 = Cloth
    CASE 108 ;더치 걸
        IF TALENT:혼인 == 2
            CSTR:1 = Marryed
        ELSEIF TALENT:혼인 == 1
            CSTR:1 = Love
        ENDIF
    CASE 171 ;뽀끄루
        IF GROUPMATCH(SELECTCOM, 101,112,113)
            VARSET LOCAL
            IF SELECTCOM == 101
                LOCAL = EXP:가창경험
            ELSEIF SELECTCOM == 112
                LOCAL = EXP:연주경험
            ELSEIF SELECTCOM == 113
                LOCAL = EXP:퍼포먼스경험
            ENDIF

            SELECTCASE LOCAL
                CASE IS < 100
                    CSTR:1 = Lowlv
                CASE 100 TO 199
                    CSTR:1 = Highlv
                CASE IS >= 200
                    CSTR:1 = MAxlv
            ENDSELECT

        ENDIF
ENDSELECT

;꼬릿말 처리
IF NO:TARGET == 21 && EXP:학대COM경험 > 9 && CSTR:1 == "Yboy"
    꼬릿말 = %CALLNAME:TARGET%의 상태가 어딘가 이상하다/이대로 두면 돌이킬 수 없는 일이 벌어질 것 같다
    IF GROUPMATCH(SELECTCOM, 20,21,22)
        CSTR:1 += "/Danger"
    ENDIF
ENDIF

SIF CSTR:1 == ""
    CSTR:1 = %LOCALS%

REPEAT 10
    IF COUNT == 9
        DEBUGPRINTFORML 조건(%CSTR:1%)에 맞는 %CALLNAME:TARGET%의 %SELECTCOM% 지문이 발견되지 않음
        BREAK
    ENDIF
    DEBUGPRINTFORML 선정된 메시지 태그 : %CSTR:1%
    TRYCCALLFORM M_No{NO:TARGET}_COM{SELECTCOM}
        IF RESULT == 0 ;해당 구문을 찾을 수 없을때
            CSTR:1 '= Alternative_CSTR(TARGET, COUNT)
        ELSE
            BREAK
        ENDIF
    CATCH
        TRYCALLFORM M_No{NO:TARGET}_COM{COM_ALTER_SELECT_Rev(TARGET, SELECTCOM)}
        IF RESULT == 0 ;해당 구문을 찾을 수 없을때
            CSTR:1 '= Alternative_CSTR(TARGET, COUNT)
        ELSE
            BREAK
        ENDIF
    ENDCATCH
REND
PRINTL

SIF CFLAG:개별그림유무
    CALL ILLUST(TARGET, "DateCom")

PRINTL

SIF 꼬릿말 != ""
    CALL SPLIT_MSG(꼬릿말, 1)
;----------------------------------------------
;함수명: Alternative_CSTR(ARG, ARG:1)
;결과가 없는 메시지 태그값 변환용
;변환한 CSTR값 반환
;ARG = 인게임 캐릭터 ID, ARG:1 = 루프 횟수, 기본값 0
;----------------------------------------------
@Alternative_CSTR(ARG, ARG:1=0)
#FUNCTIONS
#DIMS SELECTED_CSTR, 5
;아래 DIM/DIMS 변수는 구동중이면 계속 저장되어 있으니 취급 요주의
#DIMS SAVED_CSTR ;실행 당시 태그값 저장
#DIM FIRST_SWITCH ;First계열 변환 실행시 켜짐

LOCALS = 
DEBUGPRINTFORML 대상 : %CALLNAME:ARG%, 태그 : %CSTR:ARG:1% 루프수 : {ARG:1}
DEBUGPRINTFORML 스위치 : {FIRST_SWITCH} 이전태그 : %SAVED_CSTR%

;첫구동일때 로컬변수 초기회
IF ARG:1 == 0
    FIRST_SWITCH = 0
    SAVED_CSTR =
ELSE
    IF FIRST_SWITCH && STRFINDU(SAVED_CSTR, "First") != -1
        IF STRFINDU(SAVED_CSTR, CSTR:ARG:1) != -1
            DEBUGPRINTL 태그 재시도 First
            LOCALS += "First/"
        ELSE
            FIRST_SWITCH = 0
        ENDIF
    ENDIF
ENDIF

IF STRFINDU(CSTR:ARG:1, "First") != -1
    FIRST_SWITCH = 1
ELSE
    FIRST_SWITCH = 0
ENDIF

SAVED_CSTR '= CSTR:ARG:1

SPLIT CSTR:ARG:1, "/", SELECTED_CSTR
FOR LOCAL, 0, RESULT
    IF SELECTED_CSTR:LOCAL == ""
        BREAK
    ENDIF

    ;현재 First가 있다면 해당 값을 지움
    IF FIRST_SWITCH
        IF !LOCAL
            DEBUGPRINTL 태그 삭제 First
        ELSE
            SIF LOCAL > 1
                LOCALS += "/"
            LOCALS += SELECTED_CSTR:LOCAL
        ENDIF
    ;태그 변경은 여기서 진행
    ELSE
        IF LOCAL > 0
            LOCALS += "/"
        ENDIF

        ;특정 태그별 치환 시도
        ;순서에 따른 우선도가 적용됨.
        SELECTCASE SELECTED_CSTR:LOCAL
            CASE "Love"
                DEBUGPRINTL 태그 변경 Love > Affection
                LOCALS += "Affection"
            CASE "Marryed"
                DEBUGPRINTL 태그 변경 Marryed > Love
                LOCALS += "Love"
            CASE "Weak2"
                DEBUGPRINTL 태그변경 Weak2 > Weak
                LOCALS += "Weak"
            CASE "Fear2"
                DEBUGPRINTL 태그변경 Fear2 > Fear
                LOCALS += "Fear"
            CASE "Annoyed2"
                DEBUGPRINTL 태그변경 Annoyed2 > Annoyed
                LOCALS += "Annoyed"
            CASE "Submit"
                DEBUGPRINTL 태그변경 Submit > Submit 이외
                LOCALS += SET_MSG_CSTR(ARG, 1)
            CASEELSE
                LOCALS += SELECTED_CSTR:LOCAL
        ENDSELECT
    ENDIF
NEXT
DEBUGPRINTL 
RETURNF LOCALS
;----------------------------------------------
;question 대용 클래스
;CFLAG:ARG:개별선택지 bit 0 값이 1일때 진입
;개별선택지 bit 0 이외 값으로 결과값 저장
;ARG = 인게임 캐릭터 ID
;----------------------------------------------
@Personal_MSG_Select(ARG)
#DIM 대상
대상 = ARG
SELECTCASE NO:대상
    CASE 68 ;에밀리
        ;연애 시작시 고백
        IF GETBIT(CFLAG:대상:개별선택지, 0)
            ;일단 초기화
            CLEARBIT CFLAG:대상:개별선택지, 0

            SIF CSTR:1 != "Confession"
                RETURN 0
            CALLFORM M_No{NO:대상}_TRAIN_START_1

            CALL ASK_M("에밀리의 마음을 받아들였다.", 1, "에밀리의 마음을 거절했다.", 1, "고작 이런 말을 하기 위해 불렀냐고 에밀리를 다그친다.", 1)
            ;CFLAG:개별선택지 bit 1값 = 에밀리 etc1 고백 가부 판별
            IF RESULT == 0
                CSTR:대상:1 = Yes
                SETBIT CFLAG:대상:개별선택지, 1
                RETURN 1
            ELSEIF RESULT == 1
                CSTR:대상:1 = No
                CLEARBIT CFLAG:대상:개별선택지, 1
                RETURN 1
            ELSEIF RESULT == 2
                SETBIT CFLAG:MASTER:엔딩플래그, 22
                CALL ENDING(대상)
            ENDIF
        ENDIF
    CASE 91 ;땡컨
        ;연애 첫시작시 타입 선택
        IF GETBIT(CFLAG:대상:개별선택지, 0)
            ;일단 초기화
            CLEARBIT CFLAG:대상:개별선택지, 0

            CALLFORM M_No{NO:대상}_TRAIN_START_{CFLAG:조교여부}
            PRINTL [1] 슈퍼 스타지!
            PRINTL [2] 블랙리버의 실험기야!
            WHILE 1 ;CFLAG:개별선택지 bit 1값 = 땡컨 etc4 슈퍼스타 출력 유무
                INPUT
                IF RESULT == 1
                    SETBIT CFLAG:대상:개별선택지, 1
                    TALENT:대상:군인 = 0
                    TALENT:대상:슈퍼스타 = 1
                    ABL:대상:전투lv = 0
                    EXP:대상:출격경험 = 0
                    CSTR:대상:1 = Star
                    RETURN 1
                ELSEIF RESULT == 2
                    CLEARBIT CFLAG:대상:개별선택지, 1
                    CSTR:대상:1 = BR
                    RETURN 1
                ENDIF
            WEND
        ENDIF
    CASE 171 ;뽀끄루
        ;고백 후 첫 연애시 미니게임 진행여부 선택
        IF GETBIT(CFLAG:대상:개별선택지, 0)
            CLEARBIT CFLAG:대상:개별선택지, 0
            
            SIF CSTR:1 != "singer_mode"
                RETURN 0
            CALLFORM M_No{NO:대상}_TRAIN_START_1

            CALL ASK_M("뽀끄루의 부탁을 허락해준다", 1, "거절한다.", 1)
            ;CFLAG:개별선택지 bit 1값 = 뽀끄루 공연 미니게임 선택 여부
            IF RESULT == 0
                CSTR:대상:1 = Yes
                SETBIT CFLAG:대상:개별선택지, 1
                RETURN 1
            ELSEIF RESULT == 1
                CSTR:대상:1 = No
                 CLEARBIT CFLAG:대상:개별선택지, 1
                RETURN 1
           ENDIF
        ENDIF
ENDSELECT
RETURN 0
