;----------------------------------------------
;함수명: SET_MSG_CSTR(ARG)
;원본의 line 세팅과 같음
;메시지 기본 조건절 처리
;ARG = 인게임 캐릭터번호
;----------------------------------------------
@SET_MSG_CSTR(ARG)

IF TALENT:ARG:백치
    CSTR:ARG:1 += "regress"
ELSEIF TALENT:ARG:정신쇠약 == 2
    CSTR:ARG:1 += "Break"
ELSEIF TALENT:ARG:정신쇠약 == 1
    CSTR:ARG:1 += "Weak"
ELSEIF TALENT:ARG:혼인
    CSTR:ARG:1 += "Marryed"
ELSEIF TALENT:ARG:사랑 == 2
    CSTR:ARG:1 += "Love"
ELSEIF TALENT:ARG:혼인 == 1 || TALENT:ARG:사랑
    CSTR:ARG:1 += "Affection"
ELSEIF CFLAG:ARG:공포 > 199
    CSTR:ARG:1 += "Fear"
ELSEIF TALENT:ARG:짜증
    CSTR:ARG:1 += "Annoyed"
ELSEIF CFLAG:ARG:호감도 > 499
    CSTR:ARG:1 += "High"
ELSEIF CFLAG:ARG:호감도 < 500
    CSTR:ARG:1 += "Low"
ELSE
    CSTR:ARG:1 += 
ENDIF

;================================================
;함수명: EVENTTRAIN
;SYSTEM_CALL
;연애 커맨드 진입시 (BEGIN TRAIN)
;================================================
@EVENTTRAIN

;초기화
CSTR:1 = 
LOCAL = 0

CALL SET_MSG_CSTR(TARGET)

SIF !CFLAG:조교여부 ;첫조교시
    CSTR:1 = First

;조건절 추가
SELECTCASE NO:TARGET
    CASE 21 ;마리
        IF !CFLAG:조교여부
            IF TALENT:MASTER:나이 == 0
                CFLAG:호감도 += 500
                TALENT:변태 = 1
                CSTR:1 += "/Yboy"
            ELSE
                CSTR:1 += "/Nboy"
            ENDIF
        ENDIF
    CASE 51
        IF TALENT:정신쇠약 == 2 && !CFLAG:etc2
            CSTR:1 = fBreak
        ELSEIF CFLAG:etc4 == 5
            CSTR:1 = nHeal
        ELSEIF CFLAG:etc3 == 5
            CSTR:1 = nWeak
        ELSEIF CFLAG:etc4 < 5 && CFLAG:etc4 > 0 && !CFLAG:etc6
            CSTR:1 = Heal
        ELSEIF CFLAG:etc3 < 5 && CFLAG:etc3 > 0 && !CFLAG:etc5
            CSTR:1 = fWeak
        ELSEIF TALENT:사랑 == 2 && !CFLAG:etc1 ;메이 fLove 취득대사 유무
            CSTR:1 = fLove
        ENDIF
    CASE 52
        IF CFLAG:공포 < 200 && !TALENT:짜증 && !TALENT:정신쇠약
            IF ITEM:ITEM_BIO(51) > 0 && CFLAG:etc1 == 0 ;메이 이벤트
                LOCAL = GETCHARA(51)
                IF CFLAG:LOCAL:공포 < 200 && !TALENT:LOCAL:짜증 && !TALENT:LOCAL:정신쇠약
                    IF TALENT:사랑 && TALENT:LOCAL:사랑 ;메이/나엔 모두 연모 이상, 원본에서는 연모만
                        CFLAG:etc1 = 1
                        CSTR:1 = mayE2
                    ELSE
                        CFLAG:etc1 = 1
                        CSTR:1 = mayE1
                    ENDIF
                ENDIF
            ELSEIF ITEM:ITEM_BIO(106) > 0 && CFLAG:etc2 != 2 && TALENT:사랑 ;오드리 이벤트, 원본에서는 연모만
                LOCAL = GETCHARA(106)
                IF CFLAG:LOCAL:공포 < 200 && !TALENT:LOCAL:짜증 && !TALENT:LOCAL:정신쇠약
                    IF CFLAG:etc2 == 0
                        CSTR:1 = audreyE1
                    ELSEIF CFLAG:etc2 == 1
                        CSTR:1 = audreyE2
                    ENDIF
                    CFLAG:etc2 += 1
                ENDIF
            ENDIF
        ENDIF
    CASE 68
        IF TALENT:정신쇠약 == 1 && TALENT:사랑 == 2
            CSTR:1 = Weak2
        ELSEIF CFLAG:공포 > 199 && TALENT:사랑 == 2
            CSTR:1 = Fear2
        ELSEIF TALENT:혼인 == 1 || TALENT:사랑 == 2
            CSTR:1 = Love
        ELSEIF TALENT:짜증 && TALENT:사랑 == 2
            CSTR:1 = Annoyed2
        ENDIF
    CASE 76
        IF CSTR:1 == "Affection" || CSTR:1 == "Marryed"
            IF TALENT:체형 == 0
                CSTR:1 += "/Young"
            ELSEIF TALENT:체형 == 1
                CSTR:1 += "/Adult"
            ENDIF
        ENDIF
    CASE 91
        SIF !CFLAG:조교여부
            CFLAG:개별선택지유무 = 1
    CASE 108
        IF TALENT:혼인 == 2
            CSTR:1 = Marryed
        ELSEIF TALENT:혼인 == 1
            CSTR:1 = Love
        ENDIF
ENDSELECT

;Confession(고백) 가부 판별
IF TALENT:사랑 && CFLAG:고백함 == 0
    LOCALS = 
    IF NO:TARGET == 68 ;에밀리는 정신쇠약 있어도 사랑이면 가능
        SIF TALENT:사랑 == 2
            LOCALS = Confession
            CFLAG:개별선택지유무 = 1
    ELSEIF !TALENT:정신쇠약
        LOCALS = Confession
        SELECTCASE NO:TARGET
            CASE 21 ;마리
                IF TALENT:MASTER:나이 == 0
                    LOCALS += "/Yboy"
                ELSE
                    LOCALS += "/Nboy"
                ENDIF
            CASE 52 ;나엔 비관적 삭제
                SIF TALENT:긍정도 == -1
                    TALENT:긍정도 = 0
        ENDSELECT
    ENDIF
    SIF LOCALS != ""
        CSTR:1 = %LOCALS%
ENDIF

;hidden 대응
SIF FLAG:히든메시지
    RETURN 0

SIF CFLAG:개별선택지유무 > 0
    CALL Personal_MSG_Select(TARGET)

;그림 표시 설정
LOCAL = -1
IF CFLAG:개별그림유무
    SELECTCASE NO:TARGET
        CASE 7
            IF CSTR:1 == "Confession"
                LOCAL = 0
            ELSE
                LOCAL = 2
            ENDIF
        CASE 12
            LOCAL = 3
        CASE 51
            IF TALENT:정신쇠약 == 2
                LOCAL = 1
            ELSEIF CSTR:1 == "fLove"
                LOCAL = 4
            ENDIF
        CASE 138
            IF CSTR:1 == "Confession"
                LOCAL = 6
            ELSEIF !CFLAG:조교여부
                LOCAL = 5
            ENDIF
    ENDSELECT
    ;미실장이기에 디버그용으로만 열어둠
    [IF_DEBUG]
    CALL ILLUST(TARGET, LOCAL, "DATESTART")
    [ENDIF]
ENDIF

DEBUGPRINTFORML %CSTR:1% 조교여부 = {CFLAG:조교여부}

TRYCALLFORM M_No{NO:TARGET}_TRAIN_START_{CFLAG:조교여부}

;----------------------------------------------
;COM 실행시 메시지 라인 설정 함수
;----------------------------------------------
@BIO_COM_MSG
#DIMS 꼬릿말
꼬릿말 = 

CSTR:1 = 

;아직 첫키스 말고 다른 경우의 첫시도 구상은 찾은 적이 없으므로
SIF SELECTCOM == 15 && FIRSTTIME(SELECTCOM)
    CSTR:1 = First/

CALL SET_MSG_CSTR(TARGET)

DEBUGPRINTFORML 스트레스 {SOURCE:스트레스} 호감도 {SOURCE:호감도}

IF SOURCE:스트레스 > 0 || SOURCE:호감도 < 0
    LOCALS = Low
ELSEIF SOURCE:스트레스 < 0 || SOURCE:호감도 > 0
    LOCALS = High
ELSE
    LOCALS = Low
ENDIF

IF SELECTCOM == 2
    SIF CFLAG:호감도 < 400 && LOCALS == "High"
        LOCALS = Low
ENDIF
DEBUGPRINTFORML LOCAL = %LOCALS%, CSTR = %CSTR:1%

SIF GROUPMATCH(CSTR:1, "High","Low")
    CSTR:1 = %LOCALS%

SELECTCASE NO:TARGET
    CASE 21 ;마리
        IF SELECTCOM == 2
            IF CSTR:1 == "Marryed" || LOCALS == "High"
                IF TALENT:MASTER:나이 == 0
                    CSTR:1 += "/Yboy"
                ELSE
                    CSTR:1 += "/Nboy"
                ENDIF
            ENDIF
        ELSEIF SELECTCOM == 3
            IF LOCALS == "High"
                IF TALENT:MASTER:나이 == 0
                    CSTR:1 += "/Yboy"
                ELSE
                    CSTR:1 += "/Nboy"
                ENDIF
            ENDIF
        ELSEIF GROUPMATCH(SELECTCOM, 5,9,14,16,20,21,22)
            IF TALENT:MASTER:나이 == 0
                CSTR:1 += "/Yboy"
            ELSE
                CSTR:1 += "/Nboy"
            ENDIF
        ELSEIF SELECTCOM == 15 && FIRSTTIME(SELECTCOM, 1)
            IF TALENT:MASTER:나이 == 0
                CSTR:1 += "/Yboy"
            ELSE
                CSTR:1 += "/Nboy"
            ENDIF
        ENDIF
    CASE 68
        IF TALENT:정신쇠약 == 1 && TALENT:사랑 == 2
            CSTR:1 = Weak2
        ELSEIF CFLAG:공포 > 199 && TALENT:사랑 == 2
            CSTR:1 = Fear2
        ELSEIF TALENT:혼인 == 1 || TALENT:사랑 == 2
            CSTR:1 = Love
        ELSEIF TALENT:짜증 && TALENT:사랑 == 2
            CSTR:1 = Annoyed2
        ENDIF
    CASE 76 ;닥터
        IF GROUPMATCH(SELECTCOM, 2,3,12)
            IF TALENT:사랑
                IF TALENT:TARGET:체형 == 0
                    CSTR:1 = Young
                ELSEIF TALENT:TARGET:체형 == 1
                    CSTR:1 = Adult
                ENDIF
            ENDIF
        ENDIF
    CASE 106
        SIF SELECTCOM == 2 && (CFLAG:etc1 > 0 && CFLAG:etc1 < 4)
            CSTR:1 = Cloth
    CASE 108
        IF TALENT:혼인 == 2
            CSTR:1 = Marryed
        ELSEIF TALENT:혼인 == 1
            CSTR:1 = Love
        ENDIF
    CASE 138
        IF SELECTCOM == 102
            IF CFLAG:etc1 == 18 ;TODO 순차대사 이식시 사용
            ENDIF
        ENDIF
ENDSELECT

;꼬릿말 처리
IF NO:TARGET == 21 && CFLAG:etc1 > 9 && CSTR:1 == "Yboy"
    꼬릿말 = %CALLNAME:TARGET%의 상태가 어딘가 이상하다/이대로 두면 돌이킬 수 없는 일이 벌어질 것 같다
    IF GROUPMATCH(SELECTCOM, 20,21,22)
        CSTR:1 += "/Danger"
    ENDIF
ENDIF

SIF CSTR:1 == ""
    CSTR:1 = %LOCALS%

IF CFLAG:개별그림유무
    SELECTCASE NO:TARGET

    ENDSELECT
    ;미실장이기에 디버그용으로만 열어둠
    [IF_DEBUG]
    CALL ILLUST(TARGET, LOCAL, "DATECOM")
    [ENDIF]
ENDIF

DEBUGPRINTFORML %CSTR:1%

TRYCALLFORM M_No{NO:TARGET}_COM{SELECTCOM}
PRINTL
SIF 꼬릿말 != ""
    CALL SPLIT_MSG(꼬릿말, 1)

;----------------------------------------------
;question 대용 클래스
;ARG = 인게임 캐릭터 ID
;----------------------------------------------
@Personal_MSG_Select(ARG)
#DIM 대상
대상 = ARG
SELECTCASE NO:대상
    CASE 68 ;에밀리
        ;연애 시작시 고백
        IF CFLAG:대상:개별선택지유무 == 1
            ;일단 초기화
            CFLAG:대상:개별선택지유무 = -1

            SIF CSTR:1 != "Confession"
                RETURN 0
            CALLFORM M_No{NO:TARGET}_TRAIN_START_1

            CALL ASK_M("에밀리의 마음을 받아들였다.", 1, "에밀리의 마음을 거절했다.", 1, "고작 이런 말을 하기 위해 불렀냐고 에밀리를 다그친다.", 1)
            ;CFLAG:선택지확인 = 에밀리 etc1 고백 가부 판별
            IF RESULT == 0
                CSTR:대상:1 = Yes
                SETBIT CFLAG:대상:선택지확인, 0
                RETURN 1
            ELSEIF RESULT == 1
                CSTR:대상:1 = No
                CFLAG:대상:선택지확인 = CFLAG:대상:선택지확인 ^ 1
                RETURN 1
            ELSEIF RESULT == 2
                SETBIT CFLAG:MASTER:엔딩플래그, 22
                CALL ENDING(대상)
            ENDIF
        ENDIF
    CASE 91 ;땡컨
        ;연애 첫시작시 타입 선택
        IF CFLAG:대상:개별선택지유무 == 1
            ;일단 초기화
            CFLAG:대상:개별선택지유무 = -1

            PRINTW TODO 슬레이프니르 문장 추가
            CALL ASK_M("슈퍼스타가 된다.", 1, "군인 신분을 유지한다.", 1)
            ;CFLAG:선택지확인 = 땡컨 etc4 슈퍼스타 출력 유무
            IF RESULT == 0
                SETBIT CFLAG:대상:선택지확인, 0
                TALENT:대상:군인 = 0
                TALENT:대상:슈퍼스타 = 1
                ABL:대상:전투lv = 0
                EXP:대상:출격경험 = 0
                CSTR:대상:1 = Star
                RETURN 1
            ELSEIF RESULT == 1
                CFLAG:대상:선택지확인 = CFLAG:대상:선택지확인 ^ 1
                CSTR:대상:1 = BR
                RETURN 1
            ENDIF
        ENDIF
ENDSELECT
RETURN 0