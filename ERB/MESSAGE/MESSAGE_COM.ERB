;----------------------------------------------
;함수명: SET_MSG_CSTR(ARG)
;원본의 line 세팅과 같음
;메시지 기본 조건절 처리
;ARG = 인게임 캐릭터번호
;----------------------------------------------
@SET_MSG_CSTR(ARG)

IF TALENT:ARG:백치
    CSTR:ARG:1 += "regress"
ELSEIF TALENT:ARG:정신쇠약 == 2
    CSTR:ARG:1 += "Break"
ELSEIF TALENT:ARG:정신쇠약 == 1
    CSTR:ARG:1 += "Weak"
ELSEIF TALENT:ARG:혼인
    CSTR:ARG:1 += "Marryed"
ELSEIF TALENT:ARG:사랑 == 2
    CSTR:ARG:1 += "Love"
ELSEIF TALENT:ARG:혼인 == 1 || TALENT:ARG:사랑
    CSTR:ARG:1 += "Affection"
ELSEIF CFLAG:ARG:공포 > 199
    CSTR:ARG:1 += "Fear"
ELSEIF TALENT:ARG:짜증
    CSTR:ARG:1 += "Annoyed"
ELSEIF CFLAG:ARG:호감도 > 499
    CSTR:ARG:1 += "High"
ELSEIF CFLAG:ARG:호감도 < 500
    CSTR:ARG:1 += "Low"
ELSE
    CSTR:ARG:1 += 
ENDIF

SIF FLAG:히든메시지
    CSTR:ARG:1 '= "Hidden"

;================================================
;함수명: EVENTTRAIN
;SYSTEM_CALL
;연애 커맨드 진입시 (BEGIN TRAIN)
;================================================
@EVENTTRAIN

;초기화
CSTR:1 = 
LOCAL = 0

CALL SET_MSG_CSTR(TARGET)

SIF !CFLAG:조교여부 ;첫조교시
    CSTR:1 = First

;조건절 추가
SELECTCASE NO:TARGET
    CASE 21 ;마리
        IF !CFLAG:조교여부
            IF TALENT:MASTER:나이 == 0
                CFLAG:호감도 += 500
                TALENT:변태 = 1
                CSTR:1 += "/Yboy"
            ELSE
                CSTR:1 += "/Nboy"
            ENDIF
        ENDIF
    CASE 51
        IF TALENT:정신쇠약 == 2 && !CFLAG:etc2
            CSTR:1 = fBreak
        ELSEIF CFLAG:etc4 == 5
            CSTR:1 = nHeal
        ELSEIF CFLAG:etc3 == 5
            CSTR:1 = nWeak
        ELSEIF CFLAG:etc4 < 5 && CFLAG:etc4 > 0 && !CFLAG:etc6
            CSTR:1 = Heal
        ELSEIF CFLAG:etc3 < 5 && CFLAG:etc3 > 0 && !CFLAG:etc5
            CSTR:1 = fWeak
        ELSEIF TALENT:사랑 == 2 && !CFLAG:etc1 ;메이 fLove 취득대사 유무
            CSTR:1 = fLove
        ENDIF
    CASE 52
        IF CFLAG:공포 < 200 && !TALENT:짜증 && !TALENT:정신쇠약
            IF ITEM:ITEM_BIO(51) > 0 && CFLAG:etc1 == 0 ;메이 이벤트
                LOCAL = GETCHARA(51)
                IF CFLAG:LOCAL:공포 < 200 && !TALENT:LOCAL:짜증 && !TALENT:LOCAL:정신쇠약
                    IF TALENT:사랑 && TALENT:LOCAL:사랑 ;메이/나엔 모두 연모 이상, 원본에서는 연모만
                        CFLAG:etc1 = 1
                        CSTR:1 = mayE2
                    ELSE
                        CFLAG:etc1 = 1
                        CSTR:1 = mayE1
                    ENDIF
                ENDIF
            ELSEIF ITEM:ITEM_BIO(106) > 0 && CFLAG:etc2 != 2 && TALENT:사랑 ;오드리 이벤트, 원본에서는 연모만
                LOCAL = GETCHARA(106)
                IF CFLAG:LOCAL:공포 < 200 && !TALENT:LOCAL:짜증 && !TALENT:LOCAL:정신쇠약
                    IF CFLAG:etc2 == 0
                        CSTR:1 = audreyE1
                    ELSEIF CFLAG:etc2 == 1
                        CSTR:1 = audreyE2
                    ENDIF
                    CFLAG:etc2 += 1
                ENDIF
            ENDIF
        ENDIF
    CASE 68
        IF TALENT:정신쇠약 == 1 && TALENT:사랑 == 2
            CSTR:1 = Weak2
        ELSEIF CFLAG:공포 > 199 && TALENT:사랑 == 2
            CSTR:1 = Fear2
        ELSEIF TALENT:혼인 == 1 || TALENT:사랑 == 2
            CSTR:1 = Love
        ELSEIF TALENT:짜증 && TALENT:사랑 == 2
            CSTR:1 = Annoyed2
        ENDIF
    CASE 76
        IF CSTR:1 == "Affection" || CSTR:1 == "Marryed"
            IF TALENT:체형 == 0
                CSTR:1 += "/Young"
            ELSEIF TALENT:체형 == 1
                CSTR:1 += "/Adult"
            ENDIF
        ENDIF
    CASE 91
        SIF !CFLAG:조교여부 ;슬레이프니르 첫 연애
            SETBIT CFLAG:개별선택지, 0
    CASE 108
        IF TALENT:혼인 == 2
            CSTR:1 = Marryed
        ELSEIF TALENT:혼인 == 1
            CSTR:1 = Love
        ENDIF
ENDSELECT

;Confession(고백) 가부 판별
IF TALENT:사랑 && CFLAG:고백함 == 0
    LOCALS = 
    IF NO:TARGET == 68 ;에밀리는 정신쇠약 있어도 사랑이면 가능
        SIF TALENT:사랑 == 2
            LOCALS = Confession
            SETBIT CFLAG:개별선택지, 0
    ELSEIF !TALENT:정신쇠약
        LOCALS = Confession
        SELECTCASE NO:TARGET
            CASE 21 ;마리
                IF TALENT:MASTER:나이 == 0
                    LOCALS += "/Yboy"
                ELSE
                    LOCALS += "/Nboy"
                ENDIF
            CASE 52 ;나엔 비관적 삭제
                SIF TALENT:긍정도 == -1
                    TALENT:긍정도 = 0
        ENDSELECT
    ENDIF
    SIF LOCALS != ""
        CSTR:1 = %LOCALS%
ENDIF

;hidden 대응
SIF FLAG:히든메시지
    RETURN 0

SIF GETBIT(CFLAG:개별선택지, 0)
    CALL Personal_MSG_Select(TARGET)

;그림 표시 설정
LOCAL = -1
IF CFLAG:개별그림유무
    SELECTCASE NO:TARGET
        CASE 7
            IF CSTR:1 == "Confession"
                LOCAL = 0
            ELSE
                LOCAL = 2
            ENDIF
        CASE 12
            LOCAL = 3
        CASE 51
            IF TALENT:정신쇠약 == 2
                LOCAL = 1
            ELSEIF CSTR:1 == "fLove"
                LOCAL = 4
            ENDIF
        CASE 138
            IF CSTR:1 == "Confession"
                LOCAL = 6
            ELSEIF !CFLAG:조교여부
                LOCAL = 5
            ENDIF
    ENDSELECT
    CALL ILLUST(TARGET, LOCAL, "DATESTART")
ENDIF

REPEAT 10
    DEBUGPRINTFORML %CSTR:1% 조교여부 = {CFLAG:조교여부}

    TRYCALLFORM M_No{NO:TARGET}_TRAIN_START_{CFLAG:조교여부}
    IF RESULT == 0
        CSTR:1 '= Alternative_CSTR(TARGET)
        SIF COUNT == 9
            DEBUGPRINTFORML 조건(%CSTR:1%)에 맞는 %CALLNAME:TARGET%의 조교시작시 지문이 발견되지 않음
    ELSE
        BREAK
    ENDIF
REND
;----------------------------------------------
;COM 실행시 메시지 라인 설정 함수
;----------------------------------------------
@BIO_COM_MSG
#DIMS 꼬릿말
#DIM 그림번호
그림번호 = -1
꼬릿말 = 

CSTR:1 = 

SIF FIRSTTIME(SELECTCOM)
    CSTR:1 = First/

CALL SET_MSG_CSTR(TARGET)

DEBUGPRINTFORML 스트레스 {SOURCE:스트레스} 호감도 {SOURCE:호감도}

IF SOURCE:스트레스 > 0 || SOURCE:호감도 < 0
    LOCALS = Low
ELSEIF SOURCE:스트레스 < 0 || SOURCE:호감도 > 0
    LOCALS = High
ELSE
    LOCALS = Low
ENDIF

IF SELECTCOM == 2
    SIF CFLAG:호감도 < 400 && LOCALS == "High"
        LOCALS = Low
ENDIF
DEBUGPRINTFORML LOCALS = %LOCALS%, CSTR = %CSTR:1%

SIF GROUPMATCH(CSTR:1, "High","Low")
    CSTR:1 = %LOCALS%

SELECTCASE NO:TARGET
    CASE 21 ;마리
        IF SELECTCOM == 2
            IF CSTR:1 == "Marryed" || LOCALS == "High"
                IF TALENT:MASTER:나이 == 0
                    CSTR:1 += "/Yboy"
                ELSE
                    CSTR:1 += "/Nboy"
                ENDIF
            ENDIF
        ELSEIF SELECTCOM == 3
            IF LOCALS == "High"
                IF TALENT:MASTER:나이 == 0
                    CSTR:1 += "/Yboy"
                ELSE
                    CSTR:1 += "/Nboy"
                ENDIF
            ENDIF
        ELSEIF GROUPMATCH(SELECTCOM, 5,9,14,16,20,21,22)
            IF TALENT:MASTER:나이 == 0
                CSTR:1 += "/Yboy"
            ELSE
                CSTR:1 += "/Nboy"
            ENDIF
        ELSEIF SELECTCOM == 15 && FIRSTTIME(SELECTCOM, 1)
            IF TALENT:MASTER:나이 == 0
                CSTR:1 += "/Yboy"
            ELSE
                CSTR:1 += "/Nboy"
            ENDIF
        ENDIF
    CASE 68
        IF TALENT:정신쇠약 == 1 && TALENT:사랑 == 2
            CSTR:1 = Weak2
        ELSEIF CFLAG:공포 > 199 && TALENT:사랑 == 2
            CSTR:1 = Fear2
        ELSEIF TALENT:혼인 == 1 || TALENT:사랑 == 2
            CSTR:1 = Love
        ELSEIF TALENT:짜증 && TALENT:사랑 == 2
            CSTR:1 = Annoyed2
        ENDIF
    CASE 76 ;닥터
        IF GROUPMATCH(SELECTCOM, 2,3,12)
            IF TALENT:사랑
                IF TALENT:TARGET:체형 == 0
                    CSTR:1 = Young
                ELSEIF TALENT:TARGET:체형 == 1
                    CSTR:1 = Adult
                ENDIF
            ENDIF
        ENDIF
    CASE 106
        SIF SELECTCOM == 2 && (CFLAG:etc1 > 0 && CFLAG:etc1 < 4)
            CSTR:1 = Cloth
    CASE 108
        IF TALENT:혼인 == 2
            CSTR:1 = Marryed
        ELSEIF TALENT:혼인 == 1
            CSTR:1 = Love
        ENDIF
ENDSELECT

;꼬릿말 처리
IF NO:TARGET == 21 && CFLAG:etc1 > 9 && CSTR:1 == "Yboy"
    꼬릿말 = %CALLNAME:TARGET%의 상태가 어딘가 이상하다/이대로 두면 돌이킬 수 없는 일이 벌어질 것 같다
    IF GROUPMATCH(SELECTCOM, 20,21,22)
        CSTR:1 += "/Danger"
    ENDIF
ENDIF

SIF CSTR:1 == ""
    CSTR:1 = %LOCALS%

;그림 표시 처리
SELECTCASE NO:TARGET
    CASE 51 ;메이
        SIF SELECTCOM == 18 && !TALENT:짜증 && !TALENT:정신쇠약 && TALENT:사랑 ;메이 파후파후, 원본은 연모만.
            그림번호 = 1
    CASE 132 ;유미
        SIF SELECTCOM == 13 && (CSTR:1 == "High" || CSTR:1 == "Affection") ;유미 술배
            그림번호 = 3
    ;베로니카 연애커맨드 그림은 해당 캐릭터 메시지 코드 안에서 처리
    ;그림번호 10: 배설기관, 11: 뚝배기
ENDSELECT

PRINTL
REPEAT 10
    DEBUGPRINTFORML 선정된 메시지 태그 : %CSTR:1%
    TRYCCALLFORM M_No{NO:TARGET}_COM{SELECTCOM}
        IF RESULT == 0 ;해당 구문을 찾을 수 없을때
            CSTR:1 '= Alternative_CSTR(TARGET)
        ELSE
            BREAK
        ENDIF
    CATCH
        TRYCALLFORM M_No{NO:TARGET}_COM{COM_ALTER_SELECT_Rev(SELECTCOM)}
        IF RESULT == 0 ;해당 구문을 찾을 수 없을때
            CSTR:1 '= Alternative_CSTR(TARGET)
        ELSE
            BREAK
        ENDIF
    ENDCATCH
REND
PRINTL

SIF CFLAG:개별그림유무 && 그림번호 != -1
    CALL ILLUST(TARGET, 그림번호, "DATECOM")

SIF 꼬릿말 != ""
    CALL SPLIT_MSG(꼬릿말, 1)
;----------------------------------------------
;함수명: Alternative_CSTR(ARG)
;결과가 없는 메시지 태그값 변환용
;변환한 CSTR값 반환
;ARG = 인게임 캐릭터 ID
;----------------------------------------------
@Alternative_CSTR(ARG)
#FUNCTIONS
#DIMS SELECTED_CSTR, 5
;아래 DIM/DIMS 변수는 구동중이면 계속 저장되어 있으니 취급 요주의
#DIMS SAVED_CSTR ;실행 당시 태그값 저장
#DIM FIRST_SWITCH ;First계열 변환 실행시 켜짐

LOCALS = 
DEBUGPRINTFORML 대상 : %CALLNAME:ARG%, 태그 : %CSTR:ARG:1%

IF FIRST_SWITCH && STRFINDU(SAVED_CSTR, "First") != -1
    IF STRFINDU(SAVED_CSTR, CSTR:ARG:1) != -1
        DEBUGPRINTL 태그 재시도 First
        LOCALS += "First/"
    ELSE ;저장된 CSTR값이 원 값과 아예 다른 경우 통과
        FIRST_SWITCH = 0
    ENDIF
ENDIF

SAVED_CSTR '= CSTR:ARG:1

SPLIT CSTR:ARG:1, "/", SELECTED_CSTR
REPEAT RESULT
    IF SELECTED_CSTR:COUNT == ""
        BREAK
    ENDIF

    ;현재 First가 있다면 해당 값을 지움
    IF STRFINDU(CSTR:ARG:1, "First") != -1
        FIRST_SWITCH = 1
        IF COUNT == 0
            DEBUGPRINTL 태그 삭제 First
        ELSE
            SIF COUNT > 1
                LOCALS += "/"
            LOCALS += SELECTED_CSTR:COUNT
        ENDIF
    ELSE
        FIRST_SWITCH = 0
        IF COUNT > 0
            LOCALS += "/"
        ENDIF

        IF STRFINDU(SELECTED_CSTR:COUNT, "Love") != -1
            DEBUGPRINTL 태그 변경 Love > Affection
            LOCALS += "Affection"
        ELSEIF STRFINDU(SELECTED_CSTR:COUNT, "Marryed") != -1
            DEBUGPRINTL 태그 변경 Marryed > Love
            LOCALS += "Love"
        ELSE
            LOCALS += SELECTED_CSTR:COUNT
        ENDIF
    ENDIF
REND

RETURNF LOCALS
;----------------------------------------------
;question 대용 클래스
;CFLAG:ARG:개별선택지 bit 0 값이 1일때 진입
;개별선택지 bit 0 이외 값으로 결과값 저장
;ARG = 인게임 캐릭터 ID
;----------------------------------------------
@Personal_MSG_Select(ARG)
#DIM 대상
대상 = ARG
SELECTCASE NO:대상
    CASE 68 ;에밀리
        ;연애 시작시 고백
        IF GETBIT(CFLAG:대상:개별선택지, 0)
            ;일단 초기화
            CLEARBIT CFLAG:대상:개별선택지, 0

            SIF CSTR:1 != "Confession"
                RETURN 0
            CALLFORM M_No{NO:TARGET}_TRAIN_START_1

            CALL ASK_M("에밀리의 마음을 받아들였다.", 1, "에밀리의 마음을 거절했다.", 1, "고작 이런 말을 하기 위해 불렀냐고 에밀리를 다그친다.", 1)
            ;CFLAG:개별선택지 bit 1값 = 에밀리 etc1 고백 가부 판별
            IF RESULT == 0
                CSTR:대상:1 = Yes
                SETBIT CFLAG:대상:개별선택지, 1
                RETURN 1
            ELSEIF RESULT == 1
                CSTR:대상:1 = No
                CLEARBIT CFLAG:대상:개별선택지, 1
                RETURN 1
            ELSEIF RESULT == 2
                SETBIT CFLAG:MASTER:엔딩플래그, 22
                CALL ENDING(대상)
            ENDIF
        ENDIF
    CASE 91 ;땡컨
        ;연애 첫시작시 타입 선택
        IF GETBIT(CFLAG:대상:개별선택지, 0)
            ;일단 초기화
            CLEARBIT CFLAG:대상:개별선택지, 0

            CALLFORM M_No{NO:TARGET}_TRAIN_START_{CFLAG:조교여부}
            PRINTL [1] 슈퍼 스타지!
            PRINTL [2] 블랙리버의 실험기야!
            WHILE 1 ;CFLAG:개별선택지 bit 1값 = 땡컨 etc4 슈퍼스타 출력 유무
                INPUT
                IF RESULT == 1
                    SETBIT CFLAG:대상:개별선택지, 1
                    TALENT:대상:군인 = 0
                    TALENT:대상:슈퍼스타 = 1
                    ABL:대상:전투lv = 0
                    EXP:대상:출격경험 = 0
                    CSTR:대상:1 = Star
                    RETURN 1
                ELSEIF RESULT == 2
                    CLEARBIT CFLAG:대상:개별선택지, 1
                    CSTR:대상:1 = BR
                    RETURN 1
                ENDIF
            WEND
        ENDIF
ENDSELECT
RETURN 0