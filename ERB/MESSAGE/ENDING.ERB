;============================================================================================
;함수명: ENDING(ARG)
;원본 '엔딩' 대응
;분류된 엔딩플래그를 총괄하는 함수
;ARG = 인게임 캐릭터번호. -1인 경우 TURNEND에서 온 것
;============================================================================================
@ENDING(ARG)

;만약을 위한 TARGET 설정
SIF ARG != -1
    TARGET = ARG

IF CFLAG:MASTER:엔딩플래그
    CALL Classify_EndingFlag
ELSE
    CALL Ending_Result
ENDIF

IF RESULT == -1
    THROW 잘못된 엔딩플래그값 발견됨
ELSEIF RESULT ;CFLAG:MASTER 엔딩플래그 비트 위치값
    TRYCCALLFORM M_No{NO:TARGET}_ENDING
        IF !RESULT
            ;개별 엔딩 메시지는 있으나 해당 CFLAG값이 대응하지 않는 경우
            CALL ENDING_COMMON
        ELSE
            CALL Ending_Result
            ;엔딩플래그 초기화
            CFLAG:엔딩플래그 = 0
        ENDIF
    CATCH
        ;개별 엔딩 메시지가 없는 경우
        CALL ENDING_COMMON
    ENDCATCH
ELSE
    ;처리할 RESULT값이 없는 경우
    CALL ENDING_COMMON
ENDIF
;일단 날짜변경으로 보냄
BEGIN TURNEND
;============================================================================================
;함수명: ENDING_MARRY_FLAG(ARG)
;마지막 수정: v1.0
;약혼반지 사용시 메시지 + 엔딩플래그 및 수치 변환처리
;ARG = 인게임 캐릭터번호
;============================================================================================
@ENDING_MARRY_FLAG(ARG)
#DIM REAL_NUM
REAL_NUM = NO:ARG

TARGET = ARG
TALENT:혼인 = 1

SETBIT CFLAG:MASTER:엔딩플래그, 1

;정신붕괴
IF TALENT:정신쇠약 == 2
    PRINTFORML %타겟은% 아무 말 없이 반지를 받아들였다.
    CFLAG:엔딩플래그 = 2
;사랑
ELSEIF TALENT:사랑 == 2
    PRINTFORML %타겟은% 눈물을 글썽이며 손에 반지를 받아들였다.
    CFLAG:엔딩플래그 = 1

    ;캐릭터별 반지 특수엔딩 조건
    SELECTCASE REAL_NUM
        CASE 7 ;리제
            SIF FLAG:현캐릭터수 >= 10 && !RAND:10 ;히든, 10% 확률
                CFLAG:엔딩플래그 = 4
        CASE 51 ;메이
            SIF TALENT:GETCHARA(52):정신쇠약 || GETBIT(CFLAG:특수출력조건, 5) ;may5 = 1, etc7 = 메이/나앤 상대 이별 여부
                CFLAG:엔딩플래그 = 5
        CASE 52 ;나앤
            SIF TALENT:GETCHARA(51):정신쇠약 || GETBIT(CFLAG:특수출력조건, 3) ;may5 = 2, etc7 = 메이/나앤 상대 이별 여부
                CFLAG:엔딩플래그 = 3
        CASE 73 ;팬텀
            SIF EXP:출격경험 > 6
                CFLAG:엔딩플래그 = 3
        CASE 91 ;슬레이프니르
            SIF REAL_NUM == 91 && (TALENT:슈퍼스타 || GETBIT(CFLAG:특수출력유무, 8))
                CFLAG:엔딩플래그 = 3
        CASE 108 ;더치 걸
            IF CFLAG:GETCHARA(76):호감도 > 499 && !TALENT:GETCHARA(76):정신쇠약 ;닥터의 조건
                CFLAG:엔딩플래그 = 4
                TALENT:ARG:체헝 = 1
            ELSE
                TALENT:혼인 = 2
            ENDIF
        CASE 126 ;아자젤
            SIF EXP:고유COM경험 > 99 && EXP:학대COM경험 == 0
                CFLAG:엔딩플래그 = 3
    ENDSELECT
;위 조건에 해당하지 않는 엔딩
ELSE
    SELECTCASE REAL_NUM
        CASE 16 ;리리스
            IF !TALENT:TARGET:정신쇠약 > 0 && !TALENT:TARGET:사랑 && CFLAG:TARGET:공포 >= 175
                CFLAG:엔딩플래그 = 3
            ELSE
                THROW %CALLNAME:TARGET%의 잘못된 약혼반지 엔딩설정
            ENDIF
        CASEELSE
            THROW %CALLNAME:TARGET%의 잘못된 약혼반지 엔딩설정
    ENDSELECT
ENDIF

CALL ENDING(TARGET)
;============================================================================================
;함수명: ENDING_FAREWELL_FLAG(ARG, ARGS)
;마지막 수정: v0.9
;이별(판매/폐기)시 대사 및 엔딩플래그 처리
;ARG = 인게임 캐릭터번호, ARGS: 판매/폐기 구분
;============================================================================================
@ENDING_FAREWELL_FLAG(ARG, ARGS)
#DIM REAL_NUM
REAL_NUM = NO:ARG

TARGET = ARG
CSTR:ARG:1 '= SET_MSG_CSTR(ARG)

;판매/폐기 구별 처리
SELECTCASE ARGS
    CASE "판매"
        REPEAT 10
            TRYCALLFORM M_No{REAL_NUM}_Sell
            IF RESULT == 0
                CSTR:ARG:1 '= Alternative_CSTR(ARG)
                SIF COUNT == 9
                    DEBUGPRINTFORML 조건(%CSTR:ARG:1%)에 맞는 %CALLNAME:ARG%의 판매 지문이 발견되지 않음
            ELSE
                BREAK
            ENDIF
        REND
        
        IF REAL_NUM == 123 && TALENT:ARG:사랑 == 1
            SETBIT CFLAG:MASTER:엔딩플래그, 7
        ENDIF
    CASE "폐기"
        REPEAT 10
            TRYCALLFORM M_No{REAL_NUM}_Disuse
            IF RESULT == 0
                CSTR:ARG:1 '= Alternative_CSTR(ARG)
                SIF COUNT == 9
                    DEBUGPRINTFORML 조건(%CSTR:ARG:1%)에 맞는 %CALLNAME:ARG%의 폐기 지문이 발견되지 않음
            ELSE
                BREAK
            ENDIF
        REND

        IF REAL_NUM == 89 && TALENT:ARG:사랑 == 1
            SETBIT CFLAG:MASTER:엔딩플래그, 2
        ELSEIF REAL_NUM == 113 && TALENT:ARG:정신쇠약 == 2
            SETBIT CFLAG:MASTER:엔딩플래그, 4
        ELSEIF REAL_NUM == 51
            IF TALENT:ARG:사랑 == 1 && TALENT:ARG:정신쇠약 == 1
                SETBIT CFLAG:MASTER:엔딩플래그, 6
            ELSEIF TALENT:ARG:사랑 == 2 && EXP:ARG:출격경험 > 49
                SETBIT CFLAG:MASTER:엔딩플래그, 10
            ENDIF
        ENDIF
    CASEELSE
        THROW 상정외 %ARGS% 입니다.
ENDSELECT

;이별 공통 처리
SELECTCASE REAL_NUM
    CASE 51 ;메이
        SIF ITEM:ITEM_BIO(52) > 0 ;etc7 (나앤 존재시)
            SETBIT CFLAG:GETCHARA(52):특수출력조건, 3
    CASE 52 ;나앤
        SIF ITEM:ITEM_BIO(51) > 0 ;etc7 (메이 존재시)
            SETBIT CFLAG:GETCHARA(51):특수출력조건, 5
    CASE 138 ;베로니카
        SIF TALENT:ARG:사랑 == 2 && TALENT:ARG:혼인 && TALENT:ARG:정신쇠약 != 2
            SETBIT CFLAG:MASTER:엔딩플래그, 24
    CASE 171 ;뽀끄루
        SIF TALENT:ARG:정신쇠약 == 2 
            SETBIT CFLAG:MASTER:엔딩플래그, 29
ENDSELECT

RETURN 0
;============================================================================================
;함수명: ENDING_DAYEND_FLAG
;마지막 수정: v1.0
;원본 날짜변경/etc 대응
;날짜변경시 엔딩 관련 FLAG의 처리
;일부 경우 메시지 출력이 포함되며, 엔딩플래그가 있으면 마지막에 @ENDING 호출
;============================================================================================
@ENDING_DAYEND_FLAG
#DIM 주회
FOR 주회, 1, CHARANUM
    ;미보유 캐릭터 제외
    SIF ITEM:(ITEM_BIO(NO:주회)) < 1
        CONTINUE
    SELECTCASE NO:주회
        CASE 7 ;리제
            IF CFLAG:주회:엔딩조건카운트1 > 30 ;etc1 = 리제 엔딩 3 카운트
                SETBIT CFLAG:MASTER:엔딩플래그, 3
            ELSEIF CFLAG:주회:엔딩조건카운트2 > 30 ;etc2 = 리제 엔딩 4
                SETBIT CFLAG:MASTER:엔딩플래그, 8
            ELSEIF CFLAG:주회:엔딩조건카운트1 > 15
                PRINTL 리제가 공포에 휩싸인 채 혼자서 중얼거리고 있다.
                PRINTW 이대로 두면 돌이킬 수 없는 일이 벌어질 것 같다.
            ELSEIF CFLAG:주회:엔딩조건카운트2 > 15
                PRINTL 리제의 스트레스가 심각할 정도로 쌓인 것 같다.
                PRINTW 이대로 두면 돌이킬 수 없는 일이 벌어질 것 같다.
            ENDIF
        CASE 12 ;소완
            IF GETBIT(CFLAG:주회:특수출력조건, 3)
                IF CFLAG:주회:방치일수 > 30 ;etc1 = 소완 엔딩 3 카운트
                    SETBIT CFLAG:MASTER:엔딩플래그, 5
                ELSEIF CFLAG:주회:방치일수 > 25
                    PRINTL 소완의 상태가 어딘가 이상하다.
                    PRINTW 이대로 두면 돌이킬 수 없는 일이 벌어질 것 같다.
                ENDIF
            ENDIF
        CASE 21 ;마리
            SIF TALENT:MASTER:나이 == 0 && EXP:주회:학대COM경험 > 19 ;etc1 = 마리 엔딩 3 카운트
                SETBIT CFLAG:MASTER:엔딩플래그, 11
        CASE 25 ;브라우니
            IF GETBIT(CFLAG:주회:특수출력조건, 3)
                IF CFLAG:주회:방치일수 > 15
                    CFLAG:캐릭터히든 = 1 ;연애 진입시 엔딩 구현용
                ELSEIF CFLAG:주회:방치일수 == 14
                    PRINTL ...요 근래 브라우니가 너무 조용한 것 같다.
                    PRINTL 그 생각이 머릿 속을 스치자, 당신은 브라우니가 사라졌을지도 모른다는 생각에 그녀의 방을 돌아보았다.
                ENDIF
            ENDIF
        CASE 68 ;에밀리
            SIF CFLAG:주회:엔딩조건카운트1 > 4 ;etc2 = 에밀리 엔딩 4 카운트
                SETBIT CFLAG:MASTER:엔딩플래그, 15
        CASE 71 ;네오딤
            IF CFLAG:주회:엔딩조건카운트1 >= 5 ;etc1 = 스트레스 100상태 카운트
                SETBIT CFLAG:MASTER:엔딩플래그, 25
            ELSEIF CFLAG:주회:엔딩조건카운트1 > 0
                PRINTL 당신은 네오딤의 자는 모습을 보면서 최근의 이상을 떠올린다.
                PRINTL 이따금 의식이 갑자기 끊어진듯 멍한 태도를 보이는 것이었다.
                PRINTL ...상관없겠지.
                PRINTW 당신은 침대에 누웠다.
            ENDIF
        CASE 73 ;팬텀
            SIF CFLAG:주회:엔딩조건카운트1 > 19 && !TALENT:주회:혼인 ;etc3 = 팬텀 엔딩 4 카운트
                SETBIT CFLAG:MASTER:엔딩플래그, 14
        CASE 76 ;닥터
            IF GETBIT(CFLAG:주회:특수출력조건, 3) ;etc4 = 닥터 엔딩 3 카운트
                IF CFLAG:주회:방치일수 > 19
                    SETBIT CFLAG:MASTER:엔딩플래그, 18
                ELSEIF CFLAG:주회:방치일수 > 15
                    PRINTL 저 멀리서 당신을 바라보는 닥터의 모습이 보인다.
                    PRINTW 닥터는 당신에게 불만이 있는 듯 하다.
                ENDIF
            ENDIF
        CASE 90 ;테티스 ;etc1 = 테티스 연모이상 연애커맨드 카운트
            SIF !GETBIT(CFLAG:주회:특수출력유무, 3) && EXP:주회:연애COM경험2 >= 20
                SETBIT CFLAG:MASTER:엔딩플래그, 26
        CASE 106 ;오드리
            SIF CFLAG:주회:이벤트카운트1 == 4 ;etc1 = 오드리 더치 이벤트 일수
                SETBIT CFLAG:MASTER:엔딩플래그, 23
        CASE 108 ;더치 걸
            IF CFLAG:주회:엔딩조건카운트1 > 15 ;etc1 = 더치걸 엔딩 3 카운트
                SETBIT CFLAG:MASTER:엔딩플래그, 9
            ELSEIF CFLAG:주회:엔딩조건카운트1 > 10
                PRINTL 더치 걸의 상태가 어딘가 이상하다.
                PRINTW 이대로 두면 돌이킬 수 없는 일이 벌어질 것 같다.
            ENDIF
        CASE 118 ;LRL
            IF GETBIT(CFLAG:주회:특수출력조건, 3)
                SELECTCASE CFLAG:주회:방치일수
                    CASE 4
                        PRINTL 평소처럼 힘없이 쓰러져 있는 LRL
                        PRINTW 그런 LRL을 보며 당신은 오늘따라 무언가 불길한 느낌을 받았다.
                    CASE 5
                        SETBIT CFLAG:MASTER:엔딩플래그, 27
                ENDSELECT
            ENDIF
        CASE 132 ;유미
            IF GETBIT(CFLAG:주회:특수출력조건, 3) ;etc1 = 유미 엔딩 3 카운트
                SELECTCASE CFLAG:주회:방치일수
                    CASE 12
                        PRINTL 아침에 일어나 스마트폰을 열어보니 유미에게서 메세지가 와 있었다.
                        PRINTL 「사령관님, 많이... 바쁘신가요?」
                        PRINTL 「별 일은 아니에요. 그, 그냥... 요즘 저희 얼굴 본지가 좀 된 것 같아서...」
                        PRINTW 「질척거리는 것 같다면 죄송해요...」
                    CASE 13
                        PRINTL 오늘 아침에 유미에게서 메세지가 와 있었다.
                        PRINTL 「사령관님, 혹시... 지난번에 제가 보낸 메세지는 확인하셨나요?」
                        PRINTL 「확인하셨다면... 제 얼굴 좀 보러 와주시면... 안 될까요?」
                        PRINTW 「사령관님이 바쁘시다면 어쩔 수 없지만...」
                    CASE 14
                        PRINTL 「사령관님, 보고싶어요.
                        PRINTL 사령관님이 없는 저에게 내일이 올 수 있을까요?」
                        PRINTW 출처를 알 수 없는 이에게서 메세지가 와 있다.
                    CASE IS > 14
                        SETBIT CFLAG:MASTER:엔딩플래그, 16
                ENDSELECT
            ELSEIF GETBIT(CFLAG:주회:특수출력조건, 4) ;etc2 = 유미 엔딩 4 카운트
                SELECTCASE CFLAG:주회:방치일수
                    CASE 12
                        PRINTL 아침에 일어나 스마트폰을 열어보니 유미에게서 메세지가 와 있다.
                        PRINTL 「사령관님? 어디 있어요? 어디 있어요? 어디 있어요?」
                        PRINTL 「뭐 해요? 저 잊어버리신 거 아니죠?」
                        PRINTL 「저를 이렇게 만들어 버리고 영영 떠나가신 건 아니죠?」
                        PRINTW 당신은 화면을 껐다.
                    CASE 13
                        PRINTL 아침에 유미에게서 메세지가 와 있었다.
                        PRINTL 「깨진 유리창 이론이라고 알아요?」
                        PRINTL 「이미 깨진 유리창은 점점 더 깨지기 쉬워지는 거.」
                        PRINTW 「이쯤 되면 사령관님도 알아차리셨겠죠?」
                    CASE 14
                        PRINTL 「어차피 안 보고 계시잖아요.」
                        PRINTL 「저는 신경도 안 쓰시잖아요.」
                        PRINTL 「이미 늦었어요.」
                        PRINTW 출처를 알 수 없는 이에게서 메세지가 와 있었다.
                    CASE IS > 14
                        SETBIT CFLAG:MASTER:엔딩플래그, 17
                ENDSELECT
            ENDIF
        CASE 138 ;베로니카
            SELECTCASE TALENT:주회:실종 ;etc2 = 베로니카 실종 일수
                CASE 1
                    PRINTL 당신은 그녀를 찾기 위해 인근을 돌아다녔다.
                    PRINTL 그녀는 눈에 띄는 복장을 했기 때문에 주변 사람들을 탐문하면 찾을 수 있을지도 모른다.
                    PRINTW 악명높은 코헤이의 전투수녀 복장을 하고 있으니 누가 해코지는 할 리가 없다... 라고 당신은 계속 생각했다.
                CASE 2
                    PRINTL 집 근처의 사람들에게 베로니카에 대해 물었다.
                    PRINTL 「그 수녀님 말입니까? 그러고 보니 최근 본 적이 없군요.」
                    PRINTW 당신은 주변을 샅샅이 돌아다녔지만, 별 소득 없이 돌아갔다.
                CASE 3
                    PRINTL 집 근처의 사람들에게 베로니카에 대해 물었다.
                    PRINTL 「그 언니 말이죠? 옆 동네에 놀러 갔다가 봤어요!
                    PRINTL 빵집 앞에 서성이고 있었어요.
                    PRINTL 배가 고팠던 걸까요?」
                    PRINTW 당신은 주변을 샅샅이 돌아다녔지만, 별 소득 없이 돌아갔다.
                CASE 4
                    PRINTL 당신은 베로니카를 찾기 위해 지역신문에 광고를 냈다.
                    PRINTL 「제 가게에 빵 도둑이 들었던 날이 있었는데, 아마 근처에 있던 그 수녀님이 때려잡긴 했습니다.
                    PRINTL 사례를 할까 했는데 그전에 도둑이 훔친 빵을 먹어 치우곤 도망가 버렸지요.
                    PRINTL 행색이 딱해서 신고하진 않았습니다.」
                    PRINTW 당신은 그 빵집 주변을 돌아다녔지만, 별 소득 없이 돌아갔다.
                CASE 5
                    PRINTL 인근의 다른 지역을 돌아다녔다.
                    PRINTL 「그 수녀복 아가씨 말인가.
                    PRINTL 그저께 밤에 골목길에서 본 것 같네, 울면서 누군가를 계속 찾는 것 같았는데 그냥 미친 여자인지 알았어.」
                    PRINTL 주정뱅이로 보이는 남자가 말했다.
                    PRINTW 한동안 골목 근처를 돌아다녔지만 찾을 수 없었다.
                CASE 6
                    PRINTL 지역신문에 올린 광고를 보고 연락이 왔다.
                    PRINTL 「우리 애를 데리러 놀이터에 갔다가 애랑 놀고 있는 걸 봤어요.
                    PRINTL 소꿉놀이를 하던 중에 난데없이 흙을 퍼먹길래 급하게 애를 데리고 나갔죠.
                    PRINTL 근데 찾으면 사례금 주나요?」
                    PRINTW 애엄마로 보이는 여자가 말했다.
                CASE 7
                    PRINTL 당신은 전에 들은 놀이터를 찾아가 탐문했다.「그 누나 자주 봤어! 아침에 보면 미끄럼틀 안에서 자고 있었어. 사탕을 주면 되게 좋아해! 히히, 예쁘긴 한데 냄새나~」
                    PRINTL 코를 막는 시늉을 하는 아이가 말했다.
                    PRINTW 당신은 놀이터 주변을 한동안 돌아다녔지만, 찾을 수 없었다.",
                CASE 8
                    PRINTL 지역신문에 올린 광고를 보고 연락이 왔다.
                    PRINTL 「공원 벤치에서 비둘기에게 먹이를 주고 있는데, 갑자기 달려와 비둘기들이랑 같이 모이를 먹더군. 너무 충격적이라 잊히질 않아...」
                    PRINTL 노인으로 보이는 사람이 말했다.
                    PRINTW 당신은 공원 주변을 한동안 돌아다녔지만, 베로니카를 찾을 수 없었다.
                CASE 9
                    PRINTL 지역신문에 올린 광고를 보고 연락이 왔다.
                    PRINTL 「코믹 행사에서 봤어요!
                    PRINTL 수녀 코스프레를 하는 줄 알고 사람들이 막 사진을 찍고 있었는데, 무시하고 가판대에 핫도그를 사는 걸 봤어요.
                    PRINTL 그런데 알고 보니 돈이 없어 가게주인에게 혼나고 있었어요.
                    PRINTW 손에 핫도그 하나 쥐고 눈물만 뚝뚝 흘리는 걸 보니 불쌍해서 그냥 주더군요.」
                CASE 10
                    PRINTL 그 행사가 있었다던 도시를 방문했다.
                    PRINTL 탐문하기에 지나치게 넓은 도시라 눈앞이 깜깜해졌다.
                    PRINTW 당신은 전단을 뿌리고 사람들에게 말을 걸어 봤지만 결정적인 단서는 나오지 않았다.
                    PRINTL
                    PRINTW 휴식 커맨드로 이어서 진행 가능합니다.
            ENDSELECT
        CASE 171 ;뽀끄루
            IF CFLAG:주회:엔딩조건카운트1 >= 30
                SETBIT CFLAG:MASTER:엔딩플래그, 30
            ELSEIF CFLAG:주회:엔딩조건카운트1 > 0 && TALENT:주회:가수 == 1
                CALL CONCERT_D_DAY(주회, 30 - CFLAG:주회:엔딩조건카운트1)
            ENDIF
    ENDSELECT
NEXT

SIF CFLAG:MASTER:엔딩플래그
    CALL ENDING(-1)
RETURN 1
;============================================================================================
;함수명: ENDING_OBEY_FLAG(ARG)
;마지막 수정: v1.2
;종의 징표를 통한 복종 생성 및 엔딩 플래그 설정
;ARG = 인게임 캐릭터번호
;============================================================================================
@ENDING_OBEY_FLAG(ARG)

TARGET = ARG
TALENT:굴복 = 2 ;복종

SETBIT CFLAG:MASTER:엔딩플래그, 1

PRINTFORMW %타겟은% 어색하기 짝이없는 미소를 지으며 순응했다.
CFLAG:엔딩플래그 = -1

CALL ENDING(TARGET)
;============================================================================================
;함수명: Classify_EndingFlag
;마지막 수정: v0.9
;CFLAG:MASTER:엔딩플래그의 bit 값을 개별 엔딩플래그 값으로 변환 후 MASTER의 해당 값 초기화
;--------------------------------------------------------------------------------------------
;범용구상은 통과 (19~21)
;변환 실행시
;TARGET = 해당 엔딩의 캐릭터값
;RESULT = 해당 엔딩의 CFLAG:MASTER:엔딩플래그 bit 위치값
;============================================================================================
@Classify_EndingFlag
#DIM 비트값

;bit 1값은 약혼반지 관련으로 ENDING_MARRY_FLAG에서 처리됨
FOR 비트값, 1, 64
    IF GETBIT(CFLAG:MASTER:엔딩플래그, 비트값)
        SELECTCASE 비트값
            CASE 1 ;약혼반지 공용, TARGET 값도 이미 처리됨
            CASE 19, 20, 21 ;범용구상
                TARGET = MASTER
                RETURN 0
            CASE 2 ;세이렌 연모 폐기
                TARGET = GETCHARA(89)
                CFLAG:TARGET:엔딩플래그 = 3
            CASE 3, 8 ;리제
                TARGET = GETCHARA(7)
                IF 비트값 == 3
                    CFLAG:TARGET:엔딩플래그 = 3
                ELSEIF 비트값 == 8
                    CFLAG:TARGET:엔딩플래그 = 5
                ENDIF
            CASE 4 ;켈베로스 정신붕괴 폐기
                TARGET = GETCHARA(113)
                CFLAG:TARGET:엔딩플래그 = 3
            CASE 5 ;소완
                TARGET = GETCHARA(12)
                CFLAG:TARGET:엔딩플래그 = 3
            CASE 6, 10 ;메이
                TARGET = GETCHARA(51)
                IF 비트값 == 6 ;연모 정신쇠약 폐기
                    CFLAG:TARGET:엔딩플래그 = 3
                ELSEIF 비트값 == 10 ;사랑 출격경험 50 이상 폐기
                    CFLAG:TARGET:엔딩플래그 = 4
                ENDIF
            CASE 7 ;모모 연모 판매
                TARGET = GETCHARA(123)
                CFLAG:TARGET:엔딩플래그 = 3
            CASE 9 ;더치 걸
                TARGET = GETCHARA(108)
                CFLAG:TARGET:엔딩플래그 = 3
            CASE 11 ;마리
                TARGET = GETCHARA(21)
                CFLAG:TARGET:엔딩플래그 = 3
            CASE 12 ;레오나
                TARGET = GETCHARA(31)
                CFLAG:TARGET:엔딩플래그 = 3
            CASE 13 ;발키리
                TARGET = GETCHARA(32)
                CFLAG:TARGET:엔딩플래그 = 3
            CASE 14 ;팬텀
                TARGET = GETCHARA(73)
                CFLAG:TARGET:엔딩플래그 = 4
            CASE 15, 22 ;에밀리
                TARGET = GETCHARA(68)
                IF 비트값 == 15
                    CFLAG:TARGET:엔딩플래그 = 4
                ELSEIF 비트값 == 22 ;연애시작 선택지
                    CFLAG:TARGET:엔딩플래그 = 5
                ENDIF
            CASE 16, 17 ;유미
                TARGET = GETCHARA(132)
                IF 비트값 == 16
                    CFLAG:TARGET:엔딩플래그 = 3
                ELSEIF 비트값 == 17
                    CFLAG:TARGET:엔딩플래그 = 4
                ENDIF
            CASE 18 ;닥터
                TARGET = GETCHARA(76)
                CFLAG:TARGET:엔딩플래그 = 3
            CASE 23 ;오드리
                TARGET = GETCHARA(106)
                CFLAG:TARGET:엔딩플래그 = 3
            CASE 24 ;베로니카 사랑 혼인 이별
                TARGET = GETCHARA(138)
                CFLAG:TARGET:엔딩플래그 = 3
            CASE 25 ;네오딤
                TARGET = GETCHARA(71)
                CFLAG:TARGET:엔딩플래그 = 3
            CASE 26 ;테티스 사이드엔딩(번호 임의설정)
                TARGET = GETCHARA(90)
                CFLAG:TARGET:엔딩플래그 = 3
            CASE 27 ;LRL (번호 임의설정)
                TARGET = GETCHARA(118)
                CFLAG:TARGET:엔딩플래그 = 3
            CASE 28 ;브라우니 (번호 임의설정)
                TARGET = GETCHARA(25)
                CFLAG:TARGET:엔딩플래그 = 3
            CASE 29, 30 ;뽀끄루 (번호 임의설정)
                TARGET = GETCHARA(171)
                IF 비트값 == 29 ;정붕엔딩
                    CFLAG:TARGET:엔딩플래그 = 3
                ELSEIF 비트값 == 30 ;가수미니게임
                    ;ENDING A
                    IF EXP:TARGET:가창경험 >= 300 && (EXP:TARGET:연주경험 >= 150 || EXP:TARGET:퍼포먼스경험 >= 150)
                        CFLAG:TARGET:엔딩플래그 = 41
                    ;ENDING C
                    ELSEIF EXP:TARGET:퍼포먼스경험 >= 300 && (EXP:TARGET:가창경험 >= 150 || EXP:TARGET:연주경험 >= 150)
                        CFLAG:TARGET:엔딩플래그 = 43
                    ;ENDING B
                    ELSEIF EXP:TARGET:연주경험 >= 300 && (EXP:TARGET:가창경험 >= 150 || EXP:TARGET:퍼포먼스경험 >= 150)
                        CFLAG:TARGET:엔딩플래그 = 42
                    ;ENDING D
                    ELSE
                        CFLAG:TARGET:엔딩플래그 = 44
                    ENDIF
                ENDIF
            CASEELSE
                THROW 미확인 엔딩 번호 {비트값}
        ENDSELECT
        CLEARBIT CFLAG:MASTER:엔딩플래그, 비트값
        RETURN 비트값
    ENDIF
NEXT
;이상상황 처리용
PRINTL 이 문구가 보인다면
PRINTV CFLAG:MASTER:엔딩플래그
PRINTW 라는 ENDING에 문제가 생긴 것이니 개발자에게 문의하세요

RETURN -1
;============================================================================================
;함수명: ENDING_CHARA_Goodbye
;마지막 수정: v0.9
;원본 api/momo2 대응
;엔딩의 결과가 캐릭터와의 강제이별
;============================================================================================
@ENDING_CHARA_Goodbye

IF GROUPMATCH(NO:TARGET, 7,68,118,123,126)
    CALL Goodbye_CHARA(TARGET, 4)
    TARGET = 0
ELSE
    THROW 상정하지 않은 엔딩시 강제이별 대상 %CALLNAME:TARGET%, ENDING{CFLAG:TARGET:엔딩플래그}
ENDIF
;============================================================================================
;함수명: Ending_Result
;마지막 수정: v1.20
;엔딩 결과물 처리
;--------------------------------------------------------------------------------------------
;상정한 처리 조건
;1. 개별 엔딩 구상이 있고, 이를 처리 완료했을 때
;   이 경우 CFLAG:TARGET:엔딩플래그 초기화 전에 호출됨
;2. @ENDING을 들어왔는데 CFLAG:MASTER:엔딩플래그 값이 없을 때
;   TURNEND에서는 해당 값이 있어야 @ENDING을 들어오기 때문에, 상당히 특수한 경우일듯 함
;개별 구상 파일에서 처리하기 까다로우므로 화상 처리도 일단 여기서.
;--------------------------------------------------------------------------------------------
;변수 쓰임새 현황
;LOCAL (= exc) 1:게임오버 3:해당캐릭터삭제
;LOCAL:1 1일때 루프를 한 번더 돎. 정확한 역할은 모름
;그림번호 @ILLUST 함수에 사용
;============================================================================================
@Ending_Result
#DIM 그림번호
VARSET LOCAL
그림번호 = 0
;LOCAL = exc, LOCAL:1 = 1일때 원판에서 추가처리
SELECTCASE NO:TARGET
    CASE 7 ;리제
        IF CFLAG:엔딩플래그 == 1
            ;그림번호 = 3
        ELSEIF CFLAG:엔딩플래그 == 2
            ;그림번호 = 10
        ELSEIF CFLAG:엔딩플래그 == 3
            그림번호 = 6
            LOCAL = 1 ;exc = 1
        ELSEIF CFLAG:엔딩플래그 == 4 ;리제 히든 엔딩
            ;그림번호 = 11
            FLAG:히든메시지 = TARGET ;exc = 2
        ELSEIF CFLAG:엔딩플래그 == 5 ;리제 엔딩 No. 4
            ;그림번호 = 12
            LOCAL = 3 ;exc = 3
        ENDIF
    CASE 12 ;소완
        IF CFLAG:엔딩플래그 == 3 ;소완 배드 엔딩
            LOCAL = 1 ;exc = 1
        ELSEIF CFLAG:엔딩플래그 == 1
            ;그림번호 = 13
        ENDIF
    CASE 21 ;마리
        SIF CFLAG:엔딩플래그 == 3
            LOCAL = 1 ;exc = 1
    CASE 25 ;브라우니
        SIF CFLAG:엔딩플래그 == 3
            LOCAL = 3
    CASE 31, 32 ;레오나, 발키리
        SIF CFLAG:엔딩플래그 == 3
            TALENT:혼인 = 1 ;exc = 4
    CASE 51 ;메이
        IF CFLAG:엔딩플래그 == 1
            ;그림번호 = 1
        ELSEIF CFLAG:엔딩플래그 == 2
            ;그림번호 = 2
        ELSEIF CFLAG:엔딩플래그 == 4
            ;그림번호 = 9
            LOCAL:1 = 1
        ENDIF
    CASE 68 ;에밀리
        IF CFLAG:엔딩플래그 == 2
            LOCAL = 1 ;exc = 1
        ELSEIF GROUPMATCH(CFLAG:엔딩플래그, 4,5)
            LOCAL = 3 ;exc = 3
        ENDIF
    CASE 71 ;네오딤
        SIF CFLAG:엔딩플래그 == 3
            CALL RESET_CHARA(71) ;exc = 9
    CASE 73 ;팬텀
        SIF CFLAG:엔딩플래그 == 4
            TALENT:혼인 = 1 ;exc = 5
    CASE 76 ;닥터
        SIF CFLAG:엔딩플래그 == 3
            LOCAL = 3 ;exc = 3
    ;CASE 89 ;세이렌 엔딩플래그 1: 그림번호 4, 엔딩플래그 2: 그림번호 5
    CASE 90 ;테티스
        IF CFLAG:엔딩플래그 == 3
            SETBIT CFLAG:주회:특수출력유무, 3
        ENDIF
    CASE 106 ;오드리
        SIF CFLAG:엔딩플래그 == 3
            CFLAG:이벤트카운트1 = 5 ;exc = 7, etc1= 더치 걸 이벤트 일수
    CASE 108 ;더치걸
        SIF CFLAG:엔딩플래그 == 3
            LOCAL = 1 ;exc = 1
    CASE 118 ;LRL
        SIF GROUPMATCH(CFLAG:엔딩플래그, 2,3)
            LOCAL = 3
    CASE 123 ;모모
        IF CFLAG:엔딩플래그 == 1
            ;그림번호 = 7
        ELSEIF CFLAG:엔딩플래그 == 2
            ;그림번호 = 8
            LOCAL = 3 ;exc = 3
        ENDIF
    CASE 126 ;아자젤
        SIF CFLAG:엔딩플래그 == 2
            LOCAL = 3 ;exc = 3
    CASE 132 ;유미
        SIF GROUPMATCH(CFLAG:엔딩플래그, 3,4)
            TALENT:의존증 = 1 ;exc = 6
    CASE 138 ;베로니카
        IF CFLAG:엔딩플래그 == 1
            ;그림번호 = 14
        ELSEIF CFLAG:엔딩플래그 == 2
            ;그림번호 = 15
            LOCAL:1 = 1
            TALENT:실종 = 1 ;etc2 = 베로니카 실종 일수 ;exc = 8
        ELSEIF CFLAG:엔딩플래그 == 3
            ;순차 처리이기에 개인 메시지에서 따로 처리함
            ;여행
                ; 그림번호 = 16
                ; LOCAL:1 = 1
            ; 달콤한꿈
                ; 그림번호 = 17
                ; LOCAL:1 = 1
        ENDIF 
ENDSELECT

;화상 처리
SIF CFLAG:TARGET:개별그림유무
    CALL ILLUST(TARGET, "ENDING", 그림번호)

SIF FLAG:히든메시지 ;exc == 2
    CALL IS_DREAMING

;그림은 보여주고 가야하니 이곳에
IF LOCAL == 1
    CALL GAMEOVER
ELSEIF LOCAL == 3 ;exc == 3
    CALL ENDING_CHARA_Goodbye
ENDIF
RETURN 0
;============================================================================================
;함수명 ENDING_COMMON
;마지막 수정: v0.9
;범용엔딩 처리
;--------------------------------------------------------------------------------------------
;처리 조건
;CFLAG:MASTER:엔딩플래그의 BIT값이 19~21일 때 범용엔딩 처리
;CFLAG:TARGET:엔딩플래그의 값을 개인 메시지에서 발견하지 못했을 때 범용엔딩 처리
;개인 엔딩을 발견하지 못해 처리한 경우 본 함수에서 해당 플래그의 초기화는 실시하지 않음
;============================================================================================
@ENDING_COMMON

FOR LOCAL, 19, 22
    IF GETBIT(CFLAG:MASTER:엔딩플래그, LOCAL)
        SELECTCASE LOCAL
            CASE 19 ;EndingA
                PRINTW 당신이 바이오로이드들과 함께 행복한 시간들을 보낸지 어연 오랜 시간이 지났다.
                PRINTW 평소 인간들의 행태에 대해 소문을 듣던 바이오로이드들은 겁을 먹었지만
                PRINTW 당신은 그런 아이들을 아낌없이 보살펴주며 따뜻하게 대해 주었다.
                PRINTW 어느덧 당신에 대한 소문은 돌고 돌아 사람들에게 전해졌으며
                PRINTW 누구나 할 것 없이 바이오로이드들의 선망의 대상이 되었다.
                PRINTW 누군가는 당신의 아낌없는 사랑에 대해 칭찬하고
                PRINTW 누군가는 당신의 행동을 이해할 수 없다며 미친 사람 취급했다.
                PRINTW 하지만 당신은 그런 소문들을 전혀 신경쓰지 않았다.
                PRINTW 중요한건 이 사랑스러운 아이들과 함께 지내는 시간이리라.
                PRINTW 당신은 오늘도 사랑하는 바이오로이드들과 함께
                PRINTW 새로운 아침을 맞이하고 있었다.
                PRINTL 공용 엔딩 No.1
                PRINTL 바이오로이드들의 이상향.
                PRINTPLAIN 당신은 [사랑꾼]을 얻었다.
                TALENT:MASTER:사랑꾼 = 1 ;exc = 1
            CASE 20 ;EndingB
                PRINTW 바이오로이드들을 사고 폐기하기를 반복하는 날들을 보내던 당신.
                PRINTW 그런 당신에 대한 소문은 어느덧 동네를 돌고 돌아 도시에 퍼지며
                PRINTW 곧 인터넷 기사와 뉴스, SNS를 통해 순식간에 퍼져나갔다.
                PRINTW 바이오로이드들과 그들을 사랑해주는 사람들은
                PRINTW 당신의 시선을 마주치기만 해도 두려움에 떨었다.
                PRINTW 사람들은 당신과 엮이지 않기 위해 어떻게든 거리를 두었으며
                PRINTW 당신의 집 앞에서는 바이오로이드들을 위한 인권시위가 끊임없이 펼쳐졌다.
                PRINTW 바이오로이드들을 학대하던 사람들조차 당신의 행동에 학을 떼며 혀를 차곤 했다.
                PRINTW 하지만 당신은 전혀 신경쓰지 않았다.
                PRINTW 바이오로이드는 사람이 아니다.
                PRINTW 바꿔 말하면 바이오로이드에게 무슨 짓을 하더라도 법에 걸리지 않는다.
                PRINTW 당신은 알수 없는 미소를 지으며
                PRINTW 오늘도 당신의 집에 들어올 바이오로이드를 눈으로 고르고 있었다.
                PRINTL 공용 엔딩 No.2
                PRINTL The only thing they fear is you.
                PRINTPLAIN 당신은 [냉혈한]을 얻었다.
                TALENT:MASTER:냉혈한 = 1; exc = 2
            CASE 21 ;EndingC
                PRINTW 땡전 한 푼 조차 가지지 못한채로 덧없는 인생을 시작한 당신은
                PRINTW 어느새 남들이 선망하는 큰 돈을 거머쥔 상류층이 되어 있었다.
                PRINTW 당신이 어떻게 막대한 부를 거머쥐게 되었는지
                PRINTW 수많은 소문이 돌기 시작했다.
                PRINTW 바이오로이드들을 부려 돈을 벌었다던가
                PRINTW 환상의 요리사들도 무릎을 꿇고 예를 표할 기적의 요리를 만들어 팔았다던가
                PRINTW 당신의 입김 한번에 회사의 생사를 가르는 대주주가 되었다던가
                PRINTW ...혹은 바이오로이드들을 팔아 돈을 벌었다던가.
                PRINTW 하지만 소문이 중요한가?
                PRINTW 어떻게 돈을 벌었던간에 당신의 지갑 안이 마를 날이 없다는건 사실이다.
                PRINTL 공용 엔딩 No.3
                PRINTL 미다스의 손.
                PRINTPLAIN 당신은 [대부호]를 얻었다.
                TALENT:MASTER:대부호 = 1; exc = 3
        ENDSELECT
        CLEARBIT CFLAG:MASTER:엔딩플래그, LOCAL
        WAITANYKEY
        RETURN 1
    ENDIF
NEXT

;개별 엔딩 메시지가 없던 경우의 처리
SELECTCASE CFLAG:TARGET:엔딩플래그
    CASE 1 ;약혼반지, 정상
        PRINTFORMW 당신이 %CALLNAME:TARGET%에게 약혼반지를 선물한 지 하루가 지났다.
        PRINTFORMW 어제 %CALLNAME:TARGET%의 모습은 참으로 장관이었다.
        PRINTW 울다가 웃다가 갑자기 껴안질 않나.
        PRINTW 요란한 그 모습에 나도 모르게 농담을 던졌더니,
        PRINTW 이런 때에는 분위기 파악 좀 하라며 화를 내기까지 했다.
        PRINTW 뭐, 이런 일도 저런 일도 있었지만
        PRINTW 서로 미소지으며 지낼 수 있다면 문제될 것이 없을 것이다.
        PRINTFORMW 그리 생각하고 있자, %타겟이% 자고있던 방에서 인기척이 느껴지기 시작했다.
        PRINTW 오늘은 또 어떤 일들이 있을까?
        PRINTFORMW %CALLNAME:TARGET%의 모습을 상상하자 저절로 입가에 미소가 걸렸다.
        PRINTW ...
        PRINTW ..
        PRINTW .
        PRINTL 공용 엔딩 No.A
        PRINTW 검은 머리가 파뿌리가 될 때까지.
    CASE 2 ;약혼반지, 정신붕괴
        PRINTFORMW 무엇인가 중요한 것을 잃어버린 %CALLNAME:TARGET%에게 당신이 약혼반지를 선물한 지 하루가 지났다.
        PRINTFORMW 어제 반지를 건네주고 난 이후, %타겟은% 어떤 반응도 하지 않게 되었다.
        PRINTW 어디서부터 잘못된 것일까, 고민해보아도 답은 나오지 않는다.
        PRINTW 반지를 건네어 준 것? 아니면 그 전?
        PRINTFORMW 그것도 아니라면 %CALLNAME:TARGET%를 상점에서 처음 보았던 나 자신인걸까?
        PRINTW 아무리 생각해보아도 답이 나오지 않던 당신은,
        PRINTW 고장난 로봇처럼 거듭 생각할 뿐이었다.
        PRINTW ...
        PRINTW ..
        PRINTW .
        PRINTL 공용 엔딩 No.B
        PRINTW 공허한 고뇌.
    CASE 3,4,5 ;캐릭터마다 다른 특수엔딩
        PRINTW 무슨 일이 있었는지는 알 수 없다.
        PRINTW 하지만, 누군가 이 세상을 만든 이가 이런 일을 벌인 것만은 분명하다.
        PRINTFORMW 당신은, %타겟과% 의미있는 시간을 보냈던 것 같지만,
        PRINTW 이 상황을 만들어 낸 누군가는 그건 알 바 아니라 생각한 것 같다.
        PRINTW 당신은 대략 정신이 멍해지고 말았다...
        PRINTW ...
        PRINTW ..
        PRINTW .
        PRINTL 공용 엔딩 No.C
        PRINTL 나는 누구 여긴 어디.
        PRINTPLAIN 당신은 [공허함]을 얻었다.
        WAIT
        PRINTW 농담이고, 아무래도 엔딩이 들어있지 않은 것 같다. 일어나서 인터넷에 올려보자.
    CASEELSE ;잘못 적었거나 6 이상인 수
        PRINTW 당신은 어디론가 알 수 없는 곳으로 떨어져버렸다.
        PRINT 당신의 눈 앞에 있는 종이에는 
        PRINTFORML %CALLNAME:TARGET%의 \@CFLAG:MASTER:엔딩플래그 ? 방 # 침실\@ {CFLAG:TARGET:엔딩플래그}호실
        PRINTW 이라는 문구가 쓰여 있다.
        PRINTW 이제 어쩌지...?
        PRINTW ...
        PRINTW ..
        PRINTW .
        PRINTW 공용 엔딩 No.???
        PRINTW 분홍 신 (아이유)
        PRINTW
        PRINTW ...아래를 보니 작은 글씨로 무엇인가 써 있다.
        PRINTW 위 주소를 개발자에게 남겨주세요. 버그입니다.
ENDSELECT
RETURN 1
;============================================================================================
;함수명: IS_DREAMING
;마지막 수정: v0.9
;나, 꿈꾸고 있니?
;(아마도) 극단적 엔딩의 마지막 만회기회 제공 역할
;v0.9 현재 리제 히든시 호출하고, 해당 경우 리제를 제외한 캐릭터가 전부 강제이별 처리됨
;============================================================================================
@IS_DREAMING

PRINTW 이건 꿈 일거야...
CALL ASK_M("잠에서 깬다.", 1, "현실을 받아들인다.", 1)
IF RESULT == 0 ;잠에서 깸
    FLAG:히든메시지 = 0
ELSE ;현실 받아들임
    FLAG:속박상태 = 1
    ;현 TARGET 제외 전부 삭제
    ;PICKUPCHARA 사용 가능?
    FOR LOCAL, 1, CHARANUM
        SIF NO:LOCAL == 7
            CONTINUE
        CALL Goodbye_CHARA(LOCAL, 3)
    NEXT
ENDIF
;============================================================================================
;함수명: GAMEOVER
;마지막 수정: v0.9
;게임 지속 불가능 처리
;세이브를 삭제하진 않는다(아직은)
;============================================================================================
@GAMEOVER
PRINTW ...
PRINTW ..
PRINT .
PRINTW ★★★당신은 사라졌습니다★★★
SETCOLOR 255,0,0
PRINTW ----------GAME OVER----------
RESETCOLOR
RESETDATA
CLEARLINE LINECOUNT
PRINTW 재구동하지 않을시 게임이 종료됩니다.
QUIT
