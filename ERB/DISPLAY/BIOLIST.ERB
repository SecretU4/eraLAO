;===========================================================
;파일명: BIOLIST.ERB
;바이오로이드 리스트 표시용
;===========================================================
;함수명: CURRENT_BIOLIST
;현재 특정 행동이 가능한 바이오로이드 표시
;ARGS = 호출한 조건
;반환값 = 선택한 캐릭터의 인게임 캐릭터번호
;===========================================================
@CURRENT_BIOLIST(ARGS="")
#DIM CSV_ID
#DIM COUNT_CHECK,255

WHILE 1
    PRINTL
    DRAWLINEFORM =

    ;목록의 상단에 표시되는 제목
    ALIGNMENT CENTER
    SELECTCASE ARGS
        CASE "대상변경", "DEBUG"
            PRINTL 대상으로 할 바이오로이드를 선택하세요.
        CASE "이별"
            PRINTL 목록에서 선택해주세요.
        CASE "출격"
            PRINTL 출격 시킬 바이오로이드를 선택해주세요.
        CASE "선물"
            PRINTL 아이템을 선물할 바이오로이드를 선택해주세요.
        CASE "", "상태보기"
        CASEELSE
            PRINTL 상정하지 않은 경우의 수
    ENDSELECT
    ALIGNMENT LEFT

    PRINTL
    DRAWLINE
    COUNT = 0
    FOR LOCAL, 1, CHARANUM
        CSV_ID = NO:LOCAL
        IF ITEM:(ITEM_BIO(CSV_ID)) > 0
            PRINTFORM [{COUNT+1}] 
            ;노랑, 별
            IF TALENT:LOCAL:혼인 == 1
                SETCOLOR 255,255,102
                LOCALS = %NAME:LOCAL%
                LOCALS += UNICODE(0x2606)
            ;보라
            ELSEIF TALENT:LOCAL:혼인 == 2 ;입양
                SETCOLOR 204,153,255
                LOCALS = %NAME:LOCAL%
            ;짙은 분홍, 하트
            ELSEIF TALENT:LOCAL:사랑 == 2 ;사랑
                SETCOLOR 255,102,255
                LOCALS = %NAME:LOCAL%
                LOCALS += UNICODE(0x2665)
            ;옅은 분홍
            ELSEIF TALENT:LOCAL:사랑 == 1 ;연모
                SETCOLOR 255,204,255
                LOCALS = %NAME:LOCAL%
            ELSE
                LOCALS = %NAME:LOCAL%
            ENDIF
            PRINTFORM %LOCALS% 
            RESETCOLOR
            IF TALENT:MASTER:관심법
                PRINTFORM 호감도 : {CFLAG:LOCAL:호감도} 
                PRINTFORM 스트레스 : {CFLAG:LOCAL:스트레스} 
                PRINTFORM 공포 : {CFLAG:LOCAL:공포} 
            ENDIF
            PRINTFORML
            ALIGNMENT RIGHT
            CALL SHOW_SPECIAL_STATUS(LOCAL)
            PRINTL
            ALIGNMENT LEFT
            COUNT_CHECK:COUNT = LOCAL
            COUNT ++
        ENDIF
    NEXT
    SIF !COUNT
        PRINTL 표시 가능한 바이오로이드가 없습니다.

    PRINTL
    DRAWLINE
    PRINTL
    SIF !GROUPMATCH(ARGS, "상태보기","DEBUG")
        PRINT [0] 
    SELECTCASE ARGS
        CASE "대상변경"
            PRINTL 아무도 선택하지 않는다.
        CASE "상태보기","DEBUG"
            PRINTL [999] 당신
            PRINTL [0] 돌아가기
        CASEELSE
            PRINTL 돌아가기
    ENDSELECT
    PRINTL
    DRAWLINEFORM =
    INPUT

    IF RESULT == 0
        RETURN -1
    ELSEIF (ARGS == "상태보기" || ARGS == "DEBUG") && RESULT == 999
        RETURN 0
    ELSEIF (RESULT-1) > COUNT
        PRINTW 제시된 숫자에서 선택해주세요.
    ELSE
        LOCAL = COUNT_CHECK:(RESULT-1)
        ;각 기능별 선택불가 처리
        SELECTCASE ARGS
            CASE "출격"
                IF CFLAG:LOCAL:출격중
                    PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")% 이미 출격중인 대상입니다.
                ELSE
                    RETURN LOCAL
                ENDIF                
            CASE "상태보기", "DEBUG"
                RETURN LOCAL
            CASEELSE
                IF CFLAG:LOCAL:출격중
                    PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")% 현재 출격중입니다.
                ELSE
                    RETURN LOCAL
                ENDIF
        ENDSELECT
    ENDIF
WEND
;===========================================================
;함수명: BIO_INFO
;캐릭터 정보 표출
;ARG = 인게임 캐릭터번호, ARG:1 = 1일때 상점화면
;===========================================================
@BIO_INFO(ARG,ARG:1=0)

PRINTFORM No.{NO:ARG} %NAME:ARG% 
CALL SHOW_SPECIAL_STATUS(ARG)
PRINTL

IF ARG != 0
    PRINTFORMLC 소속: %STR:((TALENT:ARG:13) + 300)%
    PRINTFORM 등급: 
    SELECTCASE TALENT:ARG:등급
        CASE 1
            PRINTLC B
        CASE 2
            PRINTLC A
        CASE 3
            PRINTLC S
        CASE 4
            PRINTLC SS
        CASEELSE
            PRINTLC ???
    ENDSELECT
    PRINTFORM 분류: 
    SELECTCASE TALENT:ARG:분류
        CASE 1
            PRINTLC 경장
        CASE 2
            PRINTLC 기동
        CASE 3
            PRINTLC 중장
        CASEELSE
            PRINTLC ???
    ENDSELECT
    PRINTFORM 체형: 
    SELECTCASE TALENT:ARG:체형
        CASE 0
            PRINTPLAIN [작은체구]
        CASE 1
            PRINTPLAIN [보통체구]
        CASE 2
            PRINTPLAIN [큰체구]
    ENDSELECT
    PRINTLC
ELSE
    PRINTFORM 체형: 
    SELECTCASE TALENT:ARG:나이
        CASE 0
            PRINTPLAIN [소년] 
        CASE 1
            PRINTPLAIN [청년] 
        CASE 2
            PRINTPLAIN [중년] 
    ENDSELECT
ENDIF
PRINTL
IF ARG:1
    CALL ILLUST(ARG, NO:ARG, "BIOSHOP")
    SIF CSTR:ARG:캐릭터소개 != ""
        PRINTSL %CSTR:ARG:캐릭터소개%
ENDIF
PRINTL
DRAWLINE
PRINTL
LOCALS = 능력: 
FOR LOCAL, 21, 100
    ;해당 능력을 가지고 있지 않은 경우 패스
    SIF !TALENT:ARG:LOCAL
        CONTINUE

    CALL TALENTNAME_EX(LOCAL, TALENT:ARG:LOCAL)
    ;CSV에 존재하지 않는 경우 패스
    IF RESULTS == ""
        CONTINUE
    ;실종 등 SPECIAL_STATUS에서 표시되는 것
    ELSEIF GROUPMATCH(LOCAL, 79)
        CONTINUE
    ELSE
        LOCALS += @"[%RESULTS%]"
    ENDIF
NEXT
PRINTSL LOCALS
PRINTL
DRAWLINE
PRINTL
IF ARG != 0
    IF TALENT:MASTER:관심법
        PRINTFORM 호감도 : {CFLAG:ARG:호감도} 
        PRINTFORM 공포 : {CFLAG:ARG:공포} 
        PRINTFORML 스트레스 : {CFLAG:ARG:스트레스} 
    ELSE
        PRINTFORM 호감도 : ??? 
        PRINTFORM 공포 : ??? 
        PRINTFORML 스트레스 : ??? 
    ENDIF
    PRINTL
    PRINTFORM 출격한 횟수 : {EXP:ARG:출격경험} 
    PRINTFORM 전투 레벨 : {ABL:ARG:전투lv} 
    PRINTFORML 출격으로 번 돈 : {CFLAG:ARG:출격수입} 
ELSE
    PRINTFORM 지휘 레벨 : {ABL:ARG:전투lv} 
    PRINTFORML 출격시킨 횟수 : {EXP:ARG:출격경험} 
ENDIF
PRINTL
PRINTFORM 요리한 횟수 : {EXP:ARG:요리경험} 
PRINTFORM 요리 레벨 : {ABL:ARG:조리lv} 
PRINTFORMLC 요리로 번 돈 : {CFLAG:ARG:요리수입}
PRINTL
DRAWLINE
;개별 캐릭터 특수 수치
SELECTCASE NO:ARG ;추후 타 캐릭터에서도 사용 가능해지면 상단에 항목 추가바람
    CASE 0
        PRINTFORM 요리로 번 총 액수 : {FLAG:총요리수입} 
        PRINTFORML 출격으로 번 총 액수 : {FLAG:총출격수입}
        PRINTL
        PRINTFORM 바이오로이드를 판 횟수 : {FLAG:바이오판매횟수} 
        PRINTFORML 바이오로이드를 버린 횟수 : {FLAG:바이오폐기횟수}
        PRINTL
        PRINTFORML 고백 받은 횟수 : {FLAG:고백받은인수}
    CASE 91 ;슬레이프니르(땡컨)
        PRINTFORML 가창경험 : {CFLAG:ARG:etc3}
        ;PRINTFORML 가창경험 : {EXP:ARG:가창경험}
    CASE 126 ;아자젤
        PRINTFORML 설교 횟수 : {CFLAG:ARG:etc1}
ENDSELECT
PRINTL
DRAWLINE

;===========================================================
;함수명: SEE_BIOSTATUS
;캐릭터 정보 표출 목록 메인화면
;대상 = 선택한 캐릭터 인게임 ID
;===========================================================
@SEE_BIOSTATUS
#DIM 대상
대상 = 0
WHILE 대상 != -1
    CALL CURRENT_BIOLIST("상태보기")
    대상 = RESULT
    IF 대상 != -1
        CALL BIO_INFO(대상)
        PRINTW 엔터 키를 누르면 전으로 돌아갑니다
    ENDIF
WEND
RETURN 1

;===========================================================
;함수명: SHOW_SPECIAL_STATUS
;출격중, 실종 등 캐릭터 특수상태 표출
;ARG = 선택한 캐릭터 인게임 ID
;===========================================================
@SHOW_SPECIAL_STATUS(ARG)
;출격중일때
IF CFLAG:ARG:출격중
    ;하늘색
    SETCOLOR 102,255,255
    PRINTPLAIN [출격중] 
    RESETCOLOR
;실종상태일 때
ELSEIF TALENT:ARG:실종
    ;회색
    SETCOLOR 125,125,125
    PRINTPLAIN [실종]
    RESETCOLOR
ENDIF
;===========================================================
;함수명: TALENTNAME_EX(TALENT_NO, TALENT_STAT)
;특정 능력치값일 때 표시해야 하는 능력 문자열 반환
;===========================================================
@TALENTNAME_EX(TALENT_NO, TALENT_STAT)
#DIM TALENT_NO ;TALENT.CSV 내 번호
#DIM TALENT_STAT ;해당 능력치의 현재값

;-1 < 0 > 1 형 TALENT
IF GROUPMATCH(TALENT_NO, 23,30,62,70)
    SELECTCASE TALENT_NO
        CASE 23 ;얀데레
            IF TALENT_STAT == -1
                LOCALS '= "[약한 얀데레]"
            ELSEIF TALENT_STAT == 1
                LOCALS '= "[얀데레]"
            ELSE
                LOCALS = [약한 얀데레]/[얀데레]
            ENDIF
        CASE 30 ;나이속성
            IF TALENT_STAT == -1
                LOCALS '= "[동생]"
            ELSEIF TALENT_STAT == 1
                LOCALS '= "[누님]"
            ELSE
                LOCALS = [동생]/[누님]
            ENDIF
        CASE 62 ;긍정도
            IF TALENT_STAT == -1
                LOCALS '= "[비관적]"
            ELSEIF TALENT_STAT == 1
                LOCALS '= "[긍정적]"
            ELSE
                LOCALS = [비관적]/[긍정적]
            ENDIF
        CASE 70 ;지능
            IF TALENT_STAT == -1
                LOCALS '= "[무식]"
            ELSEIF TALENT_STAT == 1
                LOCALS '= "[지식]"
            ELSE
                LOCALS = [무식]/[지식]
            ENDIF
    ENDSELECT
;0 > 1 > 2 형 TALENT
ELSEIF GROUPMATCH(TALENT_NO, 25,26,72)
    SELECTCASE TALENT_NO
        CASE 25 ;사랑
            IF TALENT_STAT == 1
                LOCALS '= "[연모]"
            ELSEIF TALENT_STAT == 2
                LOCALS '= "[사랑]"
            ELSE
                LOCALS = [연모]/[사랑]
            ENDIF
        CASE 26 ;혼인
            IF TALENT_STAT == 1
                LOCALS '= "[혼인]"
            ELSEIF TALENT_STAT == 2
                LOCALS '= "[입양]"
            ELSE
                LOCALS = [혼인]/[입양]
            ENDIF
        CASE 72 ;정신쇠약
            IF TALENT_STAT == 1
                LOCALS '= "[정신쇠약]"
            ELSEIF TALENT_STAT == 2
                LOCALS '= "[정신붕괴]"
            ELSE
                LOCALS = [정신쇠약]/[정신붕괴]
            ENDIF
    ENDSELECT
ELSE
    LOCALS '= TALENTNAME:TALENT_NO
ENDIF

RETURN LOCALS