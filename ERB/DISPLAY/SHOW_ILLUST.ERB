;ARG = 인게임 캐릭터번호, IMG_no = 일러스트 일련번호
;ARGS = 호출 위치, LOCALS = 출력할 파일명
@ILLUST(ARG, ARGS, IMG_no=0)
#DIM CSV_ID
#DIM DYNAMIC IMG_no
#DIMS IMAGE_EXT, 3 = "webp","png","jpg"
CSV_ID = NO:ARG

SIF ARG == -1
    RETURN 0

LOCALS '= @"%ARGS%_{CSV_ID}_"

SELECTCASE ARGS
    CASE "DateStart"
        SELECTCASE CSV_ID
            CASE 7 ;리제, 그림번호 = 2
                SIF CSTR:ARG:1 == "Confession"
                    RETURN 0
                LOCALS '= FACE_DATESTART(ARG)
            CASE 12 ;소완, 그림번호 = 3
                SIF CSTR:ARG:1 == "Confession"
                    RETURN 0
                LOCALS '= FACE_DATESTART(ARG)
            CASEELSE             
            ;메이 Break - 그림번호 1 / fLove - 그림번호 4
            ;베로니카 Confession - 그림번호 6 / First - 그림번호 5
                LOCALS += CSTR:ARG:1
        ENDSELECT
    CASE "DateCom"
        SELECTCASE CSV_ID
            CASE 51 ;메이
                ;메이 파후파후, 원본은 연모만. ;그림번호 = 1
                SIF SELECTCOM == 18 && !TALENT:ARG:짜증 && !TALENT:ARG:정신쇠약 && TALENT:ARG:사랑
                    LOCALS += "pahu"
            CASE 132 ;유미
                ;유미 술배 ;그림번호 = 3
                SIF SELECTCOM == 13 && GROUPMATCH(CSTR:ARG:1, "High","Affection")
                    LOCALS += "tummy"
            CASE 138 ;베로니카
            ;베로니카 연애커맨드 그림은 해당 캐릭터 메시지 코드 안에서 처리
            ;그림번호 10: 배설기관, 11: 뚝배기
                SELECTCASE IMG_no
                    ;베로니카 연애커맨드 일부 경우, 임의 번호 산정
                    CASE 10 ;배설기관
                        LOCALS += "Penance_1"
                    CASE 11 ;불경한
                        LOCALS += "_2"
                ENDSELECT
        ENDSELECT
    CASE "Rest"
            ;REST_1
            ;그림번호 1: 1021, 2: 1025, 3: 1004, 4:1027, 5: 1030, 6: 1033, 7: 1041
            ;REST_2
            ;그림번호 1: 1009, 2: 1010, 3: 1011
            LOCALS '= @"Rest_{IMG_no}"
    CASE "Cook"
        SIF IMG_no ;메이 실패/발키리 5분
            LOCALS += "_1"
    CASE "ENDING"
        ;그림번호 1: 52_1, 2: 51_2, 3: 7_1, 4: 89_1, 5:89_2, 6:7_3-1/7_3-2,
        ;7: 123_1, 8: 123_2, 9: 51_4, 10: 7_2, 11: 7_5, 12: 7_4
        ;13: 12_1, 14: 138_1, 15: 138_2, 16: 138_3-1, 17: 138_3-2
        LOCALS += CFLAG:ARG:엔딩플래그
        SELECTCASE IMG_no
            CASE 6 ;리제 3번 엔딩플래그
                LOCALS:1 '= @"%LOCALS%-2"
                LOCALS += "-1"
            CASE 16 ;베로니카 3번 엔딩플래그 1번째
                LOCALS += "-1"
            CASE 17 ;베로니카 3번 엔딩플래그 2번째
                LOCALS += "-2"
        ENDSELECT
    CASE "BIOSHOP"
        LOCALS '= @"BIO_IMG_{CSV_ID}"
        ;IMAGE_EXT의 확장자 지원 (최대 3개)
        LOCAL = 0
        REPEAT 3
            GCREATEFROMFILE CSV_ID, @"SHOP_SELECTION/{CSV_ID}.%IMAGE_EXT:COUNT%"
            IF !RESULT
                GCREATED CSV_ID ;인게임 내 존재 여부 판별
                LOCAL = RESULT
            ELSE ;첫생성
                LOCAL = RESULT
            ENDIF

            SIF LOCAL
                BREAK
        REND
        SPRITECREATE @"%LOCALS%", CSV_ID
ENDSELECT

;출력해야 하는 이미지가 없을 때
IF LOCALS == "" || !SPRITECREATED(LOCALS)
    DEBUGPRINTFORML %LOCALS%에 해당하는 이미지를 발견하지 못함 - 조건 %ARGS%, 캐릭터 %NAME:ARG%, 일련번호 {IMG_no}
    RETURN 0
ENDIF

IF IMG_no
    CALL PRINT_ILLUST(LOCALS, 20)
ELSE
    CALL PRINT_ILLUST(LOCALS)
ENDIF

;출력해야 하는 이미지가 2개일 때
IF LOCALS:1 != ""
    IF ARGS == "ENDING"
        SELECTCASE IMG_no
            CASE 6 ;리제 3번 엔딩플래그 
                PRINTBUTTON @"[계속]", ""
                INPUTS
        ENDSELECT
    ENDIF
    CALL PRINT_ILLUST(LOCALS:1)
ENDIF
RETURN 1

;연애 시작시 얼굴 표정 처리(존재시)
;ARG = 인게임 캐릭터번호
@FACE_DATESTART(ARG)
#FUNCTIONS
LOCALS '= @"Face_{NO:ARG}_"
IF TALENT:ARG:정신쇠약 == 2
    LOCALS += "break"
ELSEIF TALENT:ARG:정신쇠약 == 1
    LOCALS += "weak"
ELSEIF TALENT:ARG:짜증
    LOCALS += "annoyed"
ELSEIF TALENT:ARG:혼인 == 1
    LOCALS += "marryed"
ELSEIF CFLAG:ARG:공포 > 149
    LOCALS += "fear"
ELSEIF TALENT:ARG:사랑 ;원판은 연모시에만
    LOCALS += "affection"
ELSEIF CFLAG:ARG:호감도 > 499
    LOCALS += "high"
ELSE
    LOCALS += "low"
ENDIF

RETURNF LOCALS

;ARGS = 출력 대상 파일 문자열
@PRINT_ILLUST(ARGS, 배율=30)
#DIMS PRINT_HTML
#DIM 배율

PRINT_HTML = 
PRINT_HTML += @"<img src='%ARGS%' height='{배율}00'>"
REPEAT 배율
    PRINT_HTML += "<br>"
REND
HTML_PRINT PRINT_HTML

;그림이 있는 바이오로이드의 플래그 켜기
;ARG = CSV 캐릭터번호
@SET_ILLUST_FLAG(ARG)
#FUNCTION

SIF GROUPMATCH(ARG, 7,12,32,51,68,89,113,123,132,138)
    RETURNF 1

RETURNF 0
